					
#Область о // Сформировать табличные документы:

Функция СформироватьТабличныеДокументы(ПараметрыОтчета) Экспорт
	
	ТабличныеДокументы = Новый Структура;
	ТабличныеДокументы.Вставить("РеестрУведомлений",ПолучитьРеестрУведомлений(ПараметрыОтчета));
	
	Возврат ТабличныеДокументы;
	
КонецФункции

Функция ПолучитьРеестрУведомлений(ПараметрыОтчета)
	
	ИсходныеДанные = РеестрУведомленийИсходныеДанные(ПараметрыОтчета);
	
	РеестрУведомлений = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Печать_РеестрУведомлений");
	
	ОбластьОтчета = Макет.ПолучитьОбласть("ЗаголовокОтчета");
	РеестрУведомлений.Вывести(ОбластьОтчета);
	
	ТекущийМесяц = ИсходныеДанные.Дата1;  
	ЗарплатаПредыдущегоМесяца = 0;
	Пока ТекущийМесяц <= ИсходныеДанные.Дата2 Цикл

		ОбластьОтчета = Макет.ПолучитьОбласть("СтрокаОтчета");

		ПоискСтруктура = Новый Структура;
		ПоискСтруктура.Вставить("Период",ТекущийМесяц);
		НалоговыеУведомленияМассив = ИсходныеДанные.НалоговыеУведомленияТаблица.НайтиСтроки(ПоискСтруктура);
		КодПериода = РегистрыСведений.инкНалоговыеУведомления.ПолучитьКодПериода(ТекущийМесяц);
		
		ОбластьОтчета.Параметры.ПериодМесяц 	= ВРег(Формат(ТекущийМесяц,"ДФ=ММММ"));
		ОбластьОтчета.Параметры.АвансКод 		= КодПериода.ПерваяПоловинаМесяца;
		ОбластьОтчета.Параметры.ЗарплатаКод 	= КодПериода.КодСФР;
		ОбластьОтчета.Параметры.СФРКод 			= КодПериода.КодСФР;
		
		Если НалоговыеУведомленияМассив.Количество() > 0 Тогда
			ОбластьОтчета.Параметры.АвансСумма 		= НалоговыеУведомленияМассив[0].АвансНДФЛСумма;
			ОбластьОтчета.Параметры.ЗарплатаСумма	= ЗарплатаПредыдущегоМесяца;
			ОбластьОтчета.Параметры.СФРСумма		= НалоговыеУведомленияМассив[0].ВзносыСумма; 
			
			ЗарплатаПредыдущегоМесяца = НалоговыеУведомленияМассив[0].НДФЛСумма;
		Иначе
			ЗарплатаПредыдущегоМесяца = 0;
		КонецЕсли;
		
		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц,1);
		РеестрУведомлений.Вывести(ОбластьОтчета);
		
	КонецЦикла;
	
	ОбластьОтчета = Макет.ПолучитьОбласть("ИтогОтчета");
	Для каждого ИтогиСтрока Из ИсходныеДанные.НалоговыеУведомленияИтогиТаблица Цикл
		ОбластьОтчета.Параметры.АвансСумма 		= ИтогиСтрока.АвансНДФЛСумма;
		ОбластьОтчета.Параметры.ЗарплатаСумма	= ИтогиСтрока.НДФЛСумма;
		ОбластьОтчета.Параметры.СФРСумма		= ИтогиСтрока.ВзносыСумма;
	КонецЦикла;
	РеестрУведомлений.Вывести(ОбластьОтчета);
	
	Возврат РеестрУведомлений;
	
КонецФункции

#КонецОбласти	

#Область о // Исходные данные:

Функция РеестрУведомленийИсходныеДанные(ПараметрыОтчета)
	
	ИсходныеДанные = Новый Структура;                       
	ИсходныеДанные.Вставить("Организация",ПараметрыОтчета.Организация);
	ИсходныеДанные.Вставить("Дата1",НачалоМесяца(ПараметрыОтчета.ПериодУдержания.ДатаНачала));
	ИсходныеДанные.Вставить("Дата2",НачалоМесяца(ПараметрыОтчета.ПериодУдержания.ДатаОкончания));
	ИсходныеДанные.Вставить("НалоговыеУведомленияТаблица",РегистрыСведений.инкНалоговыеУведомления.ПолучитьНалоговыеУведомленияТаблица(ИсходныеДанные));
	ИсходныеДанные.Вставить("НалоговыеУведомленияИтогиТаблица",ПолучитьНалоговыеУведомленияИтогиТаблица(ИсходныеДанные));
	
	Возврат ИсходныеДанные;
	
КонецФункции

Функция ПолучитьНалоговыеУведомленияИтогиТаблица(ИсходныеДанные)

	ИтогиТаблица = ИсходныеДанные.НалоговыеУведомленияТаблица.Скопировать();
	ИтогиТаблица.Свернуть("","АвансНДФЛСумма,ВзносыСумма,НДФЛСумма");
	
	Возврат ИтогиТаблица;
	
КонецФункции

#КонецОбласти
					

																					  
