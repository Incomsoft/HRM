#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем ПериодОтчета Экспорт; //	Стандартный период - Период отчета;
Перем Организация Экспорт; 	// 	Ссылка - Ссылка на справочник "Организация";
Перем Подразделение Экспорт;//	Ссылка - Ссылка на справочник "Подразделение";  

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область о // Инициализация:

// Процедура - Инициализация
//
Процедура Инициализация() Экспорт

	ПериодОтчета	= Неопределено;
	Организация 	= Неопределено;
	Подразделение 	= Неопределено;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область о // Исходные данные:

Функция ПолучитьИсходныеДанные()
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("Дата1",ПериодОтчета.ДатаНачала);
	ИсходныеДанные.Вставить("Дата2",ПериодОтчета.ДатаОкончания);
	ИсходныеДанные.Вставить("НачисленияТаблица",ПолучитьНачисленияТаблица(ИсходныеДанные));
	ИсходныеДанные.Вставить("ВычетыТаблица",ПолучитьВычетыТаблица(ИсходныеДанные));
	ИсходныеДанные.Вставить("СотрудникиТаблица",ПолучитьСотрудникиТаблица(ИсходныеДанные));
	
	
	Возврат ИсходныеДанные;
	
КонецФункции    

Функция ПолучитьСотрудникиТаблица(ИсходныеДанные)
	
	СотрудникиТаблица = ИсходныеДанные.НачисленияТаблица.Скопировать();
	СотрудникиТаблица.Свернуть("Сотрудник,СотрудникФИОСокращенное");
	
	Возврат СотрудникиТаблица;
	
КонецФункции

Функция ПолучитьНачисленияТаблица(ИсходныеДанные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНачисленияОбороты.Сотрудник КАК Сотрудник,
		|	инкНачисленияОбороты.Сотрудник.ФИОСокращенное КАК СотрудникФИОСокращенное,
		|	инкНачисленияОбороты.Начисление КАК Начисление,
		|	инкНачисленияОбороты.Начисление.ДоходНДФЛ КАК ДоходНДФЛ,
		|	инкНачисленияОбороты.ВходитВФОТ КАК ВходитВФОТ,
		|	инкНачисленияОбороты.Начисление.Налог КАК Налог,
		|	инкНачисленияОбороты.СуммаНачисленияОборот + инкНачисленияОбороты.РайонныйКоэффициентОборот + инкНачисленияОбороты.СевернаяНадбавкаОборот КАК СуммаНачисления
		|ИЗ
		|	РегистрНакопления.инкНачисления.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение) КАК инкНачисленияОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Начисление";
	
	Запрос.УстановитьПараметр("Дата1", ИсходныеДанные.Дата1);
	Запрос.УстановитьПараметр("Дата2", ИсходныеДанные.Дата2);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Организация = &Организация","Истина");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Подразделение = &Подразделение","Истина");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

Функция ПолучитьВычетыТаблица(ИсходныеДанные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкВычетыНДФЛОбороты.Сотрудник КАК Сотрудник,
		|	инкВычетыНДФЛОбороты.ВычетНДФЛ КАК ВычетНДФЛ,
		|	инкВычетыНДФЛОбороты.ГруппаВычетаНДФЛ КАК ГруппаВычетаНДФЛ,
		|	инкВычетыНДФЛОбороты.СуммаВычетаОборот КАК СуммаВычета
		|ИЗ
		|	РегистрНакопления.инкВычетыНДФЛ.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение) КАК инкВычетыНДФЛОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	Запрос.УстановитьПараметр("Дата1", ИсходныеДанные.Дата1);
	Запрос.УстановитьПараметр("Дата2", ИсходныеДанные.Дата2);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Организация = &Организация","Истина");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Подразделение = &Подразделение","Истина");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

#КонецОбласти

#Область о // Сформировать отчет:

Функция СформироватьТабличныйДокументРасшифровка6НДФЛ() Экспорт
	
	Расшифровка6НДФЛ = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("Расшифролвка6НДФЛ"); 
	
	ИсходныеДанные = ПолучитьИсходныеДанные();

	ОбластьОтчет = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	Расшифровка6НДФЛ.Вывести(ОбластьОтчет);
	
	ОбластьИтоги = Макет.ПолучитьОбласть("ИтогТаблицы");
	
	Для каждого СотрудникСтрока Из ИсходныеДанные.СотрудникиТаблица Цикл

		ОбластьОтчет = Макет.ПолучитьОбласть("СтрокаТаблицы");
		
		СотрудникПоиск = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		НачисленияМассив = ИсходныеДанные.НачисленияТаблица.НайтиСтроки(СотрудникПоиск);
		
		ОбластьОтчет.Параметры.Сотрудник      =	СотрудникСтрока.СотрудникФИОСокращенное;						
		ОбластьОтчет.Параметры.НачисленоВсего = ПолучитьНачисленоВсего(НачисленияМассив);
		ОбластьОтчет.Параметры.НеоблагаемыеВыплаты = ПолучитьНеоблагаемыеВыплаты(НачисленияМассив);
		ОбластьОтчет.Параметры.СуммаНачисленногоДохода = ОбластьОтчет.Параметры.НачисленоВсего
		                                               - ОбластьОтчет.Параметры.НеоблагаемыеВыплаты;
		ОбластьОтчет.Параметры.Дивиденды = ПолучитьДивиденды(НачисленияМассив);

		ОбластьОтчет.Параметры.СтандартныеВычеты = ПолучитьСтандартныеВычеты(ИсходныеДанные.ВычетыТаблица,СотрудникСтрока.Сотрудник);
		ОбластьОтчет.Параметры.МатПомощь = ПолучитьМатПомощь(ИсходныеДанные.ВычетыТаблица,СотрудникСтрока.Сотрудник);
		ОбластьОтчет.Параметры.ИмущВычет = ПолучитьИмущВычет(ИсходныеДанные.ВычетыТаблица,СотрудникСтрока.Сотрудник);
		
		ОбластьОтчет.Параметры.Вычеты = ОбластьОтчет.Параметры.СтандартныеВычеты
		                              + ОбластьОтчет.Параметры.МатПомощь
									  + ОбластьОтчет.Параметры.ИмущВычет;

		ОбластьОтчет.Параметры.ОблагаемыйДоход = ОбластьОтчет.Параметры.СуммаНачисленногоДохода
		                                       - ОбластьОтчет.Параметры.Вычеты;
		ОбластьОтчет.Параметры.ИсчисленныйНалог = Окр(ОбластьОтчет.Параметры.ОблагаемыйДоход*0.13,0);
		ОбластьОтчет.Параметры.НалогСДивидендов = Окр(ОбластьОтчет.Параметры.Дивиденды*0.13,0);
		
		Расшифровка6НДФЛ.Вывести(ОбластьОтчет);
		ДобавитьВИтоги(ОбластьИтоги,ОбластьОтчет);
		
	КонецЦикла;
	
	Расшифровка6НДФЛ.Вывести(ОбластьИтоги);
	
	Возврат Расшифровка6НДФЛ;
	
КонецФункции      

Процедура ДобавитьВИтоги(ОбластьИтоги,ОбластьОтчет)
	
	ДобваитьВИтогиПараметр("НачисленоВсего",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("НеоблагаемыеВыплаты",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("СуммаНачисленногоДохода",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("Дивиденды",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("СтандартныеВычеты",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("МатПомощь",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("ИмущВычет",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("Вычеты",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("ОблагаемыйДоход",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("ИсчисленныйНалог",ОбластьИтоги,ОбластьОтчет);
	ДобваитьВИтогиПараметр("НалогСДивидендов",ОбластьИтоги,ОбластьОтчет);
	
КонецПроцедуры     

Процедура ДобваитьВИтогиПараметр(ИмяПараметра,ОбластьИтоги,ОбластьОтчет)
	
	Если ОбластьИтоги.Параметры[ИмяПараметра] = Неопределено Тогда
		ОбластьИтоги.Параметры[ИмяПараметра] = 0;
	КонецЕсли;
	ОбластьИтоги.Параметры[ИмяПараметра] = ОбластьИтоги.Параметры[ИмяПараметра]
	                                     + ОбластьОтчет.Параметры[ИмяПараметра]; 
	
КонецПроцедуры     

Функция ПолучитьИмущВычет(ВычетыТаблица,Сотрудник)
	
	РасчетСумма = 0;
	
	ВычетыПоиск = Новый Структура;
	ВычетыПоиск.Вставить("Сотрудник",Сотрудник);
	ВычетыПоиск.Вставить("ГруппаВычетаНДФЛ",Перечисления.инкГруппыВычетовПоНДФЛ.Имущественные);
	ВычетыМассив = ВычетыТаблица.НайтиСтроки(ВычетыПоиск);
	
	Для каждого ВычетЭлемент Из ВычетыМассив Цикл
		
		РасчетСумма = РасчетСумма + ВычетЭлемент.СуммаВычета;

	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

Функция ПолучитьМатПомощь(ВычетыТаблица,Сотрудник)
	
	РасчетСумма = 0;
	
	СотрудникПоиск = Новый Структура("Сотрудник",Сотрудник);
	ВычетыМассив = ВычетыТаблица.НайтиСтроки(СотрудникПоиск);
	
	Для каждого ВычетЭлемент Из ВычетыМассив Цикл
		
		Если ВычетЭлемент.ВычетНДФЛ = Справочники.инкВычетыНДФЛ.Код503 Тогда
			РасчетСумма = РасчетСумма + ВычетЭлемент.СуммаВычета;
		КонецЕсли;

	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

Функция ПолучитьСтандартныеВычеты(ВычетыТаблица,Сотрудник)
	
	РасчетСумма = 0;
	
	СотрудникПоиск = Новый Структура("Сотрудник",Сотрудник);
	ВычетыМассив = ВычетыТаблица.НайтиСтроки(СотрудникПоиск);
	
	Для каждого ВычетЭлемент Из ВычетыМассив Цикл
		
		Если ВычетЭлемент.ГруппаВычетаНДФЛ = Перечисления.инкГруппыВычетовПоНДФЛ.Стандартные ИЛИ
			 ВычетЭлемент.ГруппаВычетаНДФЛ = Перечисления.инкГруппыВычетовПоНДФЛ.СтандартныеНаДетей
		Тогда
			РасчетСумма = РасчетСумма + ВычетЭлемент.СуммаВычета;
		КонецЕсли;

	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

Функция ПолучитьДивиденды(НачисленияМассив)
	
	РасчетСумма = 0;    
	
	Для каждого НачислениеЭлемент Из НачисленияМассив Цикл
		
		Если НачислениеЭлемент.ДоходНДФЛ = Справочники.инкДоходНДФЛ.Код1010 Тогда
			РасчетСумма = РасчетСумма + НачислениеЭлемент.СуммаНачисления;	
		КонецЕсли;
		    
	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

Функция ПолучитьНеоблагаемыеВыплаты(НачисленияМассив)
	
	РасчетСумма = 0;    
	
	Для каждого НачислениеЭлемент Из НачисленияМассив Цикл
		
		Если НЕ НачислениеЭлемент.Налог ИЛИ НЕ НачислениеЭлемент.ВходитВФОТ Тогда
			РасчетСумма = РасчетСумма + НачислениеЭлемент.СуммаНачисления;	
		КонецЕсли;
		    
	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

Функция ПолучитьНачисленоВсего(НачисленияМассив)
	
	РасчетСумма = 0;    
	
	Для каждого НачислениеЭлемент Из НачисленияМассив Цикл
		РасчетСумма = РасчетСумма + НачислениеЭлемент.СуммаНачисления;    
	КонецЦикла;
	
	Возврат РасчетСумма;
		
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли




