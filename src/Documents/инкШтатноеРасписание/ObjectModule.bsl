
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область о // Первичное заполнение документа:

Процедура ПервичноеЗаполнениеШтатноеРасписание() Экспорт

    ЭтотОбъект.ШтатноеРасписание.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Должность КАК Должность,
		|	СУММА(1) КАК КоличествоШтатныхЕдиниц,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Оклад КАК ТарифнаяСтавкаОклад
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение) КАК инкКадроваяИсторияСотрудниковСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Подразделение,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Должность,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Оклад";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Если ЗначениеЗаполнено(ЭтотОбъект.Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Подразделение = &Подразделение","Истина");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ШтатноеРасписаниеСтрока = ЭтотОбъект.ШтатноеРасписание.Добавить();	
		ЗаполнитьЗначенияСвойств(ШтатноеРасписаниеСтрока,ВыборкаДетальныеЗаписи);
		ШтатноеРасписаниеСтрока.РайонныйКоэффициент = ШтатноеРасписаниеСтрока.ТарифнаяСтавкаОклад * ЭтотОбъект.ПроцентРайонныйКоэффициент / 100;
		ШтатноеРасписаниеСтрока.СевернаяНадбавка = ШтатноеРасписаниеСтрока.ТарифнаяСтавкаОклад * ЭтотОбъект.ПроцентСевернаяНадбавка / 100;
		ШтатноеРасписаниеСтрока.Надбавка = ШтатноеРасписаниеСтрока.ТарифнаяСтавкаОклад * ЭтотОбъект.ПроцентНадбавка / 100;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти       

#Область о // Печать:

Функция СформироватьШтатноеРасписание() Экспорт
	
	ПФ_MXL_Т3 = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("ПФ_MXL_Т3"); 
	
	ОбластьОтчет = Макет.ПолучитьОбласть("Шапка");
	ОбластьОтчет.Параметры.ОрганизацияКодПоОКПО = Организация.ОКПО;
	ОбластьОтчет.Параметры.ОрганизацияНаименованиеПолное = Организация.Наименование; 
	ОбластьОтчет.Параметры.НомерДокумента 	= Номер; 
	ОбластьОтчет.Параметры.ДатаДокумента 	= Дата;
	ОбластьОтчет.Параметры.ДатаАктуальности	= ТекущаяДата();     
	ОбластьОтчет.Параметры.КоличествоСтавок = ШтатноеРасписание.Итог("КоличествоШтатныхЕдиниц");
	ПФ_MXL_Т3.Вывести(ОбластьОтчет);    
	
	ОбластьОтчет = Макет.ПолучитьОбласть("ШапкаЛиста");
	ПФ_MXL_Т3.Вывести(ОбластьОтчет);    
	
	Для каждого ШтатноеРасписаниеСтрока Из ШтатноеРасписание Цикл
		
		ОбластьОтчет = Макет.ПолучитьОбласть("Строка"); 
		ЗаполнитьЗначенияСвойств(ОбластьОтчет.Параметры,ШтатноеРасписаниеСтрока);
		ОбластьОтчет.Параметры.ПодразделениеКод = ШтатноеРасписаниеСтрока.Подразделение.Код;
		
		ПФ_MXL_Т3.Вывести(ОбластьОтчет);    
		
	КонецЦикла;
	
	ОбластьОтчет = Макет.ПолучитьОбласть("Подвал"); 
	ЗаполнитьИтогиПодвала("КоличествоШтатныхЕдиниц",ОбластьОтчет,ШтатноеРасписание);
	ЗаполнитьИтогиПодвала("ТарифнаяСтавкаОклад",ОбластьОтчет,ШтатноеРасписание);
	ЗаполнитьИтогиПодвала("РайонныйКоэффициент",ОбластьОтчет,ШтатноеРасписание);
	ЗаполнитьИтогиПодвала("СевернаяНадбавка",ОбластьОтчет,ШтатноеРасписание);
	ЗаполнитьИтогиПодвала("Надбавка",ОбластьОтчет,ШтатноеРасписание);
	ЗаполнитьИтогиПодвала("Всего",ОбластьОтчет,ШтатноеРасписание);
	ПФ_MXL_Т3.Вывести(ОбластьОтчет);    
	
	ОбластьОтчет = Макет.ПолучитьОбласть("Подписи");          
	
	РуководительКадровойСлужбы = Справочники.инкОрганизации.КадровыеДанныеКадровика(Организация);
	ГлавныйБухгалтер           = Справочники.инкОрганизации.КадровыеДанныеБухгалтера(Организация);
	
	ОбластьОтчет.Параметры.ДолжностьРуководителяКадровойСлужбы	= РуководительКадровойСлужбы.Должность;	
	ОбластьОтчет.Параметры.РуководительКадровойСлужбыРасшифровкаПодписи	= РуководительКадровойСлужбы.ФИОСокращенное;
	ОбластьОтчет.Параметры.ГлавныйБухгалтерРасшифровкаПодписи = ГлавныйБухгалтер.ФИОСокращенное;
	
	ПФ_MXL_Т3.Вывести(ОбластьОтчет);   
	
	Возврат ПФ_MXL_Т3;
	
КонецФункции    

Процедура ЗаполнитьИтогиПодвала(ИмяПараметра,ОбластьОтчет,ШтатноеРасписаниеТаблица)

	ОбластьОтчет.Параметры[ИмяПараметра] = ШтатноеРасписаниеТаблица.Итог(ИмяПараметра);
	
КонецПроцедуры

#КонецОбласти       

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
