
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначениямиПоУмолчанию();
	
КонецПроцедуры  

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы Тогда
		СохранитьДанныеЗаПериод();	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеЗаПериод(ПриИзмененииВида = Ложь)
	
	ВидУдержанияВПериодеСсылка = ВидУдержанияВПериоде;
	Если ПриИзмененииВида Тогда
		
		Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
			ВидУдержанияВПериодеСсылка = Перечисления.инкВидУдержанияВПериоде.ВтораяПоловинаМесяца;
		Иначе
			ВидУдержанияВПериодеСсылка = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца;
		КонецЕсли;
		
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.инкНалоговыеУведомления.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ПериодЗаписиРегистра;
	МенеджерЗаписи.Организация 		= Организация;
	МенеджерЗаписи.ВидУдержанияВПериоде = ВидУдержанияВПериодеСсылка;
	//
	МенеджерЗаписи.АвансНДФЛСумма	= АвансНДФЛСумма;
	МенеджерЗаписи.АвансНДФЛСНСумма	= АвансНДФЛСНСумма;
	МенеджерЗаписи.АвансНДФЛСОсновногоДоходаСумма = АвансНДФЛСОсновногоДоходаСумма;
	//
	МенеджерЗаписи.НДФЛСумма  		= НДФЛСумма;
	МенеджерЗаписи.НДФЛСНСумма		= НДФЛСНСумма;
	МенеджерЗаписи.НДФЛСОсновногоДоходаСумма = НДФЛСОсновногоДоходаСумма;
	//
	МенеджерЗаписи.ВзносыСумма 		= ВзносыСумма;      

	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры     

&НаСервере
Процедура ЗаполнитьЗначениямиПоУмолчанию()
	
	Организация = Справочники.инкОрганизации.ПолучитьТекущуюОрганизацию();
	
	ПериодУдержания.ДатаНачала   	= НачалоМесяца(ТекущаяДата());
	ПериодУдержания.ДатаОкончания	= КонецМесяца(ТекущаяДата());  
	
	ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца;  
	
	ЗагрузитьДанныеЗаПериод();
	
КонецПроцедуры                        

&НаСервере
Процедура ЗагрузитьДанныеЗаПериод()
	
	АвансНДФЛСумма					= 0;  
	АвансНДФЛСНСумма				= 0;
	АвансНДФЛСОсновногоДоходаСумма  = 0;
	//
	НДФЛСумма  					= 0;
	НДФЛСНСумма 				= 0;
	НДФЛСОсновногоДоходаСумма	= 0;
	//
	ВзносыСумма 				= 0;
	
	ПериодЗаписиРегистра = ПериодУдержания.ДатаНачала;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНалоговоеУведомлениеСрезПоследних.Период КАК Период,
		|	инкНалоговоеУведомлениеСрезПоследних.АвансНДФЛСумма КАК АвансНДФЛСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.АвансНДФЛСНСумма КАК АвансНДФЛСНСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.АвансНДФЛСОсновногоДоходаСумма КАК АвансНДФЛСОсновногоДоходаСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.НДФЛСумма КАК НДФЛСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.НДФЛСНСумма КАК НДФЛСНСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.НДФЛСОсновногоДоходаСумма КАК НДФЛСОсновногоДоходаСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.ВзносыСумма КАК ВзносыСумма
		|ИЗ
		|	РегистрСведений.инкНалоговыеУведомления.СрезПоследних(
		|			&ПериодОтчета,
		|			ВидУдержанияВПериоде = &ВидУдержанияВПериоде
		|				И Организация = &Организация) КАК инкНалоговоеУведомлениеСрезПоследних";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидУдержанияВПериоде", ВидУдержанияВПериоде);
	Запрос.УстановитьПараметр("ПериодОтчета", НачалоМесяца(ПериодУдержания.ДатаНачала));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		АвансНДФЛСумма	= ВыборкаДетальныеЗаписи.АвансНДФЛСумма; 
		АвансНДФЛСНСумма				= ВыборкаДетальныеЗаписи.АвансНДФЛСНСумма;
		АвансНДФЛСОсновногоДоходаСумма   = ВыборкаДетальныеЗаписи.АвансНДФЛСОсновногоДоходаСумма;
        //
		НДФЛСумма  = ВыборкаДетальныеЗаписи.НДФЛСумма;
		НДФЛСНСумма = ВыборкаДетальныеЗаписи.НДФЛСНСумма;
		НДФЛСОсновногоДоходаСумма = ВыборкаДетальныеЗаписи.НДФЛСОсновногоДоходаСумма;
		//
		ВзносыСумма = ВыборкаДетальныеЗаписи.ВзносыСумма;

	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеПоНалогу(Команда)
	
	ТекстВопроса = НСтр("ru = 'Внимание! Текущие данные перезапшутся. Продолжить?'");
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьДанныеПоНалогуЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры                           

&НаКлиенте
Процедура ЗагрузитьДанныеПоНалогуЗавершение(Результат, ИмяТабЧасти) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда

		ЗагрузитьДанныеПоНачисленномуНалогуЗаПериод();
		ЗагрузитьДанныеПоНачисленнымВзносамЗаПериод();  
		
		СохранитьДанныеЗаПериод();

	КонецЕсли;
	
КонецПроцедуры                                        

&НаСервере
Процедура ЗагрузитьДанныеПоНачисленнымВзносамЗаПериод()
	
	ВзносыСумма = 0;
	
	Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ВтораяПоловинаМесяца Тогда
    	Возврат;
	КонецЕсли;
	
	МенеджерРасчета = Обработки.инкРасчетВзносов.Создать();
	//
	МенеджерРасчета.Инициализация();
	//
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Организация = Организация;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата1 = НачалоГода(ПериодУдержания.ДатаНачала);
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата2 = КонецМесяца(ПериодУдержания.ДатаОкончания);
	МенеджерРасчета.ДанныеДляРасчетаВзносов.СформироватьТабличныеДокументы = Ложь;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.ВидОтчета = "Свод";
	
	//
	МенеджерРасчета.ВыполнитьРасчетВзносов();
	//         
	
	Для каждого ВзносыСтрока Из МенеджерРасчета.СводныеДанные.Взносы.ВзносыСводТаблица_Месяц Цикл
		
		Если ВзносыСтрока.Период = НачалоМесяца(ПериодУдержания.ДатаНачала) Тогда
			ВзносыСумма = ВзносыСтрока.СФРИтого;	
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеПоНачисленномуНалогуЗаПериод()
	
	НДФЛСумма	= 0;
	НДФЛСНСумма = 0;   
	НДФЛСОсновногоДоходаСумма = 0;
	         
	АвансНДФЛСумма = 0;
	АвансНДФЛСНСумма = 0;
	АвансНДФЛСОсновногоДоходаСумма = 0;

	Дата1 = НачалоМесяца(ПериодУдержания.ДатаНачала);
	Дата2 = КонецМесяца(ПериодУдержания.ДатаОкончания);
	Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
		Дата1 = ДобавитьМесяц(НачалоМесяца(ПериодУдержания.ДатаНачала),-1);
		Дата2 = ДобавитьМесяц(КонецМесяца(ПериодУдержания.ДатаОкончания),-1);
	КонецЕсли;
	
	// Налог:
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНалогУдержанныйОбороты.Организация КАК Организация,
		|	инкНалогУдержанныйОбороты.СуммаНалогаОборот КАК СуммаНалогаОборот
		|ИЗ
		|	РегистрНакопления.инкНалогУдержанный.Обороты(&Дата1, &Дата2, Месяц, Организация = &Организация) КАК инкНалогУдержанныйОбороты";
	
	Запрос.УстановитьПараметр("Дата1", Дата1);
	Запрос.УстановитьПараметр("Дата2", Дата2);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
        	НДФЛСумма = ВыборкаДетальныеЗаписи.СуммаНалогаОборот;
		Иначе
			АвансНДФЛСумма = ВыборкаДетальныеЗаписи.СуммаНалогаОборот;
		КонецЕсли;	
		
	КонецЦикла;
	
	// Налог с северных:  
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНачисленияОбороты.СевернаяНадбавкаОборот + инкНачисленияОбороты.РайонныйКоэффициентОборот КАК НадбавкиОборот
		|ИЗ
		|	РегистрНакопления.инкНачисления.Обороты(&Дата1, &Дата2, , ) КАК инкНачисленияОбороты";
	
	Запрос.УстановитьПараметр("Дата1", Дата1);
	Запрос.УстановитьПараметр("Дата2", Дата2);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СуммаНалога = Окр(ВыборкаДетальныеЗаписи.НадбавкиОборот * 0.13);
		Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
        	НДФЛСНСумма = СуммаНалога; 
		Иначе
			АвансНДФЛСНСумма = СуммаНалога; 
		КонецЕсли;
		
	КонецЦикла;
		
	// Налог с основного дохода:
	Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
		НДФЛСОсновногоДоходаСумма = Окр(НДФЛСумма - НДФЛСНСумма);   
		Если НДФЛСОсновногоДоходаСумма < 0 Тогда
			НДФЛСОсновногоДоходаСумма = 0;
		КонецЕсли;
	Иначе
		АвансНДФЛСОсновногоДоходаСумма = Окр(АвансНДФЛСумма - АвансНДФЛСНСумма);   
		Если АвансНДФЛСОсновногоДоходаСумма < 0 Тогда
			АвансНДФЛСОсновногоДоходаСумма = 0;
		КонецЕсли;
	КонецЕсли;    
	
	// Корректировка на выплаченный аванс:
	Если ВидУдержанияВПериоде = Перечисления.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	инкНалоговыеУведомления.АвансНДФЛСНСумма КАК АвансНДФЛСНСумма,
			|	инкНалоговыеУведомления.АвансНДФЛСОсновногоДоходаСумма КАК АвансНДФЛСОсновногоДоходаСумма,
			|	инкНалоговыеУведомления.АвансНДФЛСумма КАК АвансНДФЛСумма
			|ИЗ
			|	РегистрСведений.инкНалоговыеУведомления КАК инкНалоговыеУведомления
			|ГДЕ
			|	инкНалоговыеУведомления.Период = &Период";
		
		Запрос.УстановитьПараметр("Период", НачалоМесяца(Дата1));
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НДФЛСумма 	= НДФЛСумма - ВыборкаДетальныеЗаписи.АвансНДФЛСумма;
			НДФЛСНСумма	= НДФЛСНСумма - ВыборкаДетальныеЗаписи.АвансНДФЛСНСумма;
			НДФЛСОсновногоДоходаСумма = НДФЛСОсновногоДоходаСумма - ВыборкаДетальныеЗаписи.АвансНДФЛСОсновногоДоходаСумма;
			
		КонецЦикла;
		
	КонецЕсли;    
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СохранитьДанныеЗаПериод();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация",Организация);
	ПараметрыОткрытия.Вставить("ПериодУдержания",ПериодУдержания);
	ПараметрыОткрытия.Вставить("ВидУдержания",ВидУдержанияВПериоде);

	ПараметрыОткрытия.Вставить("НалогСумма",0);
	ПараметрыОткрытия.Вставить("ВзносыСумма",0);
	ПараметрыОткрытия.Вставить("НДФЛСНСумма",0);
	ПараметрыОткрытия.Вставить("НДФЛСОсновногоДоходаСумма",0);
	
	Если ВидУдержанияВПериоде = ПредопределенноеЗначение("Перечисление.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца") Тогда
		ПараметрыОткрытия.ВзносыСумма	= ВзносыСумма; 
		ПараметрыОткрытия.НДФЛСНСумма	= НДФЛСНСумма;
		ПараметрыОткрытия.НДФЛСОсновногоДоходаСумма = НДФЛСОсновногоДоходаСумма;
	Иначе
		ПараметрыОткрытия.НДФЛСНСумма	= АвансНДФЛСНСумма;
		ПараметрыОткрытия.НДФЛСОсновногоДоходаСумма = АвансНДФЛСОсновногоДоходаСумма;
	КонецЕсли;
		
	ОткрытьФорму("Отчет.инкНалоговоеУведомление.Форма.ФормаНалоговыеУведомления",ПараметрыОткрытия,,Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидУдержанияПриИзменении(Элемент)
	
	СохранитьДанныеЗаПериод(Истина);
	ЗагрузитьДанныеЗаПериод();

КонецПроцедуры

&НаКлиенте
Процедура ПериодУдержанияПриИзменении(Элемент)

	ВидУдержанияВПериоде = ПредопределенноеЗначение("Перечисление.инкВидУдержанияВПериоде.ПерваяПоловинаМесяца");
	ЗагрузитьДанныеЗаПериод(); 

КонецПроцедуры

&НаКлиенте
Процедура СохранитьДанные(Команда)
	
	СохранитьДанныеЗаПериод();

КонецПроцедуры




