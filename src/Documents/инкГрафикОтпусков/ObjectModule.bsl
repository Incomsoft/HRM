
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПроверкаВозможностиЗаписи(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаВозможностиЗаписи(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкГрафикОтпусков.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.инкГрафикОтпусков КАК инкГрафикОтпусков
		|ГДЕ
		|	НЕ инкГрафикОтпусков.ПометкаУдаления
		|	И инкГрафикОтпусков.Ссылка <> &ТекущийДокументСсылка
		|	И инкГрафикОтпусков.Организация = &Организация";
	
	Запрос.УстановитьПараметр("ПериодДата", ЭтотОбъект.Период);
	Запрос.УстановитьПараметр("ПериодРегламентногоОтчета", ЭтотОбъект.Период);
	Запрос.УстановитьПараметр("ТекущийДокументСсылка", ЭтотОбъект.Ссылка);   
	Запрос.УстановитьПараметр("Организация", ЭтотОбъект.Организация);   
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
    Если РезультатЗапроса.Количество() > 0 Тогда
		
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("Найден такой же документ: "+РезультатЗапроса[0].Ссылка+" с таким же периодом отчета. Запись прервана.")
		
	КонецЕсли;
	
КонецПроцедуры

#Область о // Исходные данные:

Функция ПолучитьИсходныеДанныеДляНачальногоЗаполнения()

	ИсходныеДанные = Новый Структура;

	ИсходныеДанные.Вставить("Дата",КонецГода(ЭтотОбъект.Период));
	ИсходныеДанные.Вставить("Организация",ЭтотОбъект.Организация);
	Если ЗначениеЗаполнено(ЭтотОбъект.Подразделение) Тогда
		ИсходныеДанные.Вставить("Подразделения",ЭтотОбъект.Подразделение);
	КонецЕсли;	
	ИсходныеДанные.Вставить("СотрудникиТаблица",инкКадровыйУчетСервер.ПолучитьКадровыеДанныеСотрудников(ИсходныеДанные));
	                                            
	Возврат ИсходныеДанные;
	
КонецФункции 

#КонецОбласти

#Область о // Заполнение начальными данными:

Процедура ЗаполнитьНачальнымиДанными() Экспорт
	
	ИсходныеДанные = ПолучитьИсходныеДанныеДляНачальногоЗаполнения();

	ЭтотОбъект.Графики.Очистить();
	Для каждого СотрудникиСтрока Из ИсходныеДанные.СотрудникиТаблица Цикл
		
		ГрафикСтрока = ЭтотОбъект.Графики.Добавить();
		ЗаполнитьЗначенияСвойств(ГрафикСтрока,СотрудникиСтрока); 
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 

#Область о // Сформировать отчет:

Функция СформироватьТабличныйДокументТ7() Экспорт
	
	Т7ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьОбщийМакет("ПФ_MXL_Т7"); 
	
	РуководительДанные = Справочники.инкОрганизации.КадровыеДанныеРуководителя(Организация);
	КадровикДанные = Справочники.инкОрганизации.КадровыеДанныеКадровика(Организация);
	
	ОбластьОтчет = Макет.ПолучитьОбласть("Шапка");
	ОбластьОтчет.Параметры.ОрганизацияНаименованиеСокращенное = Организация.Наименование;
	ОбластьОтчет.Параметры.ОрганизацияКодПоОКПО	 = Организация.ОКПО;
	
	ОбластьОтчет.Параметры.ДолжностьРуководителя = РуководительДанные.Должность;
	ОбластьОтчет.Параметры.РуководительРасшифровкаПодписи = РуководительДанные.ФИОСокращенное;		
	
	ОбластьОтчет.Параметры.ПараметрыДанныхНомерДок = Номер;
	ОбластьОтчет.Параметры.ПараметрыДанныхДатаДок = Дата;
	ОбластьОтчет.Параметры.ГодГрафика = Год(Период);
	
	Т7ТабличныйДокумент.Вывести(ОбластьОтчет); 
	
	Для каждого ГрафикСтрока Из Графики Цикл

		ОбластьОтчет = Макет.ПолучитьОбласть("СтрокаРаботник");
		ОбластьОтчет.Параметры.ПодразделениеНаименование = ГрафикСтрока.Подразделение;
		ОбластьОтчет.Параметры.ДолжностьНаименование = ГрафикСтрока.Должность;
		ОбластьОтчет.Параметры.ФИОПолные = ГрафикСтрока.Сотрудник;
		ОбластьОтчет.Параметры.ТабельныйНомер = ГрафикСтрока.Сотрудник.ТабельныйНомер;
		ОбластьОтчет.Параметры.КоличествоДней = ГрафикСтрока.КалендарныеДни;
		ОбластьОтчет.Параметры.ДатаНачала = ГрафикСтрока.ДатаПлан;   
		ОбластьОтчет.Параметры.ДатаФакт = ГрафикСтрока.ДатаФакт;   
		
		ОбластьОтчет.Параметры.ОснованиеПереноса = ГрафикСтрока.ОснованиеДокумент;
		ОбластьОтчет.Параметры.ПеренесеннаяДатаНачала = ГрафикСтрока.ПереносОтпуска;
		ОбластьОтчет.Параметры.Примечание = ГрафикСтрока.Примечание;
		
		Т7ТабличныйДокумент.Вывести(ОбластьОтчет); 

	КонецЦикла;
	
	ОбластьОтчет = Макет.ПолучитьОбласть("Подвал");         
	ОбластьОтчет.Параметры.ДолжностьРуководителяКадровойСлужбы = КадровикДанные.Должность;
	ОбластьОтчет.Параметры.РуководительКадровойСлужбыРасшифровкаПодписи	= КадровикДанные.ФИОСокращенное;
	Т7ТабличныйДокумент.Вывести(ОбластьОтчет); 
	
	Возврат Т7ТабличныйДокумент;
	
КонецФункции      

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
