#Если Сервер Или ТолстыйКлиентОбычноеПриложение Тогда

#Область ОписаниеПеременных

// Параметры:
//	ДанныеДляОтчета - структура, содержит информацию для формирования отчета 
//					  см. инкОтчетыСервер.ПолучитьСтруктуруДляВедомостиНачисленийУдержаний().
//						
//
//	ТабличныеДокументы - структура, содержит табличные документы для вывода на печать:	
//  	ВедомостьНачислений 	- табличный документ, Ведомость начислений. 
//		ВедомостьУдержаний		- табличный документ, Ведомость удержаний.
//		ВедомостьНачисленийУдержаний - табличный документ, Ведомость начислений/удержаний.

Перем ДанныеДляОтчета Экспорт; // Структура - Данные для отчета см. описание в модуле объекта;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Получить табличные документы
// 
// Возвращаемое значение:
//   - Структура - Табличные документы;
//
Функция ПолучитьТабличныеДокументы() Экспорт
	
	ТабличныеДокументы = Новый Структура;
	ТабличныеДокументы.Вставить("ВедомостьНачислений");
	ТабличныеДокументы.Вставить("ВедомостьУдержаний");
	ТабличныеДокументы.Вставить("ВедомостьНачисленийУдержаний");

	ИсходныеДанные = ПолучитьИсходныеДанные(ДанныеДляОтчета);
	ТабличныеДокументы.ВедомостьНачислений = СформироватьВедомостьНачислений(ДанныеДляОтчета,ИсходныеДанные);
	ТабличныеДокументы.ВедомостьУдержаний = СформироватьВедомостьУдержаний(ДанныеДляОтчета,ИсходныеДанные);
	ТабличныеДокументы.ВедомостьНачисленийУдержаний = СформироватьОбщуюВедомость(ТабличныеДокументы);
	
	
	Возврат ТабличныеДокументы;
	
КонецФункции   

#Область о // Инициализация:

// Процедура - Инициализация
//
Процедура Инициализация() Экспорт
	
	ДанныеДляОтчета = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область о // Исходные данные:

Функция ПолучитьИсходныеДанные(ДанныеДляОтчета)
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("СальдоВходящееТаблица",инкВедомостиСервер.ПолучитьСальдоВходящееТаблица(ДанныеДляОтчета));
	ИсходныеДанные.Вставить("НачисленияСоответсвие",ПолучитьНачисленияСоответсвие(ДанныеДляОтчета));
	ИсходныеДанные.Вставить("УдержанияТаблица",ПолучитьУдержанияТаблица(ДанныеДляОтчета));
	ИсходныеДанные.Вставить("ИсключитьУдержания",ПолучитьИсключенныеУдержания(ИсходныеДанные));
	ИсходныеДанные.Вставить("УдержанияСоответствие",ПолучитьУдержанияСоответсвие(ИсходныеДанные));
	ИсходныеДанные.Вставить("НачисленияКРасшифровке", Новый Соответствие);
	ИсходныеДанные.Вставить("УдержанияКРасшифровке", Новый Соответствие);
	
	Возврат ИсходныеДанные;
	
КонецФункции    

Функция ПолучитьИсключенныеУдержания(ИсходныеДанные)

	ИсключитьУдержания = Новый Соответствие;
	ИсключитьУдержания.Вставить(ПланыВидовРасчета.инкУдержания.Аванс,1);
	ИсключитьУдержания.Вставить(ПланыВидовРасчета.инкУдержания.МежрасчетныеВыплаты,1);
	ИсключитьУдержания.Вставить(ПланыВидовРасчета.инкУдержания.ВыплатаОтпускныхИБЛВТекущемМесяце,1);
	ИсключитьУдержания.Вставить(ПланыВидовРасчета.инкУдержания.ЗарплатаТекущегоМесяца,1);
	ИсключитьУдержания.Вставить(ПланыВидовРасчета.инкУдержания.ЗарплатаПредыдущегоМесяца,1);
	
	Возврат ИсключитьУдержания;
	
КонецФункции

Функция ПолучитьУдержанияСоответсвие(ИсходныеДанные)
	
	УдержанияСоответсвие = Новый Соответствие;
	ПрочиеУдержания = ИсходныеДанные.УдержанияТаблица;
	ПрочиеУдержания.Сортировать("ПорядокОтображения Убыв");
	
	Для каждого УдержаниеСтрока Из ПрочиеУдержания Цикл
		
		Если ИсходныеДанные.ИсключитьУдержания[УдержаниеСтрока.Удержание] = 1 Тогда
			Продолжить;	
		КонецЕсли;
		
		УдержанияСоответсвие.Вставить("Код - "+УдержаниеСтрока.ПорядокОтображения,УдержаниеСтрока.Удержание);			
		
	КонецЦикла; 
	
	Возврат УдержанияСоответсвие;
	
КонецФункции

Функция ПолучитьНачисленияСоответсвие(ДанныеДляОтчета)
	
	НачисленияСоответсвие = Новый Соответствие;
	ПрочиеНачисления = ДанныеДляОтчета.ТаблицаПрочихНачислений;
	ПрочиеНачисления.Сортировать("ПорядокОтображения Убыв");
	
	Для каждого НачислениеСтрока Из ПрочиеНачисления Цикл
		Если ЗначениеЗаполнено(НачислениеСтрока.Начисление) Тогда
			НачисленияСоответсвие.Вставить("Код - "+НачислениеСтрока.ПорядокОтображения,НачислениеСтрока.Начисление);			
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат НачисленияСоответсвие;
	
КонецФункции

Функция ПолучитьУдержанияТаблица(ДанныеДляОтчета)
	
	УдержанияСводТаблица = Новый ТаблицаЗначений;
	ПрочиеУдержания = ДанныеДляОтчета.ТаблицаПрочихУдержаний;
	УдержанияСводТаблица = ПрочиеУдержания.Скопировать();
	УдержанияСводТаблица.Очистить();
	
	Для каждого УдержаниеСтрока Из ПрочиеУдержания Цикл
		
		УдержанияСводСтрока = УдержанияСводТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(УдержанияСводСтрока,УдержаниеСтрока);
		
		ВидУдержания = УдержаниеСтрока.Удержание;
		Если СтрНайти(НРег(УдержаниеСтрока.Удержание.Наименование),НРег("Аванс")) > 0 Тогда
			ВидУдержания = ПланыВидовРасчета.инкУдержания.Аванс;
		ИначеЕсли СтрНайти(НРег(УдержаниеСтрока.Удержание.Наименование),НРег("Долг")) > 0 Тогда	
			ВидУдержания = ПланыВидовРасчета.инкУдержания.Долг;
		ИначеЕсли СтрНайти(НРег(УдержаниеСтрока.Удержание.Наименование),НРег("Подарки")) > 0 Тогда	
			ВидУдержания = ПланыВидовРасчета.инкУдержания.Подарки;
		ИначеЕсли СтрНайти(НРег(УдержаниеСтрока.Удержание.Наименование),НРег("Алименты")) > 0 Тогда	
			ВидУдержания = ПланыВидовРасчета.инкУдержания.Алименты;
		КонецЕсли;
		
		УдержанияСводСтрока.Удержание = ВидУдержания;
		УдержанияСводСтрока.ПорядокОтображения = Число(Прав(ВидУдержания.Код,3));
		
	КонецЦикла;                 
	
	УдержанияСводТаблица.Свернуть("Сотрудник,Удержание,ПорядокОтображения","СуммаУдержания");
	
	Возврат УдержанияСводТаблица;
	
КонецФункции

Функция ПолучитьДатуВедомости(ПериодНачисления)
	
	ПериодНачисленияСтрока = "";
	
	Если ТипЗнч(ПериодНачисления) = Тип("Дата") Тогда
		ПериодНачисленияСтрока = Формат(ПериодНачисления, "ДФ='ММММ гггг'") + "г.";
	ИначеЕсли ТипЗнч(ПериодНачисления) = Тип("СтандартныйПериод") Тогда
		ПериодНачисленияСтрока = Формат(ПериодНачисления.ДатаНачала, "ДФ='ММММ гггг'") + "г." 
		                       + " - "
							   + Формат(ПериодНачисления.ДатаОкончания, "ДФ='ММММ гггг'") + "г.";
	КонецЕсли;
	
	Возврат ПериодНачисленияСтрока;
	
КонецФункции

Процедура ВывестиБлокРасшифровки(НачисленияУдержанияСоответсвие,ВедомостьМакет,ВедомостьТабличныйДокумент)
	
	Если НачисленияУдержанияСоответсвие.Количество() > 0 Тогда
		
		РасшифровкаТаблица = Новый ТаблицаЗначений;
		РасшифровкаТаблица.Колонки.Добавить("Расшифровка");
		Для каждого НачисленияУдержанияЗначение Из НачисленияУдержанияСоответсвие Цикл
			РасшифровкаСтрока = РасшифровкаТаблица.Добавить();
			РасшифровкаСтрока.Расшифровка = Строка(НачисленияУдержанияЗначение.Ключ) + ": " 
										  + Строка(НачисленияУдержанияЗначение.Значение) + ";";	
		КонецЦикла; 
        РасшифровкаТаблица.Сортировать("Расшифровка");
		
		РасшифровкаЗаголовокОбласть = ВедомостьМакет.ПолучитьОбласть("РасшифровкаЗаголовок");
		ВедомостьТабличныйДокумент.Вывести(РасшифровкаЗаголовокОбласть);
		
		РасшифровкаСтрокаОбласть = ВедомостьМакет.ПолучитьОбласть("РасшифровкаСтрока");
		Для каждого РасшифровкаСтрока Из РасшифровкаТаблица Цикл
			ЗаполнитьЗначенияСвойств(РасшифровкаСтрокаОбласть.Параметры,РасшифровкаСтрока);
			ВедомостьТабличныйДокумент.Вывести(РасшифровкаСтрокаОбласть);
		КонецЦикла; 
		
	КонецЕсли; 
	    	
КонецПроцедуры                                                   

Функция ПолучитьИсточникФинансирования(ИсточникФинансирования)
	
	ИсточникФинансированияСтрока = "";
	
	Если ЗначениеЗаполнено(ИсточникФинансирования) Тогда
		
		ИсточникФинансированияСтрока = "(" 
		                             + ИсточникФинансирования
									 + ")";	
		
	КонецЕсли;
	
	Возврат ИсточникФинансированияСтрока;
	
КонецФункции

Функция ПолучитьСуммуСальдо(Сотрудник,ИсходныеДанные)
	
    СальдоСумма = 0;    
	
	Если Сотрудник = Неопределено Тогда
		СальдоСумма = ИсходныеДанные.СальдоВходящееТаблица.Итог("СальдоВходящее");   	
	Иначе
		ПоискСтроки = Новый Структура("Сотрудник",Сотрудник);
		СальдоВходящееМассив = ИсходныеДанные.СальдоВходящееТаблица.НайтиСтроки(ПоискСтроки);
		Для каждого СальдоВходящееЭлемент Из СальдоВходящееМассив Цикл
			СальдоСумма = СальдоСумма + СальдоВходящееЭлемент.СальдоВходящее;    
		КонецЦикла;
	КонецЕсли;
	
	Возврат СальдоСумма;	
	
КонецФункции

#КонецОбласти

#Область о // Общая ведомость:                    

Функция СформироватьОбщуюВедомость(ТабличныеДокументы)
	
	ДокументОбщий = новый ТабличныйДокумент;
	ДокументОбщий.Вывести(ТабличныеДокументы.ВедомостьНачислений);
	ДокументОбщий.Вывести(ТабличныеДокументы.ВедомостьУдержаний);
	
	ДокументОбщий.ФиксацияСлева = 1;
	
	Возврат ДокументОбщий;
	
КонецФункции

#КонецОбласти

#Область о // Таблица начислений:

Функция СформироватьВедомостьНачислений(ДанныеДляОтчета,ИсходныеДанные)
	
	ВедомостьНачислений = Новый ТабличныйДокумент;
	ВедомостьНачисленийМакет = ПолучитьМакет("МакетВедомостьНачисленийУдержаний");
	
	// Заголовок:
	ЗаголовокОбласть = ВедомостьНачисленийМакет.ПолучитьОбласть("ЗаголовокОтчета");
	ЗаголовокОбласть.Параметры.НачисленийУдержаний = "начислений"; 
	ЗаголовокОбласть.Параметры.ДатаВедомости = ПолучитьДатуВедомости(ДанныеДляОтчета.Период);
	ЗаголовокОбласть.Параметры.Организация = ДанныеДляОтчета.Организация; 
	ЗаголовокОбласть.Параметры.Подразделение = инкОтчетыСервер.ПолучитьМассивСтрокой(ДанныеДляОтчета.Подразделения);
	ЗаголовокОбласть.Параметры.ИсточникФинансирования = ПолучитьИсточникФинансирования(ДанныеДляОтчета.ИсточникФинансирования);
	ВедомостьНачислений.Вывести(ЗаголовокОбласть);
	
	ВывестиБлокТаблицыНачислений("ТаблицаЗаголовок",ВедомостьНачислений,ВедомостьНачисленийМакет,ИсходныеДанные);
	
	Для каждого ВедомостьСтрока Из ДанныеДляОтчета.ТаблицаВедомость Цикл
		ВывестиБлокТаблицыНачислений("ТаблицаСтрока",ВедомостьНачислений,ВедомостьНачисленийМакет,ИсходныеДанные,ВедомостьСтрока);
	КонецЦикла; 
	
	ВывестиБлокТаблицыНачислений("ТаблицаИтог",ВедомостьНачислений,ВедомостьНачисленийМакет,ИсходныеДанные,,ДанныеДляОтчета.ТаблицаВедомость);

	// Расшифровка:
	ВывестиБлокРасшифровки(ИсходныеДанные.НачисленияКРасшифровке,ВедомостьНачисленийМакет,ВедомостьНачислений);
	
	ВедомостьНачислений.ФиксацияСлева = 1;
	
	Возврат ВедомостьНачислений;
	
КонецФункции

Процедура ВывестиБлокТаблицыНачислений(ИмяСтроки,ВедомостьТабличныйДокумент,ВедомостьМакет,ИсходныеДанные,ВедомостьСтрока = Неопределено,ВедомостьТаблица = Неопределено)
	
	// Таблица:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаФИО");
	Если ИмяСтроки = "ТаблицаСтрока" Тогда
		ТаблицаОбласть.Параметры.ФИО = ВедомостьСтрока.Сотрудник.ФИОСокращенное;
	КонецЕсли; 
	ВедомостьТабличныйДокумент.Вывести(ТаблицаОбласть);
	
	// Сальдо:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
	Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
		ТаблицаОбласть.Параметры.НачислениеУдержание = "Сальдо";	
	ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуСальдо(ВедомостьСтрока.Сотрудник,ИсходныеДанные);
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуСальдо(Неопределено,ИсходныеДанные);;	
	КонецЕсли; 
	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	// Оклад:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
	Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
		ТаблицаОбласть.Параметры.НачислениеУдержание = "Оклад";
	ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.Оклад;	
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("Оклад");
	КонецЕсли; 	
	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	// РК:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
	Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
		ТаблицаОбласть.Параметры.НачислениеУдержание = "РК";
	ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.РайонныйКоэффициент;
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("РайонныйКоэффициент");	
	КонецЕсли; 	
	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	// СН:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
	Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
		ТаблицаОбласть.Параметры.НачислениеУдержание = "СН";
	ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.СевернаяНадбавка;
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("СевернаяНадбавка");
	КонецЕсли; 		
	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);

	// Прочие начисления:
	Для каждого НачисленияЗначение Из ИсходныеДанные.НачисленияСоответсвие Цикл
		
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
        	ТаблицаОбласть.Параметры.НачислениеУдержание = НачисленияЗначение.Ключ;
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуНачисления(ВедомостьСтрока.Сотрудник,НачисленияЗначение.Значение,ДанныеДляОтчета.ТаблицаПрочихНачислений);
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуНачисления(Неопределено,НачисленияЗначение.Значение,ДанныеДляОтчета.ТаблицаПрочихНачислений);
		КонецЕсли;	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
		
		ИсходныеДанные.НачисленияКРасшифровке.Вставить(НачисленияЗначение.Ключ,НачисленияЗначение.Значение);			

	КонецЦикла; 
	
	// Всего:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаВсего");
	Если ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.ВсегоНачислено;
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("ВсегоНачислено");
	КонецЕсли;	

	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	
КонецПроцедуры

Функция ПолучитьСуммуУдержания(Сотрудник,УдержаниеЗначение,ПрочиеУдержания)
	
	СуммаУдержания = 0;
	
	ПоискЗначения = Новый Структура;
	Если Сотрудник <> Неопределено Тогда
		ПоискЗначения.Вставить("Сотрудник",Сотрудник);	             
	КонецЕсли; 
	ПоискЗначения.Вставить("Удержание",УдержаниеЗначение);
	
	УдержанияМассив = ПрочиеУдержания.НайтиСтроки(ПоискЗначения);
	
	Для каждого УдержаниеЭлемент Из УдержанияМассив Цикл
		СуммаУдержания = СуммаУдержания + УдержаниеЭлемент.СуммаУдержания;
	КонецЦикла; 
	
	Возврат СуммаУдержания;
	
КонецФункции

Функция ПолучитьСуммуНачисления(Сотрудник,НачисленияЗначение,ПрочиеНачисления)
	
	СуммаНачисления = 0;
	
	ПоискЗначения = Новый Структура;
	Если Сотрудник <> Неопределено Тогда
		ПоискЗначения.Вставить("Сотрудник",Сотрудник);	             
	КонецЕсли; 
	ПоискЗначения.Вставить("Начисление",НачисленияЗначение);
	
	НачисленияМассив = ПрочиеНачисления.НайтиСтроки(ПоискЗначения);
	
	Для каждого НачисленияЭлемент Из НачисленияМассив Цикл
		СуммаНачисления = СуммаНачисления + НачисленияЭлемент.СуммаНачисления;
	КонецЦикла; 
	
	Возврат СуммаНачисления;
	
КонецФункции

#КонецОбласти

#Область о // Таблица удержаний:

Функция СформироватьВедомостьУдержаний(ДанныеДляОтчета,ИсходныеДанные)
	
	ВедомостьУдержаний = Новый ТабличныйДокумент;
	ВедомостьУдержанийМакет = ПолучитьМакет("МакетВедомостьНачисленийУдержаний");
	
	// Заголовок:
	ЗаголовокОбласть = ВедомостьУдержанийМакет.ПолучитьОбласть("ЗаголовокОтчета");
	ЗаголовокОбласть.Параметры.НачисленийУдержаний = "удержаний";
	ЗаголовокОбласть.Параметры.ДатаВедомости = ПолучитьДатуВедомости(ДанныеДляОтчета.Период);
	ЗаголовокОбласть.Параметры.Подразделение = инкОтчетыСервер.ПолучитьМассивСтрокой(ДанныеДляОтчета.Подразделения);
	ЗаголовокОбласть.Параметры.Организация = ДанныеДляОтчета.Организация;      
	ЗаголовокОбласть.Параметры.ИсточникФинансирования = ДанныеДляОтчета.ИсточникФинансирования;
	ВедомостьУдержаний.Вывести(ЗаголовокОбласть);
	
	// Таблица:
	ВывестиБлокТаблицыУдержаний("ТаблицаЗаголовок",ВедомостьУдержаний,ВедомостьУдержанийМакет,ИсходныеДанные);
	
	Для каждого ВедомостьСтрока Из ДанныеДляОтчета.ТаблицаВедомость Цикл
		ВывестиБлокТаблицыУдержаний("ТаблицаСтрока",ВедомостьУдержаний,ВедомостьУдержанийМакет,ИсходныеДанные,ВедомостьСтрока);
	КонецЦикла; 
	
	ВывестиБлокТаблицыУдержаний("ТаблицаИтог",ВедомостьУдержаний,ВедомостьУдержанийМакет,ИсходныеДанные);

	// Расшифровка:
	ВывестиБлокРасшифровки(ИсходныеДанные.УдержанияКРасшифровке,ВедомостьУдержанийМакет,ВедомостьУдержаний);
	
	ВедомостьУдержаний.ФиксацияСлева = 1;
	
	Возврат ВедомостьУдержаний;
	
КонецФункции                                                            

Процедура ВывестиБлокТаблицыУдержаний(ИмяСтроки,ВедомостьТабличныйДокумент,ВедомостьМакет,ИсходныеДанные,ВедомостьСтрока = Неопределено,ВедомостьТаблица = Неопределено)
	
	// Таблица:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаФИО");
	Если ИмяСтроки = "ТаблицаСтрока" Тогда
		ТаблицаОбласть.Параметры.ФИО = ВедомостьСтрока.Сотрудник.ФИОСокращенное;
	КонецЕсли; 
	ВедомостьТабличныйДокумент.Вывести(ТаблицаОбласть);
	
	// Вх. сальдо:
	ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
	Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
		ТаблицаОбласть.Параметры.НачислениеУдержание = "Вх. сальдо";	
	ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
		ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуСальдо(ВедомостьСтрока.Сотрудник,ИсходныеДанные);
	ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
		ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуСальдо(Неопределено,ИсходныеДанные);	
	КонецЕсли; 
	ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	
	// Начислено:
	ИтогСумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("ВсегоНачислено");
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Начислено";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.ВсегоНачислено;	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;
	
	// НДФЛ:    
	ИтогСумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("НДФЛ");
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "НДФЛ";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.НДФЛ;	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;
	
	// Аванс:   
	ИтогСумма = ПолучитьСуммуУдержания(Неопределено,
		                                 ПланыВидовРасчета.инкУдержания.Аванс,
										 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
	Если ИтогСумма<>0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Аванс";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,
			                                 ПланыВидовРасчета.инкУдержания.Аванс,
											 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;									 
	
	// Межрасчетные выплаты:
	ИтогСумма = ПолучитьСуммуУдержания(Неопределено,
		                               ПланыВидовРасчета.инкУдержания.МежрасчетныеВыплаты,
									   ДанныеДляОтчета.ТаблицаПрочихУдержаний);
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Межрасчетные выплаты";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,
			                                 ПланыВидовРасчета.инкУдержания.МежрасчетныеВыплаты,
											 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;									 
	
	// Выплаты отпускн. и б/л:
	ИтогСумма = ПолучитьСуммуУдержания(Неопределено,
		                                 ПланыВидовРасчета.инкУдержания.ВыплатаОтпускныхИБЛВТекущемМесяце,
										 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Выплаты отпускн. и б/л";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,
			                                 ПланыВидовРасчета.инкУдержания.ВыплатаОтпускныхИБЛВТекущемМесяце,
											 ДанныеДляОтчета.ТаблицаПрочихУдержаний);	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;	
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;                                         
	
	// Алименты:
	ИтогСумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("Алименты");
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Алименты";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.Алименты;	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("Алименты");
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;
	
	// ЗП пред. мес.:
	ИтогСумма = ПолучитьСуммуУдержания(Неопределено,
		                               ПланыВидовРасчета.инкУдержания.ЗарплатаПредыдущегоМесяца,
									   ДанныеДляОтчета.ТаблицаПрочихУдержаний);
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "ЗП пред. мес.";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,
			                                 ПланыВидовРасчета.инкУдержания.ЗарплатаПредыдущегоМесяца,
											 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;									 
			
	// Прочие удержания:
	Для каждого УдержаниеЗначение Из ИсходныеДанные.УдержанияСоответствие Цикл
		
		ИтогСумма = ПолучитьСуммуУдержания(Неопределено,УдержаниеЗначение.Значение,ДанныеДляОтчета.ТаблицаПрочихУдержаний);
		
		Если ИтогСумма <> 0 Тогда
			ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
			Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
	        	ТаблицаОбласть.Параметры.НачислениеУдержание = УдержаниеЗначение.Ключ;
			ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
				ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,УдержаниеЗначение.Значение,ДанныеДляОтчета.ТаблицаПрочихУдержаний);
			ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
				ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
			КонецЕсли;	
			ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
			
			ИсходныеДанные.УдержанияКРасшифровке.Вставить(УдержаниеЗначение.Ключ,УдержаниеЗначение.Значение);			

		КонецЕсли;    
		
	КонецЦикла; 
	
	// ЗП тек. мес.:
	ИтогСумма = ПолучитьСуммуУдержания(Неопределено,
		                                 ПланыВидовРасчета.инкУдержания.ЗарплатаТекущегоМесяца,
										 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "ЗП тек. мес.";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ПолучитьСуммуУдержания(ВедомостьСтрока.Сотрудник,
			                                 ПланыВидовРасчета.инкУдержания.ЗарплатаТекущегоМесяца,
											 ДанныеДляОтчета.ТаблицаПрочихУдержаний);
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;									 
	
	// Всего ударжано:
	ИтогСумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("ВсегоУдержано");
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "Всего уд.";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.ВсегоУдержано;	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма;
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;
	
	// К выплате:
	ИтогСумма = ДанныеДляОтчета.ТаблицаВедомость.Итог("КВыплате");
	Если ИтогСумма <> 0 Тогда
		ТаблицаОбласть = ВедомостьМакет.ПолучитьОбласть(ИмяСтроки+"|ТаблицаНачислениеУдержание");
		Если ИмяСтроки = "ТаблицаЗаголовок" Тогда
			ТаблицаОбласть.Параметры.НачислениеУдержание = "К выплате";
		ИначеЕсли ИмяСтроки = "ТаблицаСтрока" Тогда 	
			ТаблицаОбласть.Параметры.Сумма = ВедомостьСтрока.КВыплате;	
		ИначеЕсли ИмяСтроки = "ТаблицаИтог" Тогда	
			ТаблицаОбласть.Параметры.Сумма = ИтогСумма; 
		КонецЕсли; 	
		ВедомостьТабличныйДокумент.Присоединить(ТаблицаОбласть);
	КонецЕсли;
	
КонецПроцедуры
            
#КонецОбласти

#КонецОбласти

#КонецЕсли




