#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем Организация Экспорт;			// Ссылка - Ссылка на справочник "Организации";
Перем НомерКорректировки Экспорт;   // Число - Номер корректировки;
Перем Дата1 Экспорт;				// Дата	- Дата1 (начало периода);	
Перем Дата2 Экспорт;				// Дата - Дата2 (конец периода);
Перем ТабличныйДокумент Экспорт;	// Табличный документ - Отчет;    
Перем ФайлВыгрузкиСтруктура Экспорт;// Структура - Данные файла выгрузки;    

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область о // Инициализация:   

// Процедура - Инициализация
//
Процедура Инициализация() Экспорт
	
	Организация = Неопределено;
	Дата1 = Неопределено;
	Дата2 = Неопределено;
	ТабличныйДокумент = Неопределено;
	ФайлВыгрузкиСтруктура = Неопределено; 
	
КонецПроцедуры

// Процедура - Сформировать отчет
//
Процедура СформироватьОтчет() Экспорт
	
	Если НЕ ПроверкаПеременных() Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныеДанные = ПолучитьИсходныеДанные();
	ТабличныйДокумент = СформироватьТабличныйДокумент(ИсходныеДанные);
		
КонецПроцедуры                                                        

// Процедура - Сформировать файл
//
Процедура СформироватьФайл() Экспорт
	
	Если НЕ ПроверкаПеременных() Тогда
		Возврат;
	КонецЕсли;

	ИсходныеДанные = ПолучитьИсходныеДанные();
	
	ПараметрыВыгрузки = СформироватьСтруктуруПараметров(ИсходныеДанные);

	ДеревоВыгрузки = ИзвлечьСтруктуруXML();
	ЗаполнитьДанными(ДеревоВыгрузки,ПараметрыВыгрузки);
	
	КодировкаФайлаВыгрузки = "windows-1251";
	
	ТекстФайлаВыгрузки = ВыгрузитьДеревоВXML(ДеревоВыгрузки,
											 ПараметрыВыгрузки,
											 КодировкаФайлаВыгрузки,
											 ИсходныеДанные);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаФайлаВыгрузки);
	ЗаписьТекста.Записать(ТекстФайлаВыгрузки);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	
	ФайлВыгрузки = Новый ДвоичныеДанные(ИмяВременногоФайла);
	//       
	ФайлВыгрузкиСтруктура = Новый Структура;   
	ФайлВыгрузкиСтруктура.Вставить("ФайлАдрес",ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор));
	ФайлВыгрузкиСтруктура.Вставить("ФайлИмя",ПараметрыВыгрузки.ИдФайл+".xml");
	//
	УдалитьФайлы(ИмяВременногоФайла);
	
КонецПроцедуры  

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверкаПеременных()
	
	ПроверкаПеременных = Истина;
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		инкОбщийКлиентСервер.СообщитьПользователю("Не задана организация.");	
		ПроверкаПеременных = Ложь;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Дата1) ИЛИ НЕ ЗначениеЗаполнено(Дата2) Тогда
		инкОбщийКлиентСервер.СообщитьПользователю("Не задан период.");	
		ПроверкаПеременных = Ложь;
	КонецЕсли;   
	
	Возврат ПроверкаПеременных;
	
КонецФункции

#Область о // Исходные данные:   

Функция ПолучитьИсходныеДанные()
	
	ИсходныеДанные = Новый Структура;
	
	ИсходныеДанные.Вставить("НачисленияТаблица",ПолучитьНачисленияТаблица());
	
	ИсходныеДанные.Вставить("НомерКорректировки",Формат(НомерКорректировки,"ЧЦ=3; ЧН=000; ЧВН="));
	ИсходныеДанные.Вставить("ИНН",Организация.ИНН);
	ИсходныеДанные.Вставить("КПП",Организация.КПП);
    ИсходныеДанные.Вставить("НомерКорректировки",Формат(НомерКорректировки,"ЧЦ=3; ЧН=000; ЧВН="));
    ИсходныеДанные.Вставить("Период",Формат(Дата1,"ДФ=MM"));
    ИсходныеДанные.Вставить("ОтчетГод",Формат(Дата1,"ДФ=yyyy"));
    ИсходныеДанные.Вставить("НалоговыйОрган",Организация.ИФНСКод);   
	
	ИсходныеДанные.Вставить("ОГРНИП");
	ИсходныеДанные.Вставить("ПоМесту","214");
	ИсходныеДанные.Вставить("ИндивидуальныйПредприниматель",(СтрДлина(Организация.ИНН) = 12));
	
	Если ИсходныеДанные.ИндивидуальныйПредприниматель Тогда
		ИсходныеДанные.ОГРНИП = Организация.ОГРН;   
		ИсходныеДанные.ПоМесту = "120";	
	КонецЕсли;	
	
    ИсходныеДанные.Вставить("НаимОрг",Организация.Наименование);
	
	ТелОрганизации = Справочники.инкОрганизации.ПолучитьКонтактныйТелефонОрганизации(Организация);
    ИсходныеДанные.Вставить("ТелОрганизации",ТелОрганизации);
	
	КоличествоЛистов = Цел(ИсходныеДанные.НачисленияТаблица.Количество()/4) + 1;
	ИсходныеДанные.Вставить("СоставленаНа",Строка(КоличествоЛистов));
	ИсходныеДанные.Вставить("ПрПодп","1");
	
	ИсходныеДанные.Вставить("ОргПодписантФамилия",Организация.Руководитель.Фамилия);
	ИсходныеДанные.Вставить("ОргПодписантИмя",Организация.Руководитель.Имя);
	ИсходныеДанные.Вставить("ОргПодписантОтчество",Организация.Руководитель.Отчество);
	
	ИсходныеДанные.Вставить("ДатаПодписи",Формат(ТекущаяДата(),"ДФ=ddMMyyyy"));
	
	ИсходныеДанные.Вставить("ДатаДок",  Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy"));
	ИсходныеДанные.Вставить("КодНО",    Организация.ИФНСКод);
	ИсходныеДанные.Вставить("НомКорр",  ИсходныеДанные.НомерКорректировки);
	
	ИсходныеДанные.Вставить("КодИФНСПосредника","");
	
	ИсходныеДанные.Вставить("Тлф", ТелОрганизации);
	
	ИсходныеДанные.Вставить("ФормРеорг", "");
	ИсходныеДанные.Вставить("ИННРеорг",  "");
	ИсходныеДанные.Вставить("КППРеорг",  "");
	
	ФИОИП = Новый Структура;
	ФИОИП.Вставить("Фамилия");
	ФИОИП.Вставить("Имя");
	ФИОИП.Вставить("Отчество");
	
	Если ИсходныеДанные.ИндивидуальныйПредприниматель Тогда
		ФИОИП.Фамилия	= Организация.Руководитель.Фамилия;
		ФИОИП.Имя		= Организация.Руководитель.Имя;
		ФИОИП.Отчество	= Организация.Руководитель.Отчество;
	КонецЕсли;
	
	ИсходныеДанные.Вставить("ФИОИП", ФИОИП);
	
	ИсходныеДанные.Вставить("ПрПодп","1");
	ИсходныеДанные.Вставить("НаимОргПред", "");
	ИсходныеДанные.Вставить("НаимДокПред", "");

	ФИОПодписанта = Новый Структура;
	ФИОПодписанта.Вставить("Фамилия",Организация.Руководитель.Фамилия);
	ФИОПодписанта.Вставить("Имя",Организация.Руководитель.Имя);
	ФИОПодписанта.Вставить("Отчество",Организация.Руководитель.Отчество);
	
	ИсходныеДанные.Вставить("ФИОПодписанта",ФИОПодписанта);

	Возврат ИсходныеДанные;
	
КонецФункции

Функция ПолучитьНачисленияТаблица()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНачисленияОбороты.Сотрудник КАК Сотрудник,
		|	инкНачисленияОбороты.СуммаНачисленияОборот + инкНачисленияОбороты.РайонныйКоэффициентОборот + инкНачисленияОбороты.СевернаяНадбавкаОборот КАК Сумма,
		|	инкНачисленияОбороты.Сотрудник.ИНН КАК ИНН,
		|	инкНачисленияОбороты.Сотрудник.СНИЛС КАК СНИЛС,
		|	инкНачисленияОбороты.Сотрудник.Фамилия КАК Фамилия,
		|	инкНачисленияОбороты.Сотрудник.Имя КАК Имя,
		|	инкНачисленияОбороты.Сотрудник.Отчество КАК Отчество
		|ИЗ
		|	РегистрНакопления.инкНачисления.Обороты(
		|			&Дата1,
		|			&Дата2,
		|			,
		|			Организация = &Организация
		|				И Начисление.ВходитВФОТ) КАК инкНачисленияОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Фамилия,
		|	Имя,
		|	Отчество";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(Дата1));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(Дата1));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;	
	
КонецФункции

#КонецОбласти

#Область о // Сформировать отчет:   

Функция СформироватьТабличныйДокумент(ИсходныеДанные)

	ТабДок = Новый ТабличныйДокумент;
	
	СформироватьТитульныйЛист(ИсходныеДанные,ТабДок);
	СформироватьРаздел1(ИсходныеДанные,ТабДок);
	
	ТабДок.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДок.АвтоМасштаб = Истина;
	
	Возврат ТабДок;	
	
КонецФункции       

Процедура СформироватьТитульныйЛист(ИсходныеДанные,ТабличныйДокумент)

	Макет = ПолучитьМакет("ПечатныйБланк2023Кв1_Титульный");
	ОбластьТаблицы = Макет.ПолучитьОбласть("ОсновнаяЧасть");
	
	инкОтчетыСервер.ВывестиДанныеВОбласти("ИНН",ИсходныеДанные.ИНН,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("КПП",ИсходныеДанные.КПП,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("НомерКорректировки",ИсходныеДанные.НомерКорректировки,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("Период",ИсходныеДанные.Период,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("ОтчетГод",ИсходныеДанные.ОтчетГод,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("НалоговыйОрган",ИсходныеДанные.НалоговыйОрган,ОбластьТаблицы);   
	инкОтчетыСервер.ВывестиДанныеВОбласти("ОГРНИП",ИсходныеДанные.ОГРНИП,ОбластьТаблицы);   
	инкОтчетыСервер.ВывестиДанныеВОбласти("ПоМесту",ИсходныеДанные.ПоМесту,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("НаимОрг",ИсходныеДанные.НаимОрг,ОбластьТаблицы);
    инкОтчетыСервер.ВывестиДанныеВОбласти("ТелОрганизации",ИсходныеДанные.ТелОрганизации,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("СоставленаНа",ИсходныеДанные.СоставленаНа,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("ПрПодп",ИсходныеДанные.ПрПодп,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("ОргПодписантФамилия",ИсходныеДанные.ОргПодписантФамилия,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("ОргПодписантИмя",ИсходныеДанные.ОргПодписантИмя,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("ОргПодписантОтчество",ИсходныеДанные.ОргПодписантОтчество,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("ДатаПодписи",ИсходныеДанные.ДатаПодписи,ОбластьТаблицы);
	
	ТабличныйДокумент.Вывести(ОбластьТаблицы); 
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
КонецПроцедуры  

Процедура СформироватьРаздел1(ИсходныеДанные,ТабличныйДокумент)

	Макет = ПолучитьМакет("ПечатныйБланк2023Кв1_Раздел1");
	ОбластьТаблицы = Макет.ПолучитьОбласть("ОсновнаяЧасть");
	
	ИндексСотрудника = 1;
	НомерСтраницы = 2;
	ЗаполнитьНачальныеДанныеСтраницыРаздел1(ОбластьТаблицы,НомерСтраницы);
	Для каждого НачислениеСтрока Из ИсходныеДанные.НачисленияТаблица Цикл
		
		Если ИндексСотрудника = 5 Тогда   
			
			ТабличныйДокумент.Вывести(ОбластьТаблицы);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ОбластьТаблицы = Макет.ПолучитьОбласть("ОсновнаяЧасть");
			
			ИндексСотрудника = 1;
			НомерСтраницы = НомерСтраницы + 1;
			ЗаполнитьНачальныеДанныеСтраницыРаздел1(ОбластьТаблицы,НомерСтраницы);
			
		КонецЕсли; 
		
		ИмяРаздела = "П000010002001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,НачислениеСтрока.ИНН,ОбластьТаблицы);

		ИмяРаздела = "П000010003001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,НачислениеСтрока.СНИЛС,ОбластьТаблицы);
		
		ИмяРаздела = "П000010004001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,НачислениеСтрока.Фамилия,ОбластьТаблицы);
		
		ИмяРаздела = "П000010005001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,НачислениеСтрока.Имя,ОбластьТаблицы);
		
		ИмяРаздела = "П000010006001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,НачислениеСтрока.Отчество,ОбластьТаблицы);
		
		СуммаСтрока = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Формат(НачислениеСтрока.Сумма,"ЧДЦ=2; ЧРД=.; ЧГ="), 12, " ", "Слева");
		ИмяРаздела = "П000010007001_"+ИндексСотрудника;
		инкОтчетыСервер.ВывестиДанныеВОбласти(ИмяРаздела,СуммаСтрока,ОбластьТаблицы);
		
		ИндексСотрудника = ИндексСотрудника + 1;
		
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьТаблицы);
	ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
КонецПроцедуры  

Процедура ЗаполнитьНачальныеДанныеСтраницыРаздел1(ОбластьТаблицы,НомерСтраницы)
	
	инкОтчетыСервер.ВывестиДанныеВОбласти("ИНН",Организация.ИНН,ОбластьТаблицы);
	инкОтчетыСервер.ВывестиДанныеВОбласти("КПП",Организация.КПП,ОбластьТаблицы);
	
	НомерСтраницыСтрока = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(Строка(НомерСтраницы), 3, "0", "Слева");;
	инкОтчетыСервер.ВывестиДанныеВОбласти("НомСтр",НомерСтраницыСтрока,ОбластьТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область о // Сформировать XML файл:

Функция ВыгрузитьДеревоВXML(ДеревоВыгрузки,
							ПараметрыВыгрузки,
							КодировкаФайлаВыгрузки, 
							ИсходныеДанные)
	
	ПотокXML = СоздатьНовыйПотокXML(КодировкаФайлаВыгрузки);
	
	ЗаписатьУзелДереваВXML(ДеревоВыгрузки, ПотокXML, ПараметрыВыгрузки, ИсходныеДанные);
	
	ТекстДляЗаписи = ПотокXML.Закрыть();
	
	Возврат ТекстДляЗаписи;
	
КонецФункции   

Функция СоздатьНовыйПотокXML(Кодировка = "utf-8") Экспорт
	
	ПотокXML = Новый ЗаписьXML();
	ПотокXML.Отступ = Истина;
	ПотокXML.УстановитьСтроку(Кодировка);
	ПотокXML.ЗаписатьОбъявлениеXML();
	Возврат ПотокXML;
	
КонецФункции

Процедура ЗаписатьУзелДереваВXML(СтрокаДерева,
								 ПотокXML,
								 ПараметрыВыгрузки,
								 ИсходныеДанные)
	
	Если ТипЗнч(СтрокаДерева) = Тип("ДеревоЗначений") Тогда
		ПотокXML.ЗаписатьНачалоЭлемента("Файл");
		ПотокXML.ЗаписатьАтрибут("xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		Для каждого Стр Из СтрокаДерева.Строки Цикл
			ЗаписатьУзелДереваВXML(Стр, ПотокXML, ПараметрыВыгрузки, ИсходныеДанные);
		КонецЦикла;
		ПотокXML.ЗаписатьКонецЭлемента();
	Иначе
		Если СтрокаДерева.Тип = "А" ИЛИ СтрокаДерева.Тип = "A" Тогда
			ПотокXML.ЗаписатьАтрибут(СтрокаДерева.Код, Строка(СтрокаДерева.Значение));
			
		ИначеЕсли СтрокаДерева.Код = "ПерсСвФЛ" Тогда
			
			Для каждого ТаблицаСтрока Из ИсходныеДанные.НачисленияТаблица Цикл
				
				ПотокXML.ЗаписатьНачалоЭлемента("ПерсСвФЛ");
				
				П000010002001 = ТаблицаСтрока.ИНН;
				Если ЗначениеЗаполнено(П000010002001) Тогда
					ПотокXML.ЗаписатьАтрибут("ИННФЛ", П000010002001);
				КонецЕсли;
				ПотокXML.ЗаписатьАтрибут("СНИЛС",   ТаблицаСтрока.СНИЛС);
				ПотокXML.ЗаписатьАтрибут("СумВыпл", СокрЛП(Формат(ТаблицаСтрока.Сумма, "ЧРД=.;ЧН=0;ЧГ=;ЧЦ=17;ЧДЦ=2")));
				
				ПотокXML.ЗаписатьНачалоЭлемента("ФИО");
				
				ПотокXML.ЗаписатьАтрибут("Фамилия", ТаблицаСтрока.Фамилия);
				ПотокXML.ЗаписатьАтрибут("Имя", ТаблицаСтрока.Имя);
				П000010006001 = СокрЛП(ТаблицаСтрока.Отчество);
				Если ЗначениеЗаполнено(П000010006001) Тогда
					ПотокXML.ЗаписатьАтрибут("Отчество", П000010006001);
				КонецЕсли;
				
				ПотокXML.ЗаписатьКонецЭлемента();// "ФИО"
				
				ПотокXML.ЗаписатьКонецЭлемента();// "ПерсСвФЛ"
				
			КонецЦикла;
			
			ДеревоРаздела = Неопределено;
			
		Иначе
			ПотокXML.ЗаписатьНачалоЭлемента(СтрокаДерева.Код);
			Для каждого Лист из СтрокаДерева.Строки Цикл
				ЗаписатьУзелДереваВXML(Лист, ПотокXML, ПараметрыВыгрузки, ИсходныеДанные);
			КонецЦикла;
			ПотокXML.ЗаписатьТекст(Строка(СтрокаДерева.Значение));
			ПотокXML.ЗаписатьКонецЭлемента();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанными(ДеревоВыгрузки,ПараметрыВыгрузки) Экспорт
	
	ОбработатьУсловныеЭлементы(ДеревоВыгрузки,ПараметрыВыгрузки);
	
	ЗаполнитьДаннымиУзел(ПараметрыВыгрузки,ДеревоВыгрузки);
	
	ОтсечьНезаполненныеНеобязательныеУзлы(ДеревоВыгрузки, ПараметрыВыгрузки);
	
КонецПроцедуры  

Процедура ОтсечьНезаполненныеНеобязательныеУзлы(Узел, ПараметрыВыгрузки)
	
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		Стр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		
		Если Стр.Код = "ПерсСвФЛ" Тогда
			Продолжить;
		КонецЕсли;
		
		ОтсечьНезаполненныеНеобязательныеУзлы(Стр, ПараметрыВыгрузки);
	КонецЦикла;
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если (СтрНайти(Узел.Обязательность, "Н") <> 0 ИЛИ СтрНайти(Узел.Обязательность, "H") <> 0)
			И УзелПуст(Узел, ПараметрыВыгрузки) Тогда // учтем оба варианта: кириллицу и латиницу
			УдалитьУзел(Узел);
		ИначеЕсли (СтрНайти(Узел.Обязательность, "М") <> 0
			ИЛИ СтрНайти(Узел.Обязательность, "M") <> 0) // учтем оба варианта: кириллицу и латиницу
			И УзелПуст(Узел, ПараметрыВыгрузки) И ?(СтрНайти(Узел.Обязательность, "О") <> 0
			ИЛИ СтрНайти(Узел.Обязательность, "O") <> 0,
			ИмеютсяАналогичныеСоседниеУзлыКлюч(Узел), Истина) Тогда
			УдалитьУзел(Узел);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры   

Функция ИмеютсяАналогичныеСоседниеУзлыКлюч(Стр) Экспорт 
	
	Возврат (Стр.Родитель.Строки.НайтиСтроки(Новый Структура("Ключ", Стр.Ключ), Ложь).Количество() > 1);
	
КонецФункции

Процедура УдалитьУзел(Узел) Экспорт 
	
	РодительУзла = ?(Узел.Родитель = Неопределено, Узел.Владелец(), Узел.Родитель);
	РодительУзла.Строки.Удалить(Узел);
	
КонецПроцедуры

Функция УзелПуст(Узел, ПараметрыВыгрузки)
	
	Для Каждого Стр из Узел.Строки Цикл
		Если НЕ УзелПуст(Стр, ПараметрыВыгрузки) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если Узел.Формат = "S" Тогда
		Возврат Истина;
	ИначеЕсли Узел.Формат = "N" Тогда
		Возврат Узел.Значение = "0" ИЛИ (НЕ ЗначениеЗаполнено(Узел.Значение));
	Иначе
		Возврат НЕ ЗначениеЗаполнено(Узел.Значение);
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДаннымиУзел(ПараметрыВыгрузки,Узел,НомерСтроки = Неопределено)
	
	СтрокиУзла = Новый Массив;
	Для Каждого Стр Из Узел.Строки Цикл
		СтрокиУзла.Добавить(Стр);
	КонецЦикла;
	
	Для Каждого Стр из СтрокиУзла Цикл
		Если Стр.Тип = "С" ИЛИ Стр.Тип = "C" Тогда // учет обоих вариантов: кириллицы и латиницы
			
			Если Стр.Многострочность Тогда
				НомСтр = 1;
				ПодчиненныйЭлемент = ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Стр);
				КлючПодчиненногоЭлемента = ПодчиненныйЭлемент.Ключ;
				Пока СвойствоОпределено(ПараметрыВыгрузки,
						КлючПодчиненногоЭлемента + "_" + Формат(НомСтр, "ЧГ=")) Цикл
					УзелСоответствующийСтроке = СкопироватьУзел(Узел, Стр);
					ЗаполнитьДаннымиУзел(ПараметрыВыгрузки,
										 УзелСоответствующийСтроке,
										 НомСтр);
					НомСтр = НомСтр + 1;
				КонецЦикла;
			Иначе
				ЗаполнитьДаннымиУзел(ПараметрыВыгрузки,
									 Стр,
									 НомерСтроки);
			КонецЕсли;
		Иначе
			Если ПустаяСтрока(Стр.ЗначениеПоУмолчанию) Тогда
				Если НЕ ПустаяСтрока(Стр.Ключ) Тогда
					ПолныйКодПоказателя = Стр.Ключ + ?(ЗначениеЗаполнено(НомерСтроки), "_" + Формат(НомерСтроки, "ЧГ="), "");
					ЗначениеПоказателя = Неопределено;
					Если ПараметрыВыгрузки.Свойство(ПолныйКодПоказателя, ЗначениеПоказателя) Тогда
						ВывестиПоказательВXML(Стр, ЗначениеПоказателя);
					Иначе
						ВывестиПоказательВXML(Стр, "");
					КонецЕсли;
				Иначе
					Стр.Значение = "";
				КонецЕсли;
			ИначеЕсли Лев(Стр.ЗначениеПоУмолчанию, 1) = "&" Тогда
				ВывестиПоказательВXML(Стр, ПараметрыВыгрузки[Сред(Стр.ЗначениеПоУмолчанию, 2)]);
			Иначе
				Стр.Значение = Стр.ЗначениеПоУмолчанию;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СкопироватьУзел(Родитель, Узел, Знач МаксИндекс = Неопределено)
	
	// Нахождение узла с максимальным индексом и с тем же кодом,
	// что и у копируемого, и добавление нового сразу после найденного.
	Если МаксИндекс = Неопределено Тогда
		СтрокиСТемЖеКодом = Родитель.Строки.НайтиСтроки(Новый Структура("Код", Узел.Код), Ложь);
		МаксИндекс = - 1;
		КолСтрокСТемЖеКодом = СтрокиСТемЖеКодом.Количество();
		Если КолСтрокСТемЖеКодом > 0 Тогда
			МаксИндекс = Родитель.Строки.Индекс(СтрокиСТемЖеКодом[КолСтрокСТемЖеКодом - 1]);
		КонецЕсли;
	КонецЕсли;
	
	ИндексСоздаваемогоУзла = МаксИндекс + 1;
	Если ИндексСоздаваемогоУзла >= Родитель.Строки.Количество() ИЛИ ИндексСоздаваемогоУзла = 0 Тогда
		// Аналогичный узел не найден или найденный узел - последний.
		НовыйУзел = Родитель.Строки.Добавить();
	Иначе
		НовыйУзел = Родитель.Строки.Вставить(ИндексСоздаваемогоУзла);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйУзел, Узел, , "Родитель, Строки");
	
	Для Каждого Стр из Узел.Строки Цикл
		СкопироватьУзел(НовыйУзел, Стр, Узел.Строки.Количество() - 1);
	КонецЦикла;
	
	Возврат НовыйУзел;
	
КонецФункции

Функция ПолучитьПервыйПодчиненныйУзелСЗаполненнымКлючом(Родитель)
	
	Для Каждого Стр из Родитель.Строки Цикл
		Если ЗначениеЗаполнено(Стр.Ключ) Тогда
			Возврат Стр;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
	
КонецФункции

// Возвращает признак определения свойства.
//
Функция СвойствоОпределено(Объект, ИмяСвойства) Экспорт
	
	ГУИД = Новый УникальныйИдентификатор;
	ВремСтрукт = Новый Структура(ИмяСвойства, ГУИД);
	ЗаполнитьЗначенияСвойств(ВремСтрукт, Объект);
	Возврат (ВремСтрукт[ИмяСвойства] <> ГУИД);
	
КонецФункции

Процедура ВывестиПоказательВXML(Узел, ЗначениеПоказателя)
	
	МинШирина = Узел.МинРазмерность;
	МаксШирина = Узел.МаксРазмерность;
	
	Если Узел.Формат = "T" ИЛИ Узел.Формат = "Т" Тогда // учтем оба варианта: кириллицу и латиницу
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			ЗначениеПоказателяСтр = Формат(ЗначениеПоказателя, "ДФ=dd.MM.yyyy");
		Иначе
			ЗначениеПоказателяСтр = СокрЛП(ЗначениеПоказателя);
		КонецЕсли;
		Узел.Значение = ?(МаксШирина < СтрДлина(ЗначениеПоказателяСтр),
		СокрЛП(Лев(ЗначениеПоказателяСтр, МаксШирина)), ЗначениеПоказателяСтр);
	ИначеЕсли Узел.Формат = "N" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		Если Узел.МаксРазмерность <> 0 И Узел.МаксРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(Узел.МаксРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Если Узел.МинРазмерность <> 99999 Тогда
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(Узел.МинРазмерность, "ЧГ=") + ";";
		КонецЕсли;
		Узел.Значение = СокрЛП(Формат(ЗначениеПоказателя, СтрокаФормата));
	КонецЕсли;
	
КонецПроцедуры

// Если в дереве параметр и он не выполнен, то удаляется из дерева.
Процедура ОбработатьУсловныеЭлементы(Узел,ПараметрыВыгрузки)
	
	Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
		Если Узел.Код = ПараметрыВыгрузки.ИмяКлючевогоУзлаСодержательнойЧасти Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	КоличествоСтрок = Узел.Строки.Количество();
	Для Инд = 1 По КоличествоСтрок Цикл
		ТекСтр = Узел.Строки.Получить(КоличествоСтрок - Инд);
		Если НЕ ПустаяСтрока(ТекСтр.Условие) Тогда
			Если НЕ УсловиеВыполнено(ПараметрыВыгрузки, ТекСтр.Условие) Тогда
				Узел.Строки.Удалить(ТекСтр);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ОбработатьУсловныеЭлементы(ТекСтр,ПараметрыВыгрузки);
	КонецЦикла;
	
КонецПроцедуры    

Функция УсловиеВыполнено(ИсходныеДанные, Условие) Экспорт
	
	Попытка
		РезультатВычисленияВыражения = ОбщегоНазначения.ВычислитьВБезопасномРежиме(
			СтрЗаменить(Условие, "&", "Параметры."), ИсходныеДанные);
		
		Если РезультатВычисленияВыражения <> Неопределено Тогда
			Если РезультатВычисленияВыражения = 1 ИЛИ РезультатВычисленияВыражения = 0
			 ИЛИ РезультатВычисленияВыражения = Истина ИЛИ РезультатВычисленияВыражения = Ложь Тогда
				Возврат НЕ (Булево(РезультатВычисленияВыражения) = Ложь);
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

Функция СформироватьСтруктуруПараметров(ИсходныеДанные) 
	
	ПараметрыВыгрузки = Новый Структура;
	
	ПараметрыВыгрузки.Вставить("ПоМесту", ИсходныеДанные.ПоМесту);
	
	ПараметрыВыгрузки.Вставить("ЭтоЮЛ", НЕ ИсходныеДанные.ИндивидуальныйПредприниматель);
	ПараметрыВыгрузки.Вставить("ЭтоПБОЮЛ", ИсходныеДанные.ИндивидуальныйПредприниматель);
	
	ПараметрыВыгрузки.Вставить("ВерсПрог", инкОбщийСервер.НазваниеИВерсияПрограммы());
	
	ПараметрыВыгрузки.Вставить("ДатаПодписи", ИсходныеДанные.ДатаПодписи);
	
	ПараметрыВыгрузки.Вставить("ДатаДок_Дата", ТекущаяДатаСеанса());
	
	ПараметрыВыгрузки.Вставить("ДатаДок",  ИсходныеДанные.ДатаДок);
	ПараметрыВыгрузки.Вставить("Период",   ИсходныеДанные.Период);
	ПараметрыВыгрузки.Вставить("ОтчетГод", ИсходныеДанные.ОтчетГод);
	ПараметрыВыгрузки.Вставить("КодНО",    ИсходныеДанные.КодНО);
	ПараметрыВыгрузки.Вставить("НомКорр",  ИсходныеДанные.НомКорр);
	
	ПараметрыВыгрузки.Вставить("КодИФНСПосредника",ИсходныеДанные.КодИФНСПосредника);
	
	ПараметрыВыгрузки.Вставить("Тлф", ИсходныеДанные.Тлф);
	
	ПараметрыВыгрузки.Вставить("НаимОрг", СокрЛП(ИсходныеДанные.НаимОрг));
	ПараметрыВыгрузки.Вставить("ИННОрг",  СокрЛП(ИсходныеДанные.ИНН));
	ПараметрыВыгрузки.Вставить("КПП",     СокрЛП(ИсходныеДанные.КПП));
	
	ПараметрыВыгрузки.Вставить("ОГРНИП", СокрЛП(ИсходныеДанные.ОГРНИП));
	
	ПараметрыВыгрузки.Вставить("ФормРеорг", СокрЛП(ИсходныеДанные.ФормРеорг));
	ПараметрыВыгрузки.Вставить("ИННРеорг",  СокрЛП(ИсходныеДанные.ИННРеорг));
	ПараметрыВыгрузки.Вставить("КППРеорг",  СокрЛП(ИсходныеДанные.КППРеорг));
	
	ПараметрыВыгрузки.Вставить("Фамилия",  ИсходныеДанные.ФИОИП.Фамилия);
	ПараметрыВыгрузки.Вставить("Имя",      ИсходныеДанные.ФИОИП.Имя);
	ПараметрыВыгрузки.Вставить("Отчество", ИсходныеДанные.ФИОИП.Отчество);
	
	ПараметрыВыгрузки.Вставить("ПрПодп",      СокрЛП(ИсходныеДанные.ПрПодп));
	ПараметрыВыгрузки.Вставить("НаимОргПред", "");
	ПараметрыВыгрузки.Вставить("НаимДокПред", "");

	ПараметрыВыгрузки.Вставить("ФамилияПодписанта",  ИсходныеДанные.ФИОПодписанта.Фамилия);
	ПараметрыВыгрузки.Вставить("ИмяПодписанта",      ИсходныеДанные.ФИОПодписанта.Имя);
	ПараметрыВыгрузки.Вставить("ОтчествоПодписанта", ИсходныеДанные.ФИОПодписанта.Отчество);
	
	ПараметрыВыгрузки.Вставить("ИмяКлючевогоУзлаСодержательнойЧасти", "ПерсСвФЛ");
	
	ПараметрыВыгрузки.Вставить("ИдФайл", ИдентификаторФайлаВыгрузки(ПараметрыВыгрузки, "NO_PERSSVFL"));
	
	Возврат ПараметрыВыгрузки;
	
КонецФункции 

Функция ИдентификаторФайлаВыгрузки(ПараметрыВыгрузки, ПрефиксИмени)
	
	Если ПараметрыВыгрузки.ЭтоЮЛ Тогда
		ИННКПП = СокрЛП(ПараметрыВыгрузки.ИННОрг) + СокрЛП(ПараметрыВыгрузки.КПП);
	Иначе
		ИННКПП = СокрЛП(ПараметрыВыгрузки.ИННОрг);
	КонецЕсли;
	
	ИдентификаторФайла = ПрефиксИмени
		+ "_" + ?(ЗначениеЗаполнено(ПараметрыВыгрузки.КодИФНСПосредника),
				ПараметрыВыгрузки.КодИФНСПосредника, ПараметрыВыгрузки.КодНО)
		+ "_" + ПараметрыВыгрузки.КодНО
		+ "_" + ИННКПП
		+ "_" + Формат(ТекущаяДатаСеанса(), "ДФ=ггггММдд")
		+ "_" + Строка(Новый УникальныйИдентификатор);
	
	Возврат ИдентификаторФайла;
	
КонецФункции

Функция ИзвлечьСтруктуруXML() Экспорт
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Код");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Формат");
	ДеревоСтруктуры.Колонки.Добавить("МинРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("МаксРазмерность");
	ДеревоСтруктуры.Колонки.Добавить("Обязательность");
	ДеревоСтруктуры.Колонки.Добавить("Многостраничность");
	ДеревоСтруктуры.Колонки.Добавить("Многострочность");
	ДеревоСтруктуры.Колонки.Добавить("Раздел");
	ДеревоСтруктуры.Колонки.Добавить("Ключ");
	ДеревоСтруктуры.Колонки.Добавить("Условие");
	ДеревоСтруктуры.Колонки.Добавить("ЗначениеПоУмолчанию");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	ДеревоСтруктуры.Колонки.Добавить("Представление");
	ДеревоСтруктуры.Колонки.Добавить("Показатели");
	
	Макет = ЭтотОбъект.ПолучитьМакет("СхемаВыгрузки501");
	
	ВысотаТаблицы = Макет.ВысотаТаблицы;
	
	УчтенныеГруппы = Новый Соответствие;
	
	Для Уровень = 0 По Макет.КоличествоУровнейГруппировокСтрок() - 1 Цикл
		Макет.ПоказатьУровеньГруппировокСтрок(Уровень);
		Для НомерСтроки = 2 По ВысотаТаблицы Цикл
			НомСтр = ВысотаТаблицы - НомерСтроки + 2;
			Если Макет.Область(НомСтр, 0, НомСтр, 0).Видимость И УчтенныеГруппы.Получить(НомСтр) = Неопределено Тогда
				
				РодительскийУзел = ДеревоСтруктуры;
				Если Уровень <> 0 Тогда
					Для Инд = 1 По НомСтр - 2 Цикл
						Узел = УчтенныеГруппы.Получить(НомСтр - Инд);
						Если Узел <> Неопределено Тогда
							РодительскийУзел = Узел;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				НовСтр = РодительскийУзел.Строки.Вставить(0);
				НовСтр.Код = СокрЛП(Макет.Область(НомСтр, 1, НомСтр, 1).Текст);
				НовСтр.Раздел = СокрЛП(Макет.Область(НомСтр, 2, НомСтр, 2).Текст);
				НовСтр.Ключ = СокрЛП(Макет.Область(НомСтр, 3, НомСтр, 3).Текст);
				НовСтр.Тип = СокрЛП(Макет.Область(НомСтр, 4, НомСтр, 4).Текст);
				НовСтр.Формат = СокрЛП(Макет.Область(НомСтр, 5, НомСтр, 5).Текст);
				МинРазмерность = СокрЛП(Макет.Область(НомСтр, 6, НомСтр, 6).Текст);
				НовСтр.МинРазмерность = ?(ПустаяСтрока(МинРазмерность), ?(НовСтр.Формат = "N", 99999, 0), Число(МинРазмерность));
				МаксРазмерность = СокрЛП(Макет.Область(НомСтр, 7, НомСтр, 7).Текст);
				НовСтр.МаксРазмерность = ?(ПустаяСтрока(МаксРазмерность), 99999, Число(МаксРазмерность));
				НовСтр.Обязательность = СокрЛП(Макет.Область(НомСтр, 8, НомСтр, 8).Текст);
				НовСтр.Многостраничность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 9, НомСтр, 9).Текст);
				НовСтр.Многострочность = НЕ ПустаяСтрока(Макет.Область(НомСтр, 10, НомСтр, 10).Текст);
				НовСтр.Условие = СокрЛП(Макет.Область(НомСтр, 11, НомСтр, 11).Текст);
				НовСтр.ЗначениеПоУмолчанию = СокрЛП(Макет.Область(НомСтр, 12, НомСтр, 12).Текст);
				НовСтр.Представление = СокрЛП(Макет.Область(НомСтр, 13, НомСтр, 13).Текст);
				
				УчтенныеГруппы.Вставить(НомСтр, НовСтр);
				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
