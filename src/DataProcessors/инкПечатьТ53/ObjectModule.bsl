
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Получить печатный документ ведомости на сервере
//
// Параметры:
//  ДокументВедомость	 - Ссылка	 - Ссылка на документ (источник данных);
// 
// Возвращаемое значение:
//  ТабличныйДокумент - Кадровый отчет Т-53 для печати;
//
Функция ПолучитьПечатныйДокументВедомостиНаСервере(ДокументВедомость) Экспорт
	
	МакетВедомости = ПолучитьОбщийМакет("инкПФ_MXL_Т53"); 
	ВедомостьТабличныйДокумент = Новый ТабличныйДокумент;
	
	СуммаДокумента = ДокументВедомость.СуммаДокумента;
	СуммаДокументаРубли = Цел(СуммаДокумента);
    СуммаДокументаКопейки = Прав(Строка(100+(СуммаДокумента-СуммаДокументаРубли)*100),2);
	
	СуммаДокументаПрописью 		= инкОтчетыСервер.ЧислоПрописьюРублиКопейки(СуммаДокумента); 
	СуммаДепонированоПрописью 	= инкОтчетыСервер.ЧислоПрописьюРублиКопейки(0);
	
	РуководительДанные = Справочники.инкОрганизации.КадровыеДанныеРуководителя(ДокументВедомость.Организация);
	БухгалтерДанные  = Справочники.инкОрганизации.КадровыеДанныеБухгалтера(ДокументВедомость.Организация);
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ШапкаДокумента");
	ОбластьДокумента.Параметры.КодПоОКПО 			= ДокументВедомость.Организация.ОКПО; 	
	ОбластьДокумента.Параметры.НазваниеОрганизации	= ДокументВедомость.Организация;						
	ОбластьДокумента.Параметры.Подразделение		= ДокументВедомость.Подразделение;					
	ОбластьДокумента.Параметры.СуммаДок				= СуммаДокументаПрописью;						
	ОбластьДокумента.Параметры.СуммаДокРублей      	= СуммаДокументаРубли;
	ОбластьДокумента.Параметры.СуммаДокКопеек       = СуммаДокументаКопейки;
	ОбластьДокумента.Параметры.РуководительДолжность= РуководительДанные.Должность;	
	ОбластьДокумента.Параметры.Руководитель			= РуководительДанные.ФИОСокращенное;
	ОбластьДокумента.Параметры.ГлавныйБухгалтер		= БухгалтерДанные.ФИОСокращенное;
	
	// ПЛАТЕЖНАЯ ВЕДОМОСТЬ:
	ОбластьДокумента.Параметры.Номер	= ДокументВедомость.Номер;	
	ОбластьДокумента.Параметры.Дата	    = ДокументВедомость.Дата;

	ОбластьДокумента.Параметры.ОтчетныйПериодС	= НачалоМесяца(ДокументВедомость.ДатаВыплаты);
	ОбластьДокумента.Параметры.ОтчетныйПериодПо = КонецМесяца(ДокументВедомость.ДатаВыплаты);
	Если ДокументВедомость.СпособВыплатыЗарплаты = Перечисления.инкСпособыВыплатыЗарплаты.Аванс Тогда
		ОбластьДокумента.Параметры.ОтчетныйПериодПо = НачалоМесяца(ДокументВедомость.ДатаВыплаты) + 60*60*24*14;	
	КонецЕсли;
	
	ВедомостьТабличныйДокумент.Вывести(ОбластьДокумента);
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("Шапка");
	ВедомостьТабличныйДокумент.Вывести(ОбластьДокумента);
	
	Для каждого ЗарплатаСтрока Из ДокументВедомость.Зарплата Цикл
		ОбластьДокумента = МакетВедомости.ПолучитьОбласть("Строка");
		ОбластьДокумента.Параметры.НомерСтроки		= ЗарплатаСтрока.НомерСтроки;
		ОбластьДокумента.Параметры.ТабельныйНомер   = ЗарплатаСтрока.Сотрудник.ТабельныйНомер;
		ОбластьДокумента.Параметры.Физлицо			= ЗарплатаСтрока.Сотрудник.ФИОСокращенное;
		ОбластьДокумента.Параметры.Сумма	        = ЗарплатаСтрока.КВыплате;
		ОбластьДокумента.Параметры.ЗаписьОДепонировании	= "";
		ВедомостьТабличныйДокумент.Вывести(ОбластьДокумента);
	КонецЦикла;
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ИтогПоЛисту");
	ОбластьДокумента.Параметры.ИтогоНаСтранице = СуммаДокумента;	
	ВедомостьТабличныйДокумент.Вывести(ОбластьДокумента);
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("Подвал");  
	ОбластьДокумента.Параметры.КоличествоЛистов 	= 1;
	ОбластьДокумента.Параметры.ВсегоВыплачено		= "";								
	ОбластьДокумента.Параметры.ВсегоДепонировано	= "";									
	ОбластьДокумента.Параметры.КассирДолжность		= "Кассир";	
	ОбластьДокумента.Параметры.Кассир				= БухгалтерДанные.ФИОСокращенное;
	ОбластьДокумента.Параметры.Бухгалтер		 	= БухгалтерДанные.ФИОСокращенное;
	ВедомостьТабличныйДокумент.Вывести(ОбластьДокумента);
	
	ВедомостьТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ВедомостьТабличныйДокумент; 
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий
//
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//
#КонецОбласти

#КонецЕсли
