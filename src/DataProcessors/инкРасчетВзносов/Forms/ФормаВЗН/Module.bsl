
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДаннымиПоУмолчаниюНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетВзносов(Команда)
	
	КлючеваяОперация = "Обработка.инкРасчетВзносов.ФормированиеОтчетаВзносыВФонды";
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ПараметрыФормы = ПолучитьРасчетВзносовНаСервере();
	
	Если ЭтоВнешняяОбработкаОтчет() Тогда
		ОткрытьФорму("ВнешняяОбработка.инкРасчетВзносов.Форма.ФормаОтчетаАналитика", ПараметрыФормы, , Истина);
	Иначе
		ОткрытьФорму("Обработка.инкРасчетВзносов.Форма.ФормаОтчетаАналитика", ПараметрыФормы, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация",Организация);
	
	Оповещение = Новый ОписаниеОповещения("мПодразделениеНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.инкПодразделения.ФормаВыбора", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура мПодразделениеНачалоВыбораЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРедактирования) Тогда
		Подразделение = РезультатРедактирования;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация",Организация);
	ПараметрыОткрытия.Вставить("Подразделение",Подразделение);
	
	Оповещение = Новый ОписаниеОповещения("СотрудникНачалоВыбораЗавершение", ЭтотОбъект);

	ОткрытьФорму("Справочник.инкСотрудники.ФормаВыбора", 
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбораЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРедактирования) Тогда
		Сотрудник = РезультатРедактирования;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДаннымиПоУмолчаниюНаСервере()

	Организация	= Справочники.инкОрганизации.ПолучитьЕдинственнуюОрганизацию();
	ПериодРасчетаВзносов.ДатаНачала		= НачалоГода(ТекущаяДата());
	ПериодРасчетаВзносов.ДатаОкончания  = КонецГода(ТекущаяДата());
	
КонецПроцедуры

&НаСервере
Функция  ПолучитьРасчетВзносовНаСервере()
	
	МенеджерРасчета = РеквизитФормыВЗначение("Объект");
	//
	МенеджерРасчета.Инициализация();
	//
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Организация = Организация;
	Если ЗначениеЗаполнено(Подразделение) Тогда
		МенеджерРасчета.ДанныеДляРасчетаВзносов.Подразделения.Добавить(Подразделение);
	КонецЕсли;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата1 = ПериодРасчетаВзносов.ДатаНачала;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата2 = ПериодРасчетаВзносов.ДатаОкончания;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.СформироватьТабличныеДокументы = Истина;
	//
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		МенеджерРасчета.ДанныеДляРасчетаВзносов.Сотрудники.Добавить(Сотрудник);
	КонецЕсли;
	
	//
	МенеджерРасчета.ВыполнитьРасчетВзносов();
	//         
	
	ДанныеСтруктура = Новый Структура;
	ДанныеСтруктура.Вставить("ТабличныеДокументы",МенеджерРасчета.ТабличныеДокументы);
	ДанныеСтруктура.Вставить("ДанныеДляРасчетаВзносов",МенеджерРасчета.ДанныеДляРасчетаВзносов);
	
	Возврат ДанныеСтруктура; 
	  	
КонецФункции

&НаСервере
Функция ЭтоВнешняяОбработкаОтчет()
	
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	Возврат инкОбщийСервер.ЭтоВнешняяОбработкаОтчет(ОбъектОбработка);
	
КонецФункции

#КонецОбласти

