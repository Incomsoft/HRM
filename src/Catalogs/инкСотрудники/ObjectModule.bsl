
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
    ПроверкаРегистрацииПрограммы(Отказ);	
	ЗаполнениеРеквизитовОбъекта(Отказ);
	
КонецПроцедуры    

Процедура ПриЗаписи(Отказ)
	
	Если НЕ ЗначениеЗаполнено(ТабельныйНомер) Тогда
		Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	ОчиститьРегистрыСведений();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьРегистрыСведений()
	
	ОчиститьРегистрСведений("инкТабельУчетаРабочегоВремени");	
	ОчиститьРегистрСведений("инкПостоянныеНачисления");	
	ОчиститьРегистрСведений("инкПостоянныеУдержания");	
		
КонецПроцедуры

Процедура ОчиститьРегистрСведений(НаименованиеСтрока)
	
	НаборЗаписей = РегистрыСведений[НаименованиеСтрока].СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Сотрудник.Установить(Ссылка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
		
КонецПроцедуры

Процедура ПроверкаРегистрацииПрограммы(Отказ)

	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		
		Если НЕ инкОбщийСервер.ПроверкаРегистрацииПрограммы() Тогда

			СписокСотрудников = Справочники.инкСотрудники.ПолучитьСписокСотрудников(); 
			Если СписокСотрудников.Количество() >= 5 Тогда
				Отказ = Истина;
				инкОбщийКлиентСервер.СообщитьПользователю("Ограничение демо версии (одна организация и до 5-ти сотрудников). Пожалуйста зарегистрируйте программу!");
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли;	
		
КонецПроцедуры

Процедура ЗаполнениеРеквизитовОбъекта(Отказ)
	
	// Заполнение табельного номера:
	Если Не ЗначениеЗаполнено(ЭтотОбъект.ТабельныйНомер) Тогда
		ЭтотОбъект.ТабельныйНомер = ЭтотОбъект.Код;
	КонецЕсли;
	
	ЗаполнитьФИО();

	// Имущественный вычет:
	Если ЭтотОбъект.ИмущественныйВычетСумма <> 0 Тогда
		Если Не ЗначениеЗаполнено(ЭтотОбъект.ИмущественныйВычетКодВычета) Тогда
			Отказ = Истина;
			инкОбщийКлиентСервер.СообщитьПользователю("Внимание! У имущественного вычета должен быть указан код вычета.");
		КонецЕсли;
	КонецЕсли; 
		
КонецПроцедуры

Процедура ЗаполнитьФИО() Экспорт

    Если ЭтотОбъект.ЭтоГруппа Тогда
    	Возврат;
    КонецЕсли; 
    
	Если ЗначениеЗаполнено(ЭтотОбъект.Наименование) Тогда
		
		СтруктураФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(Наименование);
		
		ЭтотОбъект.Фамилия = СтруктураФИО.Фамилия;
		ЭтотОбъект.Имя = СтруктураФИО.Имя;
		ЭтотОбъект.Отчество = СтруктураФИО.Отчество;
		
		Если ЗначениеЗаполнено(ЭтотОбъект.Фамилия) Тогда
			
			ЭтотОбъект.ФИОСокращенное = ЭтотОбъект.Фамилия;
			
			Если ЗначениеЗаполнено(ЭтотОбъект.Имя) Тогда
				ЭтотОбъект.ФИОСокращенное = ЭтотОбъект.ФИОСокращенное + " "
				                          + Лев(ЭтотОбъект.Имя,1)
										  + ".";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЭтотОбъект.Отчество) Тогда
				ЭтотОбъект.ФИОСокращенное = ЭтотОбъект.ФИОСокращенное + " "
				                          + Лев(ЭтотОбъект.Отчество,1)
										  + ".";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЭтотОбъект.ФИО = ЭтотОбъект.Наименование;	
	
КонецПроцедуры

Функция ПолучитьКадровыеДанныеСотрудникаВСтруктуру(Сотрудник,Дата = Неопределено) Экспорт
	
	Возврат инкКадровыйУчетСервер.ПолучитьКадровыеДанныеСотрудникаВСтруктуру(Сотрудник,Дата);
		
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли

