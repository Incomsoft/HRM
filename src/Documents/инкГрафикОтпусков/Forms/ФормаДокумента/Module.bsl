
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗаполнитьЗначениямиПоУмолчанию();

КонецПроцедуры       

&НаКлиенте
Процедура ГрафикиОснованиеПриИзменении(Элемент)
	
	ТекущиеДанные = ЭтотОбъект.Элементы.Графики.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;                        
	
	ДанныеДляЗаполненияГрафика = ПолучитьДанныеДляЗаполненияГрафикаНаСервере(ТекущиеДанные.ОснованиеДокумент);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные,ДанныеДляЗаполненияГрафика);
	
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаПлан) Тогда
		ТекущиеДанные.ДатаПлан = ТекущиеДанные.ДатаФакт;
	КонецЕсли;
	
	Если ТекущиеДанные.ДатаПлан <> ТекущиеДанные.ДатаФакт Тогда
		ТекущиеДанные.ПереносОтпуска = ТекущиеДанные.ДатаФакт;
	КонецЕсли;
 	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОснованиеИзКадровыхДокументов(Команда)
	ЗаполнитьОснованиеИзКадровыхДокументовНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОснованиеИзКадровыхДокументовНаСервере()
	
	Если Объект.Графики.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкРеестрКадровыхДокументов.Регистратор.Ссылка КАК ОснованиеДокумент,
		|	инкРеестрКадровыхДокументов.Регистратор.ПериодС КАК ДатаФакт,
		|	инкРеестрКадровыхДокументов.Сотрудник КАК Сотрудник,
		|	инкРеестрКадровыхДокументов.Регистратор.КоличествоДнейОтпуска КАК КалендарныеДни
		|ИЗ
		|	РегистрСведений.инкРеестрКадровыхДокументов КАК инкРеестрКадровыхДокументов
		|ГДЕ
		|	инкРеестрКадровыхДокументов.ДатаПриказа МЕЖДУ &Дата1 И &Дата2
		|	И инкРеестрКадровыхДокументов.Сотрудник В(&СотрудникиМассив)
		|	И ТИПЗНАЧЕНИЯ(инкРеестрКадровыхДокументов.Регистратор) = ЗНАЧЕНИЕ(Документ.инкПриказНаОтпуск)";

	
	Запрос.УстановитьПараметр("Дата1", НачалоГода(Объект.Период));
	Запрос.УстановитьПараметр("Дата2", КонецГода(Объект.Период));
	
	СотрудникиМассив = Объект.Графики.Выгрузить().ВыгрузитьКолонку("Сотрудник");
	Запрос.УстановитьПараметр("СотрудникиМассив", СотрудникиМассив);
	
	КадровыеДокументыТаблица = Запрос.Выполнить().Выгрузить();
	
    Для каждого ГрафикСтрока Из Объект.Графики Цикл
		
		Если НЕ ЗначениеЗаполнено(ГрафикСтрока.ОснованиеДокумент) И ЗначениеЗаполнено(ГрафикСтрока.ДатаПлан) Тогда
			
			ПоискДокумента = Новый Структура;
			ПоискДокумента.Вставить("Сотрудник");
			
			ЗаполнитьЗначенияСвойств(ПоискДокумента,ГрафикСтрока);
			КадровыеДокументыМассив = КадровыеДокументыТаблица.НайтиСтроки(ПоискДокумента);
			
			Для каждого ЭлементыМассива Из КадровыеДокументыМассив Цикл
				
				Дата1 = ГрафикСтрока.ДатаПлан - 60*60*24*15;
				Дата2 = ГрафикСтрока.ДатаПлан + 60*60*24*15;
				Если (ЭлементыМассива.ДатаФакт >= Дата1) И (ЭлементыМассива.ДатаФакт <= Дата2) Тогда
					
					ЗаполнитьЗначенияСвойств(ГрафикСтрока,ЭлементыМассива);  
					Если ГрафикСтрока.ДатаПлан <> ГрафикСтрока.ДатаФакт Тогда
						ГрафикСтрока.ПереносОтпуска = ГрафикСтрока.ДатаФакт;
					КонецЕсли;   
					
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДляЗаполненияГрафикаНаСервере(ДокументОтпуск)
	
	ДанныеДляГрафика = Новый Структура;
	ДанныеДляГрафика.Вставить("ДатаФакт"); 
	ДанныеДляГрафика.Вставить("КалендарныеДни"); 
	
	ЗаполнитьЗначенияСвойств(ДанныеДляГрафика,ДокументОтпуск);
	ДанныеДляГрафика.ДатаФакт = ДокументОтпуск.ПериодС; 
	ДанныеДляГрафика.КалендарныеДни = ДокументОтпуск.КоличествоДнейОтпуска;
	
	Возврат ДанныеДляГрафика;	
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗначениямиПоУмолчанию()

	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Справочники.инкОрганизации.ПолучитьЕдинственнуюОрганизацию();
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
    	Объект.Период = НачалоГода(ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры
		  
#Область о // Заполнить:

&НаКлиенте
Процедура Заполнить(Команда)  
	
	ЗадатьВопросОПерезаполненииДокумента();
	
КонецПроцедуры                             

&НаКлиенте
Процедура ЗадатьВопросОПерезаполненииДокумента()

	ТекстВопроса = НСтр("ru = 'Перезаполнить документ данными?'");
	Оповещение = Новый ОписаниеОповещения("ЗадатьВопросОПерезаполненииДокументаЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗадатьВопросОПерезаполненииДокументаЗавершение(Знач Результат, Знач ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда

		ЗаполнитьНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	МенеджерЗаполнения = РеквизитФормыВЗначение("Объект");	
	МенеджерЗаполнения.ЗаполнитьНачальнымиДанными();
	ЗначениеВРеквизитФормы(МенеджерЗаполнения,"Объект");
	инкОбщийКлиентСервер.СообщитьПользователю("Данные документа перезаполнены");
	
КонецПроцедуры

#КонецОбласти

#Область о // Печать:

&НаКлиенте
Процедура ГрафикОтпусковТ7(Команда)
	
	ТабличныйДокумент = ПолучитьТабличныйДокументТ7(); 
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;   
	ТабличныйДокумент.АвтоМасштаб = Истина;        
	ТабличныйДокумент.ПолеСлева = 20;
	ТабличныйДокумент.ПолеСправа = 0;
	инкОтчетыКлиент.ПечатьТабличногоДокумента(ТабличныйДокумент,"",ЭтаФорма);

КонецПроцедуры    

&НаСервере
Функция ПолучитьТабличныйДокументТ7()
		
	МенеджерПечати = РеквизитФормыВЗначение("Объект");
	ТабличныйДокумент = МенеджерПечати.СформироватьТабличныйДокументТ7();
	
	Возврат ТабличныйДокумент; 
	
КонецФункции 

#КонецОбласти

#КонецОбласти
		  

