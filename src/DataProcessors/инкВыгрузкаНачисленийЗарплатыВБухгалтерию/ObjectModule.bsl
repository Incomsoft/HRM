
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область о //Регистрация отчета:

// Функция - Сведения о внешней обработке
// 
// Возвращаемое значение:
// 	Структура - Параметры регистрации;
//
Функция СведенияОВнешнейОбработке() Экспорт
	
	ИмяОтчета = ЭтотОбъект.Метаданные().Имя; 
    Синоним = ЭтотОбъект.Метаданные().Синоним; 
    Синоним = ?(ЗначениеЗаполнено(Синоним),Синоним, ИмяОтчета);         
	
	ПараметрыРегистрации = Новый Структура;
    ПараметрыРегистрации.Вставить("Вид", "ДополнительнаяОбработка");

    ПараметрыРегистрации.Вставить("Наименование", Синоним);
    ПараметрыРегистрации.Вставить("Версия", "1.0");
    ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
    ПараметрыРегистрации.Вставить("Информация", "Обработка: "+Синоним);
    ТаблицаКоманд = ПолучитьТаблицуКоманд();
    ДобавитьКоманду(ТаблицаКоманд, Синоним, "1", "ОткрытиеФормы", Истина, "");
    ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Функция ПолучитьТаблицуКоманд()
	
    Команды = Новый ТаблицаЗначений;
    Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
    Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
    Возврат Команды;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
    НоваяКоманда.Представление = Представление;
    НоваяКоманда.Идентификатор = Идентификатор;
    НоваяКоманда.Использование = Использование;
    НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
    НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

#КонецОбласти

#Область о // Выгрузка данных:

// Функция - Выгрузить данные в файл на сервере
// 
// Возвращаемое значение:
// 	Строка - Адрес файла;
//
Функция ВыгрузитьДанныеВФайлНаСервере() Экспорт
    
    АдресФайла = Неопределено;
    
    Попытка
        
        ВремФайл = ПолучитьИмяВременногоФайла("xml");
        МакетПравилОбмена = ПолучитьМакет("ПравилаВыгрузкиДанных");
		МакетПравилОбмена.Записать(ВремФайл);
        
        ИмяФайлаВыгрузки = ПолучитьИмяВременногоФайла("xml");
        
        Обработка = Обработки.УниверсальныйОбменДаннымиXML.Создать();
        Обработка.РежимОбмена="Выгрузка";
        Обработка.ИмяФайлаПравилОбмена = ВремФайл; 
        Обработка.ЗагружатьДанныеВРежимеОбмена = Истина;
        Обработка.ЗаписыватьРегистрыНаборамиЗаписей = Истина;
        Обработка.ЗапоминатьЗагруженныеОбъекты = Истина;
        Обработка.ИспользоватьОтборПоДатеДляВсехОбъектов = Истина;
        Обработка.ВыгружатьТолькоРазрешенные = Истина;
        Обработка.ИмяФайлаОбмена = ИмяФайлаВыгрузки;
        Обработка.ТипУдаленияРегистрацииИзмененийДляУзловОбменаПослеВыгрузки = 0; // 0 - не снимать регистрацию, 1 - снимать регистрацию
        Обработка.ЗагрузитьПравилаОбмена();
        //
		//Обработка.Параметры.Дата1 = Объект.ПериодВыгрузки.ДатаНачала;
		//Обработка.Параметры.Дата2 = Объект.ПериодВыгрузки.ДатаОкончания;  
		Для каждого СтрокаПараметров Из Обработка.ТаблицаНастройкиПараметров Цикл
			Если ВРег(СтрокаПараметров.Имя) = ВРег("Дата1") Тогда
				СтрокаПараметров.Значение = ЭтотОбъект.ПериодВыгрузки.ДатаНачала;
			КонецЕсли;
			Если ВРег(СтрокаПараметров.Имя) = ВРег("Дата2") Тогда
				СтрокаПараметров.Значение = ЭтотОбъект.ПериодВыгрузки.ДатаОкончания;
			КонецЕсли;
		КонецЦикла;
        //
        Обработка.ВыполнитьВыгрузку();    
        
        АдресФайла = ПоместитьВоВременноеХранилище(новый ДвоичныеДанные(ИмяФайлаВыгрузки));

    Исключение
        ОбщегоНазначения.СообщитьПользователю("Ошибка при формировании файла выгрузки. Описание: "+ОписаниеОшибки());
    КонецПопытки; 
    
    Возврат АдресФайла;    
    
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли

