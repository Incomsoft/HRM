
#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Получить данные табеля на сервере
//
// Параметры:
//  ИсходныеДанные	 - Структура 		- Содержит параметры для запроса
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные табеля 
//
Функция ПолучитьДанныеТабеляНаСервере(ИсходныеДанные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	КадроваяИсторияСотрудниковСрезПоследних.Организация КАК Организация,
		|	КадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
		|	КадроваяИсторияСотрудниковСрезПоследних.ВидСобытия КАК ВидСобытия
		|ПОМЕСТИТЬ втСотрудники
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			,
		|			Организация = &Организация
		|				И Подразделение = &Подразделение) КАК КадроваяИсторияСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	инкТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(инкТабельУчетаРабочегоВремени.День, ДЕНЬ) КАК Дата,
		|	инкТабельУчетаРабочегоВремени.ВДнях КАК ВДнях,
		|	инкТабельУчетаРабочегоВремени.ВЧасах КАК ВЧасах
		|ПОМЕСТИТЬ втПроизводственныйКалендарь
		|ИЗ
		|	РегистрСведений.инкТабельУчетаРабочегоВремени КАК инкТабельУчетаРабочегоВремени
		|ГДЕ
		|	инкТабельУчетаРабочегоВремени.День МЕЖДУ &Дата1 И &Дата2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПроизводственныйКалендарь.Сотрудник КАК Сотрудник,
		|	втПроизводственныйКалендарь.Сотрудник.Наименование КАК СотрудникНаименование,
		|	втПроизводственныйКалендарь.Дата КАК Дата,
		|	втПроизводственныйКалендарь.ВДнях КАК ВДнях,
		|	втПроизводственныйКалендарь.ВЧасах КАК ВЧасах
		|ИЗ
		|	втПроизводственныйКалендарь КАК втПроизводственныйКалендарь
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСотрудники КАК втСотрудники
		|		ПО втПроизводственныйКалендарь.Сотрудник = втСотрудники.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	СотрудникНаименование";
	

	инкОбщийСервер.УстановитьПараметрВЗапросе("Организация","Организация = &Организация",ИсходныеДанные,Запрос);
	инкОбщийСервер.УстановитьПараметрВЗапросе("Подразделение","Подразделение = &Подразделение",ИсходныеДанные,Запрос);

	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ИсходныеДанные.МесяцНачисления));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ИсходныеДанные.МесяцНачисления));
	
	тз = Запрос.Выполнить().Выгрузить();
	
	Возврат тз; 
	
КонецФункции

// Функция - Формирует структуру для получения данных табеля. См. ПолучитьДанныеТабеляНаСервере
// 
// Возвращаемое значение:
//  Структура - Данные для табеля;
//
Функция ПолучитьСтруктуруДляДанныхТабеля() Экспорт 
	
	стр = Новый Структура;
	стр.Вставить("Организация");
	стр.Вставить("Подразделение");
	стр.Вставить("МесяцНачисления");
	                         
	Возврат стр;
	
КонецФункции

// Функция - Получить табель таблица
//
// Параметры:
//  ИсходныеДанные	 - Структура	 - Содержит период и ссылку на сотрудника
// 
// Возвращаемое значение:
//   ТаблицаЗначений - Данные по табелю за период
//
Функция ПолучитьТабельТаблица(ИсходныеДанные) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкТабельУчетаРабочегоВремени.Сотрудник КАК Сотрудник,
		|	инкТабельУчетаРабочегоВремени.День КАК День,
		|	инкТабельУчетаРабочегоВремени.Месяц КАК Месяц,
		|	инкТабельУчетаРабочегоВремени.ВДнях КАК ВДнях,
		|	инкТабельУчетаРабочегоВремени.ВЧасах КАК ВЧасах
		|ИЗ
		|	РегистрСведений.инкТабельУчетаРабочегоВремени КАК инкТабельУчетаРабочегоВремени
		|ГДЕ
		|	инкТабельУчетаРабочегоВремени.День МЕЖДУ &Дата1 И &Дата2
		|	И инкТабельУчетаРабочегоВремени.Сотрудник = &Сотрудник";
	
	Запрос.УстановитьПараметр("Дата1", ИсходныеДанные.Дата1);
	Запрос.УстановитьПараметр("Дата2", ИсходныеДанные.Дата2);  
	
	Если НЕ инкОбщийКлиентСервер.ЕстьСвойство(ИсходныеДанные,"Сотрудник") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"инкТабельУчетаРабочегоВремени.Сотрудник = &Сотрудник","Истина");	
	Иначе
		Запрос.УстановитьПараметр("Сотрудник", ИсходныеДанные.Сотрудник); 	
	КонецЕсли;
	
	тз = Запрос.Выполнить().Выгрузить();
	
	Возврат тз;
	
КонецФункции            

#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Код процедур и функций
#КонецОбласти

#Область Инициализация

#КонецОбласти
