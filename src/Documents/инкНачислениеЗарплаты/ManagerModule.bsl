
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// Процедура - Обновление до версии 11.0.2.7:
//
Процедура Обновление_11_0_2_7() Экспорт
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("Дата1",Дата("00010101000000"));
	ИсходныеДанные.Вставить("Дата2",ТекущаяДата());
	
	ПерепровестиВсеДокументыПоПериоду(ИсходныеДанные);  
	инкКадровыйУчетСервер.ПерепровестиВсеКадровыеДокументыЗаПериод(ИсходныеДанные);
	
КонецПроцедуры

// Процедура - Обновление 11.0.2.6:
//
Процедура Обновление_11_0_2_6() Экспорт
	
	ИсходныеДанные = Новый Структура;
	ИсходныеДанные.Вставить("Дата1",Дата("00010101000000"));
	ИсходныеДанные.Вставить("Дата2",ТекущаяДата());
	
	ПерепровестиВсеДокументыПоПериоду(ИсходныеДанные);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Перепровести все документы по периоду
//
// Параметры:
//  ИсходныеДанные - Структура	 - Данные для перепроведения документов:
//		Дата1 - Дата - Дата начала периода;
//		Дата2 - Дата - Дата окончания периода;
//
Процедура ПерепровестиВсеДокументыПоПериоду(ИсходныеДанные,ОтключатьЗащиту = Ложь) Экспорт
	
	ВключенаЗащита = Константы.инкБлокировкаПроведенияДокументовНачисленияЗарплатыЗаднимЧислом.Получить();
	
	Если ВключенаЗащита И ОтключатьЗащиту Тогда
		Константы.инкБлокировкаПроведенияДокументовНачисленияЗарплатыЗаднимЧислом.Установить(Ложь);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНачислениеЗарплаты.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.инкНачислениеЗарплаты КАК инкНачислениеЗарплаты
		|ГДЕ
		|	инкНачислениеЗарплаты.Проведен
		|	И инкНачислениеЗарплаты.Дата МЕЖДУ &Дата1 И &Дата2";
	
	Запрос.УстановитьПараметр("Дата1", ИсходныеДанные.Дата1);
	Запрос.УстановитьПараметр("Дата2", ИсходныеДанные.Дата2);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Попытка
			ДокументОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();	
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);	
		Исключение
			инкОбщийКлиентСервер.СообщитьПользователю("Ошибка при проведении документа: " + ВыборкаДетальныеЗаписи.Ссылка
			                                         +". Описание ошибки: " + ОписаниеОшибки());
		КонецПопытки;
			
	КонецЦикла;
	
	Если ВключенаЗащита И ОтключатьЗащиту Тогда
		Константы.инкБлокировкаПроведенияДокументовНачисленияЗарплатыЗаднимЧислом.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
