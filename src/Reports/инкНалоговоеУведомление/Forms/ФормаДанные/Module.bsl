
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначениямиПоУмолчанию();
	
КонецПроцедуры  

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если НЕ ЗавершениеРаботы Тогда
		СохранитьДанныеЗаПериод();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидУдержанияПриИзменении(Элемент)
	
	УстановитьДоступностьРеквизитов();
	
КонецПроцедуры      

&НаКлиенте
Процедура ПериодУдержанияПриИзменении(Элемент)
	
	СохранитьДанныеЗаПериод();
	ЗагрузитьДанныеЗаПериод();

КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеЗаПериод()

	МенеджерЗаписи = РегистрыСведений.инкНалоговыеУведомления.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Период = ПериодЗаписиРегистра;
	МенеджерЗаписи.Организация 		= Организация;
	МенеджерЗаписи.АвансНДФЛСумма	= АвансНДФЛСумма;
	МенеджерЗаписи.НДФЛСумма  		= НДФЛСумма;
	МенеджерЗаписи.ВзносыСумма 		= ВзносыСумма;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры     

&НаСервере
Процедура ЗаполнитьЗначениямиПоУмолчанию()
	
	Организация = Справочники.инкОрганизации.ПолучитьТекущуюОрганизацию();
	
	ПериодУдержания.ДатаНачала   	= НачалоМесяца(ТекущаяДата());
	ПериодУдержания.ДатаОкончания	= КонецМесяца(ТекущаяДата());  
	
	ВидУдержания = "ЗаПервуюПоловинуМесяца";  
	
	ЗагрузитьДанныеЗаПериод();
	УстановитьДоступностьРеквизитов();
	
КонецПроцедуры                        

&НаСервере
Процедура ЗагрузитьДанныеЗаПериод()
	
	ПериодЗаписиРегистра = ПериодУдержания.ДатаНачала;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНалоговоеУведомлениеСрезПоследних.Период КАК Период,
		|	инкНалоговоеУведомлениеСрезПоследних.АвансНДФЛСумма КАК АвансНДФЛСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.ВзносыСумма КАК ВзносыСумма,
		|	инкНалоговоеУведомлениеСрезПоследних.НДФЛСумма КАК НДФЛСумма
		|ИЗ
		|	РегистрСведений.инкНалоговыеУведомления.СрезПоследних(&ПериодОтчета, Организация = &Организация) КАК инкНалоговоеУведомлениеСрезПоследних";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодОтчета", НачалоМесяца(ПериодУдержания.ДатаНачала));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		АвансНДФЛСумма	= ВыборкаДетальныеЗаписи.АвансНДФЛСумма;
		НДФЛСумма  = ВыборкаДетальныеЗаписи.НДФЛСумма;
		ВзносыСумма = ВыборкаДетальныеЗаписи.ВзносыСумма;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьРеквизитов()
	
	ЗПЗаПервуюПоловинуМесяца = (ВидУдержания = "ЗаПервуюПоловинуМесяца");

	Элементы.СуммаАвансовыхПлатежей.Доступность = ЗПЗаПервуюПоловинуМесяца;
	Элементы.СуммаНалога.Доступность 			= НЕ ЗПЗаПервуюПоловинуМесяца;
	Элементы.СуммаСтраховыхВзносов.Доступность 	= НЕ ЗПЗаПервуюПоловинуМесяца;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеПоНалогу(Команда)
	
	ТекстВопроса = НСтр("ru = 'Внимание! Текущие данные перезапшутся. Продолжить?'");
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьДанныеПоНалогуЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры                           

&НаКлиенте
Процедура ЗагрузитьДанныеПоНалогуЗавершение(Результат, ИмяТабЧасти) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда

		ЗагрузитьДанныеПоНачисленномуНалогуЗаПериод();
		ЗагрузитьДанныеПоНачисленнымВзносамЗаПериод();
		
	КонецЕсли;
	
КонецПроцедуры                                        

&НаСервере
Процедура ЗагрузитьДанныеПоНачисленнымВзносамЗаПериод()
	
	Если ВидУдержания = "ЗаПервуюПоловинуМесяца" Тогда
    	Возврат;
	КонецЕсли;
	
	МенеджерРасчета = Обработки.инкРасчетВзносов.Создать();
	//
	МенеджерРасчета.Инициализация();
	//
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Организация = Организация;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата1 = НачалоГода(ПериодУдержания.ДатаНачала);
	МенеджерРасчета.ДанныеДляРасчетаВзносов.Дата2 = КонецМесяца(ПериодУдержания.ДатаОкончания);
	МенеджерРасчета.ДанныеДляРасчетаВзносов.СформироватьТабличныеДокументы = Ложь;
	МенеджерРасчета.ДанныеДляРасчетаВзносов.ВидОтчета = "Свод";
	
	//
	МенеджерРасчета.ВыполнитьРасчетВзносов();
	//         
	
	ВзносыСумма = 0;
	Для каждого ВзносыСтрока Из МенеджерРасчета.СводныеДанные.Взносы.ВзносыСводТаблица_Месяц Цикл
		
		Если ВзносыСтрока.Период = НачалоМесяца(ПериодУдержания.ДатаНачала) Тогда
			ВзносыСумма = ВзносыСтрока.СФРИтого;	
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеПоНачисленномуНалогуЗаПериод()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	инкНалогУдержанныйОбороты.Организация КАК Организация,
		|	инкНалогУдержанныйОбороты.СуммаНалогаОборот КАК СуммаНалогаОборот
		|ИЗ
		|	РегистрНакопления.инкНалогУдержанный.Обороты(&Дата1, &Дата2, Месяц, Организация = &Организация) КАК инкНалогУдержанныйОбороты";
	
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ПериодУдержания.ДатаНачала));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ПериодУдержания.ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВидУдержания = "ЗаПервуюПоловинуМесяца" Тогда
			АвансНДФЛСумма = ВыборкаДетальныеЗаписи.СуммаНалогаОборот;
		Иначе
			НДФЛСумма = ВыборкаДетальныеЗаписи.СуммаНалогаОборот
			          - АвансНДФЛСумма;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	СохранитьДанныеЗаПериод();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организация",Организация);
	ПараметрыОткрытия.Вставить("ПериодУдержания",ПериодУдержания);
	ПараметрыОткрытия.Вставить("ВидУдержания",ВидУдержания);

	ПараметрыОткрытия.Вставить("НалогСумма",0);
	ПараметрыОткрытия.Вставить("ВзносыСумма",0);

	Если ВидУдержания = "ЗаПервуюПоловинуМесяца" Тогда
		
		ПараметрыОткрытия.НалогСумма	= АвансНДФЛСумма;

	Иначе
		
		ПараметрыОткрытия.НалогСумма	= НДФЛСумма;
		ПараметрыОткрытия.ВзносыСумма	= ВзносыСумма;
		
	КонецЕсли;
	
	ОткрытьФорму("Отчет.инкНалоговоеУведомление.Форма.ФормаНалоговыеУведомления",ПараметрыОткрытия,,Новый УникальныйИдентификатор);
	//ОткрытьФорму("ВнешнийОтчет.инкНалоговоеУведомление.Форма.ФормаНалоговоеУведомление",ПараметрыОткрытия,,Новый УникальныйИдентификатор);
	
КонецПроцедуры



