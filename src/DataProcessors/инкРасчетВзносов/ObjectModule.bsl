
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Параметры:
//	ДанныеДляРасчетаВзносов - структура, содержит информацию для расчета взносов:
// 		Организация  - ссылка, справочник инкОрганизации;
//		Подразделения - массив, справочник инкПодразделения;
//		Дата1 - дата, начало расчетного периода;
// 		Дата2 - дата, окончание расчетного периода;
//		СортировкаПоСотрудникам - булево, сортировать табличную часть Сотрудники.
//		Сотрудники - массив, список сотрудников для расчета (отладки);
//		СформироватьТабличныеДокументы - булево, признак формирования табличных документово;
//		ВидОтчета - Строка - Вид отчета по взносам;
//
//	РезультатРасчета - структура, содержит результирующие таблицы расчета взносов:
// 		ВзносыТаблица_Месяц		- расчет взносов по-месячно;	
//  	ВзносыТаблица_Квартал	- расчет взносов по-квартально;
//  	ВзносыТаблица_Год		- расчет взносов за год (весь период);
//
//  СводныеДанные  - структура, содержит результирующие таблицы расчета взносов:
//		Взносы  					- расчет взносов по периодам
//      Численность					- расчет численности в разрезе по периодам;
//		Пособия						- расчет пособий за период (для отчета);
//
//	ТабличныеДокументы - структура, содержит табличные документы для вывода на печать:	
//  	ВзносыСортировкаПоОблагаемойБазе 	- табличный документ, отчет: ВЗН. 
//		ВзносыСортировкаПоФИО 				- табличный документ, отчет: ВЗН.
//		Инвалиды                            - табличный документ, отчет: ВЗН.
//		СводПоСтраховымВзносам				- табличный документ, отчет: ВЗН (свод).

Перем ДанныеДляРасчетаВзносов Экспорт;	// Структура - Состав структуры см. описание в модуле объекта;
Перем РезультатРасчета Экспорт;			// Структура - Состав структуры см. описание в модуле объекта;	
Перем ТабличныеДокументы Экспорт;		// Структура - Состав структуры см. описание в модуле объекта;
Перем СводныеДанные Экспорт;	        // Структура - Состав структуры см. описание в модуле объекта;

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область о // Инициализация:

// Процедура - Инициализация
//
Процедура Инициализация() Экспорт
	
	ДанныеДляРасчетаВзносов = Новый Структура;
	ДанныеДляРасчетаВзносов.Вставить("Организация",Справочники.инкОрганизации.ПустаяСсылка());
	ДанныеДляРасчетаВзносов.Вставить("Подразделения",Новый Массив);
	ДанныеДляРасчетаВзносов.Вставить("Дата1",НачалоГода(ТекущаяДата()));
	ДанныеДляРасчетаВзносов.Вставить("Дата2",КонецГода(ТекущаяДата()));
	ДанныеДляРасчетаВзносов.Вставить("СортировкаПоСотрудникам",Истина); 
	ДанныеДляРасчетаВзносов.Вставить("Сотрудники",Новый Массив);
	ДанныеДляРасчетаВзносов.Вставить("СформироватьТабличныеДокументы",Ложь);
	ДанныеДляРасчетаВзносов.Вставить("ВидОтчета","");
	
	РезультатРасчета = Новый Структура;
	РезультатРасчета.Вставить("ВзносыТаблица_Месяц",ПолучитьСтруктуруВзносы_Месяц());
	РезультатРасчета.Вставить("ВзносыТаблица_Квартал");
	РезультатРасчета.Вставить("ВзносыТаблица_Год");
	РезультатРасчета.Вставить("Регресс_Взносы",ПолучитьСтруктуруРегрессВзносы());
	РезультатРасчета.Вставить("Регресс_ПФ",ПолучитьСтруктуруРегрессПФ());
	РезультатРасчета.Вставить("Регресс_Инвалиды",ПолучитьСтруктуруРегрессВзносы());
	
	ТабличныеДокументы = Новый Структура;
	ТабличныеДокументы.Вставить("ВзносыСортировкаПоОблагаемойБазе",Неопределено);
	ТабличныеДокументы.Вставить("ВзносыСортировкаПоФИО",Неопределено);
	ТабличныеДокументы.Вставить("ПФ",Неопределено);
	ТабличныеДокументы.Вставить("Инвалиды",Неопределено);
	ТабличныеДокументы.Вставить("ВзносыМСБ",Неопределено); 
	ТабличныеДокументы.Вставить("СводПоСтраховымВзносам",Неопределено);
	
    СводныеДанные = Новый Структура;
	СводныеДанные.Вставить("Взносы", ПолучитьСтруктуруСводВзносы());
	СводныеДанные.Вставить("Численность", ПолучитьСтруктуруСводЧисленность());
	СводныеДанные.Вставить("Пособия", ПолучитьСтруктуруСводПособия());
	                                   	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область о // Фомирование табличных документов:

Процедура Взносы_СформироватьТабличныеДокументы(ИсходныеДанные)
	
	Если Не ДанныеДляРасчетаВзносов.СформироватьТабличныеДокументы Тогда
		Возврат;
	КонецЕсли;                                                               
	
	Если ДанныеДляРасчетаВзносов.ВидОтчета = "" Тогда
		ТабличныеДокументы.ВзносыСортировкаПоОблагаемойБазе = СформироватьТабличныйДокумент_Взносы("ОблагаемаяБаза",ИсходныеДанные);
		ТабличныеДокументы.ВзносыСортировкаПоФИО = СформироватьТабличныйДокумент_Взносы("ФИО",ИсходныеДанные);
		ТабличныеДокументы.ВзносыМСБ = СформироватьТабличныйДокумент_ВзносыМСБ("ФИО",ИсходныеДанные);
	ИначеЕсли НРег(ДанныеДляРасчетаВзносов.ВидОтчета) = НРег("Свод") Тогда  
		Если НЕ ДанныеДляРасчетаВзносов.Организация.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
			ТабличныеДокументы.СводПоСтраховымВзносам = СформироватьТабличныйДокумент_СводПоСтраховымВзносам(ИсходныеДанные);
		Иначе
			ТабличныеДокументы.СводПоСтраховымВзносам = СформироватьТабличныйДокумент_СводПоСтраховымВзносамМСБ(ИсходныеДанные);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры       

Функция СформироватьТабличныйДокумент_СводПоСтраховымВзносам(ИсходныеДанные)
	
	ТабДок = Новый ТабличныйДокумент;

	Если Год(ДанныеДляРасчетаВзносов.Дата1) >= 2024 Тогда
		Макет = ПолучитьМакет("СводПоСтраховымВзносам_2024");	
	Иначе                                    
		Макет = ПолучитьМакет("СводПоСтраховымВзносам");
	КонецЕсли;

	ВзносыТаблица_Месяц		= РезультатРасчета.ВзносыТаблица_Месяц.Скопировать(); 
	ПоляДляГруппировки 	= "Месяц,Квартал,Период";
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	ВзносыТаблица_Месяц.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);

	ВзносыТаблица_Квартал	= РезультатРасчета.ВзносыТаблица_Квартал.Скопировать(); 
	ПоляДляГруппировки 	= "Квартал,Период";
	ВзносыТаблица_Квартал.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);

	ВзносыТаблица_Год	= РезультатРасчета.ВзносыТаблица_Год.Скопировать(); 
	ПоляДляГруппировки 	= "";
	ВзносыТаблица_Год.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);
	
	// Заголовок:
	ВывестиЗаголовокСтраницы(Макет,ТабДок,ДанныеДляРасчетаВзносов);
	ВывестиШапкуТаблицы(Макет,ТабДок,ДанныеДляРасчетаВзносов,ИсходныеДанные);
	
	// Табличная часть:
	ТекущийКвартал = 1;
	Для каждого МесяцСтрока Из ВзносыТаблица_Месяц Цикл

		Если ТекущийКвартал <> МесяцСтрока.Квартал Тогда
			
			ВывестиКварталСвод(ТекущийКвартал,ВзносыТаблица_Квартал,Макет,ТабДок);
			ТекущийКвартал = МесяцСтрока.Квартал;

		КонецЕсли;
		
		ОбластьДокумента = Макет.ПолучитьОбласть("СтрокаТаблицы"); 
		ЗаполнитьЗначенияСвойств(ОбластьДокумента.Параметры, МесяцСтрока);
		ТабДок.Вывести(ОбластьДокумента); 	
		
	КонецЦикла;
	ВывестиКварталСвод(4,ВзносыТаблица_Квартал,Макет,ТабДок);
	
	ОбластьДокумента = Макет.ПолучитьОбласть("СтрокаТаблицыИтог"); 
	Если ВзносыТаблица_Год.Количество() > 0 Тогда
    	ЗаполнитьЗначенияСвойств(ОбластьДокумента.Параметры, ВзносыТаблица_Год[0]);
	КонецЕсли;	
	ОбластьДокумента.Параметры.Период = "Итого с начала года:";
	ТабДок.Вывести(ОбластьДокумента); 	
	
	Возврат ТабДок;
	
КонецФункции       

Функция СформироватьТабличныйДокумент_СводПоСтраховымВзносамМСБ(ИсходныеДанные)
	
	ТабДок = Новый ТабличныйДокумент;

	Если Год(ДанныеДляРасчетаВзносов.Дата1) >= 2024 Тогда
		Макет = ПолучитьМакет("СводПоСтраховымВзносамМСБ_2024");	
	Иначе                                    
		Макет = ПолучитьМакет("СводПоСтраховымВзносамМСБ");
	КонецЕсли;

	ВзносыТаблица_Месяц		= РезультатРасчета.ВзносыТаблица_Месяц.Скопировать(); 
	ПоляДляГруппировки 	= "Месяц,Квартал,Период";
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	ВзносыТаблица_Месяц.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);

	ВзносыТаблица_Квартал	= РезультатРасчета.ВзносыТаблица_Квартал.Скопировать(); 
	ПоляДляГруппировки 	= "Квартал,Период";
	ВзносыТаблица_Квартал.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);

	ВзносыТаблица_Год	= РезультатРасчета.ВзносыТаблица_Год.Скопировать(); 
	ПоляДляГруппировки 	= "";
	ВзносыТаблица_Год.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);
	
	
	// Заголовок:
	ВывестиЗаголовокСтраницы(Макет,ТабДок,ДанныеДляРасчетаВзносов);
	ВывестиШапкуТаблицы(Макет,ТабДок,ДанныеДляРасчетаВзносов,ИсходныеДанные,Истина);
	
	// Табличная часть:
	ТекущийКвартал = 1;
	Для каждого МесяцСтрока Из ВзносыТаблица_Месяц Цикл

		Если ТекущийКвартал <> МесяцСтрока.Квартал Тогда
			
			ВывестиКварталСвод(ТекущийКвартал,ВзносыТаблица_Квартал,Макет,ТабДок);
			ТекущийКвартал = МесяцСтрока.Квартал;

		КонецЕсли;
		
		ОбластьДокумента = Макет.ПолучитьОбласть("СтрокаТаблицы"); 
		ЗаполнитьЗначенияСвойств(ОбластьДокумента.Параметры, МесяцСтрока);
		ТабДок.Вывести(ОбластьДокумента); 	
		
	КонецЦикла;
	ВывестиКварталСвод(4,ВзносыТаблица_Квартал,Макет,ТабДок);
	
	ОбластьДокумента = Макет.ПолучитьОбласть("СтрокаТаблицыИтог"); 
	Если ВзносыТаблица_Год.Количество() > 0 Тогда
    	ЗаполнитьЗначенияСвойств(ОбластьДокумента.Параметры, ВзносыТаблица_Год[0]);
	КонецЕсли;	
	ОбластьДокумента.Параметры.Период = "Итого с начала года:";
	ТабДок.Вывести(ОбластьДокумента); 	
	
	Возврат ТабДок;

КонецФункции   

Процедура ВывестиКварталСвод(ТекущийКвартал,ВзносыТаблица_Квартал,Макет,ТабДок)

	ПоискСтроки = Новый Структура("Квартал",ТекущийКвартал);
	ВзносыМассив_Квартал = ВзносыТаблица_Квартал.НайтиСтроки(ПоискСтроки); 
	Для каждого КварталСтрока Из ВзносыМассив_Квартал Цикл
		
		ОбластьДокумента = Макет.ПолучитьОбласть("СтрокаТаблицыИтог"); 
		ЗаполнитьЗначенияСвойств(ОбластьДокумента.Параметры, КварталСтрока);
		ОбластьДокумента.Параметры.Период = Строка(ТекущийКвартал) +" квартал (итого):";
		ТабДок.Вывести(ОбластьДокумента); 	
		
	КонецЦикла;

КонецПроцедуры

Функция СформироватьТабличныйДокумент_ВзносыМСБ(ВидСортировки,ИсходныеДанные)
	
	ТабДок = Новый ТабличныйДокумент;
	Если Год(ДанныеДляРасчетаВзносов.Дата1) >= 2024 Тогда
		Макет = ПолучитьМакет("ВзносыМСБ_2024");	
	Иначе                                    
		Макет = ПолучитьМакет("ВзносыМСБ");
	КонецЕсли;
	ВзносыТаблица_Год	= РезультатРасчета.ВзносыТаблица_Год; 
	Регресс_Взносы		= РезультатРасчета.Регресс_Взносы; 
	           	
	Если ВидСортировки = "ОблагаемаяБаза" Тогда
		ВзносыТаблица_Год.Сортировать("ОблагаемаяБаза");
	ИначеЕсли ВидСортировки = "ФИО" Тогда
		ВзносыТаблица_Год.Сортировать("ФИО");
	КонецЕсли;
	
	// Основная таблица:
	ВывестиЗаголовокСтраницы(Макет,ТабДок,ДанныеДляРасчетаВзносов);
	ВывестиШапкуТаблицы(Макет,ТабДок,ДанныеДляРасчетаВзносов,ИсходныеДанные,Истина);
	
	СтрокаТаблицыОбласть = Макет.ПолучитьОбласть("СтрокаТаблицы");
	Для каждого ВзносыСтрока из ВзносыТаблица_Год Цикл
		
		Если ВзносыСтрока.Доход <> 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыОбласть.Параметры,ВзносыСтрока);
			ТабДок.Вывести(СтрокаТаблицыОбласть);
		КонецЕсли; 
		
	КонецЦикла;
	
	ВзносыТаблица_Всего = ВзносыТаблица_Год.Скопировать();
	ВзносыТаблица_Всего.Свернуть(,ПолучитьПоляДляСуммированияТаблицВЗН());
	
	ИтогТаблицыОбласть = Макет.ПолучитьОбласть("ИтогТаблицы"); 
	Для каждого ВзносыСтрока из ВзносыТаблица_Всего Цикл
		
		ЗаполнитьЗначенияСвойств(ИтогТаблицыОбласть.Параметры,ВзносыСтрока);
		ТабДок.Вывести(ИтогТаблицыОбласть);
		
	КонецЦикла;
	
	ОтступОбласть = Макет.ПолучитьОбласть("Отступ"); 
	ТабДок.Вывести(ОтступОбласть);

	// Регресс:
	//РегрессШапкаОбласть = Макет.ПолучитьОбласть("РегрессШапка");
	//ТабДок.Вывести(РегрессШапкаОбласть);

	//РегрессСтрокаОбласть = Макет.ПолучитьОбласть("РегрессСтрока");
	//Для каждого РегрессСтрока из Регресс_Взносы Цикл
	//	ЗаполнитьЗначенияСвойств(РегрессСтрокаОбласть.Параметры,РегрессСтрока);
	//	ТабДок.Вывести(РегрессСтрокаОбласть);
	//КонецЦикла;
	//
	//Регресс_Взносы_Итоги = Регресс_Взносы.Скопировать();
	//Регресс_Взносы_Итоги.Свернуть("","ОблагаемаяБазаСПревышением,ОблагаемаяБаза,ОблагаемаяБазаПревышение,ПФ,ФФОМС,НСиПЗ,ФСС,ВЗНВсего");
	//РегрессИтогиОбласть = Макет.ПолучитьОбласть("РегрессИтоги");
	//Для каждого РегрессИтогиСтрока из Регресс_Взносы_Итоги Цикл
	//	ЗаполнитьЗначенияСвойств(РегрессИтогиОбласть.Параметры,РегрессИтогиСтрока);
	//	ТабДок.Вывести(РегрессИтогиОбласть);
	//КонецЦикла;
	
	Возврат ТабДок;
	
КонецФункции

Функция СформироватьТабличныйДокумент_Взносы(ВидСортировки,ИсходныеДанные)
	
	ТабДок = Новый ТабличныйДокумент;
	Если Год(ДанныеДляРасчетаВзносов.Дата1) >= 2024 Тогда
		Макет = ПолучитьМакет("Взносы_2024");	
	Иначе                                    
		Макет = ПолучитьМакет("Взносы");
	КонецЕсли;
	
	ВзносыТаблица_Год	= РезультатРасчета.ВзносыТаблица_Год; 
	                	
	Если ВидСортировки = "ОблагаемаяБаза" Тогда
		ВзносыТаблица_Год.Сортировать("Доход Возр");
	ИначеЕсли ВидСортировки = "ФИО" Тогда
		ВзносыТаблица_Год.Сортировать("ФИО Возр");
	КонецЕсли;
	
	// Основная таблица:
	ВывестиЗаголовокСтраницы(Макет,ТабДок,ДанныеДляРасчетаВзносов);
	ВывестиШапкуТаблицы(Макет,ТабДок,ДанныеДляРасчетаВзносов,ИсходныеДанные);
	
	СтрокаТаблицыОбласть = Макет.ПолучитьОбласть("СтрокаТаблицы");
	Для каждого ВзносыСтрока из ВзносыТаблица_Год Цикл
		
		Если ВзносыСтрока.Доход <> 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыОбласть.Параметры,ВзносыСтрока);
			ТабДок.Вывести(СтрокаТаблицыОбласть);
		КонецЕсли; 
		
	КонецЦикла;
	
	ВзносыТаблица_Всего = ВзносыТаблица_Год.Скопировать();
	ВзносыТаблица_Всего.Свернуть(,ПолучитьПоляДляСуммированияТаблицВЗН());
	
	ИтогТаблицыОбласть = Макет.ПолучитьОбласть("ИтогТаблицы"); 
	Для каждого ВзносыСтрока из ВзносыТаблица_Всего Цикл
		
		ЗаполнитьЗначенияСвойств(ИтогТаблицыОбласть.Параметры,ВзносыСтрока);
		ТабДок.Вывести(ИтогТаблицыОбласть);
		
	КонецЦикла; 
	
	Возврат ТабДок; 
	
 КонецФункции

Процедура ВывестиЗаголовокСтраницы(Макет,ТабДок,ДанныеДляРасчетаВзносов)
	
	ЗаголовокОбласть = Макет.ПолучитьОбласть("Заголовок");
	ЗаголовокОбласть.Параметры.РасчетныйПериод = "За "
	                                           + Формат(ДанныеДляРасчетаВзносов.Дата1, "ДФ='ММММ гггг'")
											   + " - "
											   + Формат(ДанныеДляРасчетаВзносов.Дата2, "ДФ='ММММ гггг'"); 
	ЗаголовокОбласть.Параметры.ТекущаяДата = ТекущаяДата();
	//
	Если инкОбщийКлиентСервер.ЕстьСвойство(ЗаголовокОбласть.Параметры,"Подразделения") Тогда
		ЗаголовокОбласть.Параметры.Подразделения = "Все подразделения";
		Если ДанныеДляРасчетаВзносов.Подразделения.Количество() > 0 Тогда
			ЗаголовокОбласть.Параметры.Подразделения = "";
			Для каждого ПодразделениеСтрока из ДанныеДляРасчетаВзносов.Подразделения Цикл
				ЗаголовокОбласть.Параметры.Подразделения = ЗаголовокОбласть.Параметры.Подразделения
				                                         + ", "
				                                         + ПодразделениеСтрока;
			КонецЦикла;
			ЗаголовокОбласть.Параметры.Подразделения = Лев(ЗаголовокОбласть.Параметры.Подразделения,СтрДлина(ЗаголовокОбласть.Параметры.Подразделения) - 2);
		КонецЕсли;
	КонецЕсли;  
	//
	Если инкОбщийКлиентСервер.ЕстьСвойство(ЗаголовокОбласть.Параметры,"Организация") Тогда
        ЗаголовокОбласть.Параметры.Организация 	   = ДанныеДляРасчетаВзносов.Организация; 
    КонецЕсли;
	
	ТабДок.Вывести(ЗаголовокОбласть);

КонецПроцедуры

Процедура ВывестиШапкуТаблицы(Макет,ТабДок,ДанныеДляРасчетаВзносов,ИсходныеДанные,МСБ = Ложь)

	ШапкаОбласть = Макет.ПолучитьОбласть("ШапкаТаблицы");
	
	// Заполнение тарифов в пределах лимита:        
	СтрокаТарифа = Перечисления.инкСтрокаТарифаВЗН.ТарифВПределахЛимита;
	ПараметрыПоТарифам = ПолучитьПараметрыПоТарифам(СтрокаТарифа,ИсходныеДанные.КонфигурацияВыплатВФонды);
	ЗаполнитьЗначенияСвойств(ШапкаОбласть.Параметры,ПараметрыПоТарифам);
	
	// Заполнение тарифов сверх лимита:
	Если НЕ МСБ Тогда
		СтрокаТарифа = Перечисления.инкСтрокаТарифаВЗН.ТарифСверхЛимита;
	Иначе	
		СтрокаТарифа = Перечисления.инкСтрокаТарифаВЗН.ТарифСвышеМРОТ_1_5_ДляМСБ;
	КонецЕсли;
	ПараметрыПоТарифам = ПолучитьПараметрыПоТарифам(СтрокаТарифа,ИсходныеДанные.КонфигурацияВыплатВФонды);
	Если ЗначениеЗаполнено(ПараметрыПоТарифам) Тогда
		
		Если инкОбщийКлиентСервер.ЕстьСвойство(ШапкаОбласть.Параметры,"СФРПревышение") Тогда
			ШапкаОбласть.Параметры.СФРПревышение = ПараметрыПоТарифам.СФР;	
		КонецЕсли;
			
	КонецЕсли;
		
	ТабДок.Вывести(ШапкаОбласть);
	
КонецПроцедуры  

Функция ПолучитьПараметрыПоТарифам(СтрокаТарифа,КонфигурацияВыплатВФонды)
	
	ПараметрыПоТарифам = Неопределено;

	ПоискСтроки 	= Новый Структура("СтрокаТарифаВЗН",СтрокаТарифа);
	КВФМассив 		= КонфигурацияВыплатВФонды.НайтиСтроки(ПоискСтроки);
	
	Если КВФМассив.Количество() = 1 Тогда
		ПараметрыПоТарифам = КВФМассив[0];
	КонецЕсли;

	Возврат ПараметрыПоТарифам;
	
КонецФункции	

#КонецОбласти

#Область о // Структура таблиц:

Функция ПолучитьСтруктуруВзносы_Месяц()
	
	СтруктураТаблицы = Новый ТаблицаЗначений;
	
	//
	СтруктураТаблицы.Колонки.Добавить("Сотрудник");
	СтруктураТаблицы.Колонки.Добавить("ФИО");
	СтруктураТаблицы.Колонки.Добавить("Инвалид");
	СтруктураТаблицы.Колонки.Добавить("Пол");
	СтруктураТаблицы.Колонки.Добавить("ГодРождения");
	СтруктураТаблицы.Колонки.Добавить("ДатаРождения");
	СтруктураТаблицы.Колонки.Добавить("СтатусРезидента");
	//
	СтруктураТаблицы.Колонки.Добавить("ДатаПриемаНаРаботу");
	СтруктураТаблицы.Колонки.Добавить("ДатаУвольнения");
	//
	СтруктураТаблицы.Колонки.Добавить("Месяц",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("Квартал",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата"));
	//
	СтруктураТаблицы.Колонки.Добавить("Доход",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБаза",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаВсего",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаФСС",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаФССПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("Ст9ФЗ212",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("МатПомощь",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ВыплатыИнвалидам",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ДоговорыГПХ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФФОМС",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФФОМСПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("НСиПЗ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФСС",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФССИностранцы",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФССПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ВЗНВсего",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФВсего",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФФОМСВсего",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФССВсего",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("НДФЛ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("СФР",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("СФРПревышение",Новый ОписаниеТипов("Число")); 
	СтруктураТаблицы.Колонки.Добавить("СФРИтого",Новый ОписаниеТипов("Число")); 
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаНСиПЗ",Новый ОписаниеТипов("Число"));
	
	//
	ДобавитьВСтруктурыТаблицыКолонкиПоЧисленности(СтруктураТаблицы);
	//
	
	Возврат СтруктураТаблицы; 
	
КонецФункции

Процедура ДобавитьВСтруктурыТаблицыКолонкиПоЧисленности(СтруктураТаблицы)

    //
	СтруктураТаблицы.Колонки.Добавить("СреднесписочнаяЧисленность",Новый ОписаниеТипов("Число"));
	//
	СтруктураТаблицы.Колонки.Добавить("МужчиныКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ЖенщиныКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ИнвалидыКоличество",Новый ОписаниеТипов("Число"));
	//
	СтруктураТаблицы.Колонки.Добавить("ЗастрахованныеКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ЗастрахованныеПФКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ЗастрахованныеПФПревышениеБазыКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ЗастрахованныеФССКоличество",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ЗастрахованныеФССПревышениеБазыКоличество",Новый ОписаниеТипов("Число"));
		
КонецПроцедуры

Функция ПолучитьСтруктуруРегрессВзносы()
	
	СтруктураТаблицы = Новый ТаблицаЗначений;
	
	//
	СтруктураТаблицы.Колонки.Добавить("СтрокаТарифа");
	СтруктураТаблицы.Колонки.Добавить("Численность",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаСПревышением",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБаза",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФФОМС",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("НСиПЗ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ФСС",Новый ОписаниеТипов("Число"));   
	СтруктураТаблицы.Колонки.Добавить("ВЗНВсего",Новый ОписаниеТипов("Число"));   
	//
	
	Возврат СтруктураТаблицы; 
	
КонецФункции

Функция ПолучитьСтруктуруРегрессПФ()
	
	СтруктураТаблицы = Новый ТаблицаЗначений;
	
	//
	СтруктураТаблицы.Колонки.Добавить("НаименованиеПоказателя");
	СтруктураТаблицы.Колонки.Добавить("КодОснования");
	СтруктураТаблицы.Колонки.Добавить("Численность",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБаза",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ОблагаемаяБазаПревышение",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФ",Новый ОписаниеТипов("Число"));
	СтруктураТаблицы.Колонки.Добавить("ПФПревышение",Новый ОписаниеТипов("Число"));
	//
	
	Возврат СтруктураТаблицы; 
	
КонецФункции

Функция ПолучитьСтруктуруВзносы_Год(ИсходныеДанные,РезультатРасчета)

	ПоляДляГруппировки = ПолучитьПоляДляГруппировкиТаблицВЗН();
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	
	ВзносыТаблица_Год	= РезультатРасчета.ВзносыТаблица_Месяц.Скопировать();
	УдалитьСтрокиНеВходящиеВПериод(ИсходныеДанные, ВзносыТаблица_Год);
	
	ВзносыТаблица_Год.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);

	Возврат ВзносыТаблица_Год; 
	
КонецФункции

Процедура УдалитьСтрокиНеВходящиеВПериод(ИсходныеДанные, ТЗ)
	
	Если ИсходныеДанные.Месяц1 = ИсходныеДанные.Месяц2 Тогда
		
		Для Индекс = -ТЗ.Количество() + 1 По 0 Цикл
		    Если ТЗ[-Индекс].Месяц <> ИсходныеДанные.Месяц1 Тогда
		        ТЗ.Удалить(-Индекс);
		    КонецЕсли;
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

Функция СтруктураДанныхПособияСводТаблица()
	
	ПособияСтруктура = Новый Структура;
	ПособияСтруктура.Вставить("ЧислоСлучаев",Новый ОписаниеТипов("Число"));
	ПособияСтруктура.Вставить("КоличествоДней",Новый ОписаниеТипов("Число"));
	ПособияСтруктура.Вставить("РасходыВсего",Новый ОписаниеТипов("Число"));
	ПособияСтруктура.Вставить("РасходыЗасчетФедеральногоБюджета",Новый ОписаниеТипов("Число"));
	
	Возврат ПособияСтруктура;
	
КонецФункции                                

Функция ПолучитьСтруктуруСводВзносы()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ВзносыСводТаблица_Месяц");
	СтруктураДанных.Вставить("ВзносыСводТаблица_Квартал");
	СтруктураДанных.Вставить("ВзносыСводТаблица_Год");
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ПолучитьСтруктуруЧисленностьТаблица()
	
	СтруктураТаблицы = Новый ТаблицаЗначений;
	СтруктураТаблицы.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата"));

	//
	ДобавитьВСтруктурыТаблицыКолонкиПоЧисленности(СтруктураТаблицы);
	//
	
	Возврат СтруктураТаблицы;
	
КонецФункции

Функция ПолучитьСтруктуруСводЧисленность()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ЧисленностьСводТаблица_Месяц",ПолучитьСтруктуруЧисленностьТаблица());
	СтруктураДанных.Вставить("ЧисленностьСводТаблица_Квартал",ПолучитьСтруктуруЧисленностьТаблица());
	СтруктураДанных.Вставить("ЧисленностьСводТаблица_Год",ПолучитьСтруктуруЧисленностьТаблица());
	
	Возврат СтруктураДанных;

КонецФункции

Функция ПолучитьСтруктуруПособияСвод()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Период",Новый ОписаниеТипов("Дата"));

    //
	СтруктураДанных.Вставить("НетрудБезИностр",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("НетрудБезИнострСовместители",СтруктураДанныхПособияСводТаблица());
	//
	СтруктураДанных.Вставить("НетрудИностр",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("НетрудИнострСовместители",СтруктураДанныхПособияСводТаблица());
	//
	СтруктураДанных.Вставить("ПоБеременностиИРодам",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПоБеременностиИРодамСовместители",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПоБеременностиИРодамВРанниеСроки",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПриРожденииРебенка",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПоУходуЗаРебенком",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПоУходуЗаРебенкомЗаПервымРебенком",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ПоУходуЗаРебенкомЗаВторымИПослед",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("ОплатаДнейЗаДетьмиИнвалидами",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("НаПогребение",СтруктураДанныхПособияСводТаблица());
	СтруктураДанных.Вставить("Итоги",СтруктураДанныхПособияСводТаблица());
	//
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция ПолучитьСтруктуруСводПособия()
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Пособия_Год",ПолучитьСтруктуруПособияСвод());
	
	Возврат СтруктураДанных;
		
КонецФункции

#КонецОбласти

#Область о // Исходные данные:

Функция ПолучитьИсходныеДанные(ДанныеДляРасчетаВзносов)
	
	ИсходныеДанные = Новый Структура;
	//
	ИсходныеДанные.Вставить("ОсобыеУсловияТруда",ПолучитьОсобыеУсловияТруда());
	ИсходныеДанные.Вставить("КонфигурацияВыплатВФонды",ПолучитьКонфигурациюВыплатВФонды());
	ИсходныеДанные.Вставить("СтандартныеВычетыНастройки",инкОбщийСервер.СтандартныеВычетыНастройки(ДанныеДляРасчетаВзносов.Дата2));
	//
	ИсходныеДанные.Вставить("СотрудникиТаблица",ПолучитьСотрудникиТаблица(ДанныеДляРасчетаВзносов));
	ИсходныеДанные.Вставить("НачисленияТаблица",ПолучитьНачисленияТаблица(ДанныеДляРасчетаВзносов));
	ИсходныеДанные.Вставить("НДФЛТаблица",ПолучитьНДФЛТаблица(ДанныеДляРасчетаВзносов));
	//
	ИсходныеДанные.Вставить("Месяц1",Месяц(ДанныеДляРасчетаВзносов.Дата1));
	ИсходныеДанные.Вставить("Месяц2",Месяц(ДанныеДляРасчетаВзносов.Дата2));
	//
	ИсходныеДанные.Вставить("Дата1",НачалоМесяца(ДанныеДляРасчетаВзносов.Дата1));
	ИсходныеДанные.Вставить("Дата2",НачалоМесяца(ДанныеДляРасчетаВзносов.Дата2));
	//
	ИсходныеДанные.Вставить("ПредприятиеМалогоИлиСреднегоБизнеса",ДанныеДляРасчетаВзносов.Организация.ПредприятиеМалогоИлиСреднегоБизнеса);
	ИсходныеДанные.Вставить("МРОТ",ИсходныеДанные.СтандартныеВычетыНастройки.МРОТ);
	
	Возврат ИсходныеДанные;
	
КонецФункции

Функция ПолучитьОсобыеУсловияТруда()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	инкОсобыеУсловияТруда.УсловияТрудаКласс КАК УсловияТрудаКласс,
	               |	инкОсобыеУсловияТруда.УсловияТрудаПодкласс КАК УсловияТрудаПодкласс,
	               |	инкОсобыеУсловияТруда.Тариф КАК Тариф
	               |ИЗ
	               |	РегистрСведений.инкОсобыеУсловияТруда КАК инкОсобыеУсловияТруда"; 
	
	тз = Запрос.Выполнить().Выгрузить(); 
	
	Возврат тз;	
	
КонецФункции

Функция ПолучитьКонфигурациюВыплатВФонды()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	инкКонфигурацияВыплатВФонды.СтрокаТарифаВЗН КАК СтрокаТарифаВЗН,
	               |	инкКонфигурацияВыплатВФонды.ПФ КАК ПФ,
	               |	инкКонфигурацияВыплатВФонды.ФСС КАК ФСС,
	               |	инкКонфигурацияВыплатВФонды.ФФОМС КАК ФФОМС,
	               |	&НСиПЗ КАК НСиПЗ,
	               |	инкКонфигурацияВыплатВФонды.СФР КАК СФР
	               |ИЗ
	               |	РегистрСведений.инкКонфигурацияВыплатВФонды КАК инкКонфигурацияВыплатВФонды"; 
		
	Запрос.Параметры.Вставить("НСиПЗ",Константы.инкФондТравматизма.Получить());
	
	тз = Запрос.Выполнить().Выгрузить(); 
	
	Возврат тз;	
	
КонецФункции
                                        
Функция ПолучитьНДФЛТаблица(ДанныеДляРасчетаВзносов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	инкНалогУдержанныйОбороты.Период КАК Период,
	               |	МЕСЯЦ(инкНалогУдержанныйОбороты.Период) КАК Месяц,
	               |	инкНалогУдержанныйОбороты.Сотрудник КАК Сотрудник,
	               |	инкНалогУдержанныйОбороты.СуммаНалогаОборот КАК НДФЛ
	               |ИЗ
	               |	РегистрНакопления.инкНалогУдержанный.Обороты(
	               |			&Дата1,
	               |			&Дата2,
	               |			Месяц,
	               |			Организация = &Организация
	               |				И Подразделение В (&Подразделения)
	               |				И Сотрудник В (&Сотрудники)) КАК инкНалогУдержанныйОбороты
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Период"; 
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Сотрудники) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&Сотрудники)","Истина");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Организация = &Организация","Истина");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Подразделения) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Подразделение В (&Подразделения)","Истина");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", ДанныеДляРасчетаВзносов.Организация);
	Запрос.УстановитьПараметр("Подразделения", ДанныеДляРасчетаВзносов.Подразделения);
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ДанныеДляРасчетаВзносов.Дата1));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ДанныеДляРасчетаВзносов.Дата2));
	Запрос.УстановитьПараметр("Сотрудники", ДанныеДляРасчетаВзносов.Сотрудники);
	
	тз = Запрос.Выполнить().Выгрузить(); 
	
	Возврат тз;
	
КонецФункции
					 
Функция ПолучитьНачисленияТаблица(ДанныеДляРасчетаВзносов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	инкНачисленияОбороты.Период КАК Период,
	               |	МЕСЯЦ(инкНачисленияОбороты.Период) КАК Месяц,
	               |	инкНачисленияОбороты.Сотрудник КАК Сотрудник,
	               |	инкНачисленияОбороты.Сотрудник.Пол КАК СотрудникПол,
	               |	инкНачисленияОбороты.Сотрудник.СтатусРезидента КАК СотрудникСтатусРезидента,
	               |	инкНачисленияОбороты.Сотрудник.Гражданство КАК СотрудникГражданство,
	               |	инкНачисленияОбороты.Сотрудник.Совместитель КАК СотрудникСовместитель,
	               |	инкНачисленияОбороты.Сотрудник.Иностранец КАК СотрудникИностранец,
	               |	инкНачисленияОбороты.Организация КАК Организация,
	               |	инкНачисленияОбороты.Подразделение КАК Подразделение,
	               |	инкНачисленияОбороты.Начисление КАК Начисление,
	               |	инкНачисленияОбороты.Ст9ФЗ212 КАК Ст9ФЗ212,
	               |	инкНачисленияОбороты.МатериальнаяПомощь КАК МатериальнаяПомощь,
	               |	инкНачисленияОбороты.ФондТравматизма КАК ФондТравматизма,
	               |	инкНачисленияОбороты.Взносы КАК Взносы,
	               |	инкНачисленияОбороты.ДоходНДФЛ КАК ДоходНДФЛ,
	               |	инкНачисленияОбороты.ДоходНДФЛ.ВычетНДФЛ КАК ВычетНДФЛ,
	               |	инкНачисленияОбороты.СуммаНачисленияОборот КАК СуммаНачисления,
	               |	инкНачисленияОбороты.РайонныйКоэффициентОборот КАК РайонныйКоэффициент,
	               |	инкНачисленияОбороты.СевернаяНадбавкаОборот КАК СевернаяНадбавка,
	               |	инкНачисленияОбороты.КоличествоДетейОборот КАК КоличествоДетей,
	               |	инкНачисленияОбороты.ДниОборот КАК Дни,
	               |	инкНачисленияОбороты.ДниФактОборот КАК ДниФакт
	               |ИЗ
	               |	РегистрНакопления.инкНачисления.Обороты(
	               |			&Дата1,
	               |			&Дата2,
	               |			Месяц,
	               |			Организация = &Организация
	               |				И Подразделение В (&Подразделения)
	               |				И Сотрудник В (&Сотрудники)
	               |				И ВходитВФОТ) КАК инкНачисленияОбороты
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сотрудник,
	               |	Период"; 
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Сотрудники) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Сотрудник В (&Сотрудники)","Истина");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Организация = &Организация","Истина");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Подразделения) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"Подразделение В (&Подразделения)","Истина");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", ДанныеДляРасчетаВзносов.Организация);
	Запрос.УстановитьПараметр("Подразделения", ДанныеДляРасчетаВзносов.Подразделения);
	Запрос.УстановитьПараметр("Дата1", НачалоГода(ДанныеДляРасчетаВзносов.Дата1));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ДанныеДляРасчетаВзносов.Дата2));
	Запрос.УстановитьПараметр("Сотрудники", ДанныеДляРасчетаВзносов.Сотрудники);
	
	тз = Запрос.Выполнить().Выгрузить(); 
	
	Возврат тз;
	
КонецФункции

Функция ПолучитьСотрудникиТаблица(ДанныеДляРасчетаВзносов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = ПолучитьТекстЗапроса_Сотрудники(ДанныеДляРасчетаВзносов); 
	
	массВидыСобытийПриема = Новый Массив;
	массВидыСобытийПриема.Добавить(Перечисления.инкВидыКадровыхСобытий.Прием);
	массВидыСобытийПриема.Добавить(Перечисления.инкВидыКадровыхСобытий.Перемещение);
	массВидыСобытийПриема.Добавить(Перечисления.инкВидыКадровыхСобытий.ВосстановлениеВДолжности);
	                                                                  
	Запрос.УстановитьПараметр("ВидыСобытийПриема", массВидыСобытийПриема);
	Запрос.УстановитьПараметр("Организация", ДанныеДляРасчетаВзносов.Организация);
	Запрос.УстановитьПараметр("Подразделения", ДанныеДляРасчетаВзносов.Подразделения);
	Запрос.УстановитьПараметр("Дата1", НачалоМесяца(ДанныеДляРасчетаВзносов.Дата1));
	Запрос.УстановитьПараметр("Дата2", КонецМесяца(ДанныеДляРасчетаВзносов.Дата2));
	Запрос.УстановитьПараметр("Сотрудники", ДанныеДляРасчетаВзносов.Сотрудники);
	
	тз = Запрос.Выполнить().Выгрузить(); 
	тз.Индексы.Добавить("Сотрудник");
	
	Возврат тз;
	
КонецФункции

Функция ПолучитьТекстЗапроса_Сотрудники(ДанныеДляРасчетаВзносов)

	стрТекстЗапроса =	
		"ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втРаботающиеСотрудникиНаНачалоПериода
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			&Дата1,
		|			Организация = &Организация
		|				И Подразделение В (&Подразделения)
		|				И Сотрудник В (&Сотрудники)) КАК инкКадроваяИсторияСотрудниковСрезПоследних
		|ГДЕ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.ВидСобытия В(&ВидыСобытийПриема)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втРаботающиеСотрудникиНаКонецПериода
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			&Дата2,
		|			Организация = &Организация
		|				И Подразделение В (&Подразделения)
		|				И Сотрудник В (&Сотрудники)) КАК инкКадроваяИсторияСотрудниковСрезПоследних
		|ГДЕ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.ВидСобытия В(&ВидыСобытийПриема)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втУволенныеСотрудникиНаНачалоПериода
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			&Дата1,
		|			Организация = &Организация
		|				И Подразделение В (&Подразделения)
		|				И Сотрудник В (&Сотрудники)) КАК инкКадроваяИсторияСотрудниковСрезПоследних
		|ГДЕ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.инкВидыКадровыхСобытий.Увольнение)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудников.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втУволенныеВТекущемПериоде
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников КАК инкКадроваяИсторияСотрудников
		|ГДЕ
		|	инкКадроваяИсторияСотрудников.Период >= &Дата1
		|	И инкКадроваяИсторияСотрудников.Период <= &Дата2
		|	И инкКадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.инкВидыКадровыхСобытий.Увольнение)
		|	И инкКадроваяИсторияСотрудников.Организация = &Организация
		|	И инкКадроваяИсторияСотрудников.Подразделение В(&Подразделения)
		|	И инкКадроваяИсторияСотрудников.Сотрудник В(&Сотрудники)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботающиеСотрудникиНаНачалоПериода.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втОбъединение
		|ИЗ
		|	втРаботающиеСотрудникиНаНачалоПериода КАК втРаботающиеСотрудникиНаНачалоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втРаботающиеСотрудникиНаКонецПериода.Сотрудник
		|ИЗ
		|	втРаботающиеСотрудникиНаКонецПериода КАК втРаботающиеСотрудникиНаКонецПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втУволенныеСотрудникиНаНачалоПериода.Сотрудник
		|ИЗ
		|	втУволенныеСотрудникиНаНачалоПериода КАК втУволенныеСотрудникиНаНачалоПериода
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	втУволенныеВТекущемПериоде.Сотрудник
		|ИЗ
		|	втУволенныеВТекущемПериоде КАК втУволенныеВТекущемПериоде
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОбъединение.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втГруппировка
		|ИЗ
		|	втОбъединение КАК втОбъединение
		|
		|СГРУППИРОВАТЬ ПО
		|	втОбъединение.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГруппировка.Сотрудник КАК Сотрудник,
		|	втГруппировка.Сотрудник.ФИОСокращенное КАК ФИО,
		|	втГруппировка.Сотрудник.Пол КАК Пол,
		|	втГруппировка.Сотрудник.Инвалид КАК Инвалид,
		|	втГруппировка.Сотрудник.ДатаРождения КАК ДатаРождения,
		|	ГОД(втГруппировка.Сотрудник.ДатаРождения) КАК ГодРождения,
		|	втГруппировка.Сотрудник.СтатусРезидента КАК СтатусРезидента,
		|	втГруппировка.Сотрудник.СтатусИностранца КАК СтатусИностранца,
		|	втГруппировка.Сотрудник.Иностранец КАК Иностранец
		|ПОМЕСТИТЬ втРаботающиеСотрудники
		|ИЗ
		|	втГруппировка КАК втГруппировка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Организация КАК Организация,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.Подразделение КАК Подразделение,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.ДатаПриемаНаРаботу КАК ДатаПриемаНаРаботу,
		|	инкКадроваяИсторияСотрудниковСрезПоследних.ДатаУвольнения КАК ДатаУвольнения
		|ПОМЕСТИТЬ втКадровыеДанные
		|ИЗ
		|	РегистрСведений.инкКадроваяИсторияСотрудников.СрезПоследних(
		|			,
		|			Сотрудник В
		|				(ВЫБРАТЬ
		|					втРаботающиеСотрудники.Сотрудник КАК Сотрудник
		|				ИЗ
		|					втРаботающиеСотрудники КАК втРаботающиеСотрудники)) КАК инкКадроваяИсторияСотрудниковСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втРаботающиеСотрудники.Сотрудник КАК Сотрудник,
		|	втРаботающиеСотрудники.ФИО КАК ФИО,
		|	втРаботающиеСотрудники.Пол КАК Пол,
		|	втРаботающиеСотрудники.Инвалид КАК Инвалид,
		|	втРаботающиеСотрудники.ДатаРождения КАК ДатаРождения,
		|	втРаботающиеСотрудники.ГодРождения КАК ГодРождения,
		|	втРаботающиеСотрудники.СтатусРезидента КАК СтатусРезидента,
		|	втРаботающиеСотрудники.СтатусИностранца КАК СтатусИностранца,
		|	втРаботающиеСотрудники.Иностранец КАК Иностранец,
		|	втКадровыеДанные.ДатаПриемаНаРаботу КАК ДатаПриемаНаРаботу,
		|	втКадровыеДанные.ДатаУвольнения КАК ДатаУвольнения,
		|	втКадровыеДанные.Организация КАК Организация,
		|	втКадровыеДанные.Подразделение КАК Подразделение
		|ИЗ
		|	втРаботающиеСотрудники КАК втРаботающиеСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ втКадровыеДанные КАК втКадровыеДанные
		|		ПО втРаботающиеСотрудники.Сотрудник = втКадровыеДанные.Сотрудник";
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Организация) Тогда  
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"инкКадроваяИсторияСотрудников.Организация = &Организация","Истина");
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"Организация = &Организация","Истина");
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Подразделения) Тогда
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"инкКадроваяИсторияСотрудников.Подразделение В(&Подразделения)","Истина");
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"Подразделение В (&Подразделения)","Истина");
	КонецЕсли;		
	
	Если Не ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Сотрудники) Тогда
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"инкКадроваяИсторияСотрудников.Сотрудник В(&Сотрудники)","Истина");
		стрТекстЗапроса = СтрЗаменить(стрТекстЗапроса,"Сотрудник В (&Сотрудники)","Истина");
	КонецЕсли;
	
	Возврат стрТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область о // Расчет взносов - детальный расчет:

Функция ПолучучитьНастройкиВЗН(ИсходныеДанные,ИмяДанныхТаблицы,Фонд)
	
	НастройкиВЗН 	= 0;

	Если ИмяДанныхТаблицы = "ОблагаемаяБаза" Тогда
		
		Если ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
			// НастройкиВЗН = ИсходныеДанные.МРОТ;
			НастройкиВЗН = ИсходныеДанные.МРОТ * 1.5; // Изменения с 2025 года
		Иначе			
			НастройкиВЗН = ПолучитьЗначениеСтрокиТаблицы(ИсходныеДанные,
			                                             Перечисления.инкСтрокаТарифаВЗН.ОблагаемаяБазаВЗН,
														 Фонд);  
		КонецЕсли;
		
	ИначеЕсли ИмяДанныхТаблицы = "ТарифВПределахЛимита" Тогда
		
		НастройкиВЗН = ПолучитьЗначениеСтрокиТаблицы(ИсходныеДанные,
		                                             Перечисления.инкСтрокаТарифаВЗН.ТарифВПределахЛимита,
													 Фонд)/100;
		
	ИначеЕсли ИмяДанныхТаблицы = "ТарифСверхЛимита" Тогда 	
		
		Если ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
			НастройкиВЗН = ПолучитьЗначениеСтрокиТаблицы(ИсходныеДанные,
			                                             Перечисления.инкСтрокаТарифаВЗН.ТарифСвышеМРОТ_1_5_ДляМСБ,
														 Фонд)/100;
		Иначе			
			НастройкиВЗН = ПолучитьЗначениеСтрокиТаблицы(ИсходныеДанные,
			                                             Перечисления.инкСтрокаТарифаВЗН.ТарифСверхЛимита,
														 Фонд)/100;  
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НастройкиВЗН; 
	
КонецФункции

Функция ПолучитьЗначениеСтрокиТаблицы(ИсходныеДанные,СтрокаТаблицыВЗН,Фонд)
	
	ЗначениеФонда = 0;
	
	ПоискСтроки 	= Новый Структура("СтрокаТарифаВЗН",СтрокаТаблицыВЗН);
	КВФМассив 		= ИсходныеДанные.КонфигурацияВыплатВФонды.НайтиСтроки(ПоискСтроки);
	
	Для каждого КВФЭлемент Из КВФМассив Цикл
		ЗначениеФонда = КВФЭлемент[Фонд];	
	КонецЦикла;
	
	Возврат ЗначениеФонда; 
	
КонецФункции

Процедура ВзносыТаблица_НачальноеЗаполнениеДанными(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	ВзносыТаблица_Месяц.Очистить();
	
	Для каждого Сотрудник из ИсходныеДанные.СотрудникиТаблица Цикл
		
		Если Сотрудник.Инвалид Тогда
			Сотрудник.ФИО = Сотрудник.ФИО + " (инв.)";
		КонецЕсли;
		
		МесяцДата   = НачалоГода(ИсходныеДанные.Дата1);

		Для Месяц = 1 По 12 Цикл 
			
			ВзносыСтрока = ВзносыТаблица_Месяц.Добавить();
			ЗаполнитьЗначенияСвойств(ВзносыСтрока,Сотрудник);
			ВзносыСтрока.Месяц = Месяц;
			ВзносыСтрока.Квартал = инкОбщийКлиентСервер.КварталПоМесяцу(ВзносыСтрока.Месяц);	
			ВзносыСтрока.Период = МесяцДата;
			
			МесяцДата   = ДобавитьМесяц(МесяцДата,1);
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_Доход(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	
	Для каждого ВзносыСтрока из ВзносыТаблица_Месяц Цикл
		
		ПоискСтруктура = ПолучитьСтруктуруПоиска(ВзносыСтрока);
		НачисленияСтроки = НачисленияТаблица.НайтиСтроки(ПоискСтруктура);
		ВзносыСтрока.Доход = 0;
		
		Для каждого НачислениеСтрока из НачисленияСтроки Цикл
			
			ВзносыСтрока.Доход = ВзносыСтрока.Доход
			                   + НачислениеСтрока.СуммаНачисления
							   + НачислениеСтрока.РайонныйКоэффициент
							   + НачислениеСтрока.СевернаяНадбавка;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВзносыТаблица_Ст9ФЗ212(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	
	Для каждого ВзносыСтрока из ВзносыТаблица_Месяц Цикл
		
		ПоискСтруктура = ПолучитьСтруктуруПоиска(ВзносыСтрока);
		НачисленияСтроки = НачисленияТаблица.НайтиСтроки(ПоискСтруктура);
		ВзносыСтрока.Ст9ФЗ212 = 0;
		
		Для каждого НачислениеСтрока из НачисленияСтроки Цикл
			
			Если НачислениеСтрока.Ст9ФЗ212 Тогда
				ВзносыСтрока.Ст9ФЗ212 = ВзносыСтрока.Ст9ФЗ212 
				                      + НачислениеСтрока.СуммаНачисления
									  + НачислениеСтрока.РайонныйКоэффициент
									  + НачислениеСтрока.СевернаяНадбавка;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла; 	
	
КонецПроцедуры

Процедура ВзносыТаблица_ОблагаемаяБаза(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	
	ПредельнаяВеличинаБазы = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ОблагаемаяБаза","ПФ");
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		НакопительнаяБаза = 0;
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.ОблагаемаяБаза = ВзносыСтрока.Доход
			                            - ВзносыСтрока.Ст9ФЗ212
										- ВзносыСтрока.МатПомощь;
										
			НакопительнаяБаза = НакопительнаяБаза 
			                  + ВзносыСтрока.ОблагаемаяБаза;	
			Если ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
				НакопительнаяБаза = ВзносыСтрока.ОблагаемаяБаза; 
			КонецЕсли;
							  
			Если НакопительнаяБаза >= ПредельнаяВеличинаБазы Тогда
				
				ПревышениеОблагаемойБазы = НакопительнаяБаза - ПредельнаяВеличинаБазы;
				Если ПревышениеОблагаемойБазы > ВзносыСтрока.ОблагаемаяБаза Тогда
					ПревышениеОблагаемойБазы = ВзносыСтрока.ОблагаемаяБаза;
				КонецЕсли;
				
				ВзносыСтрока.ОблагаемаяБаза = ВзносыСтрока.ОблагаемаяБаза
				                            - ПревышениеОблагаемойБазы;
				ВзносыСтрока.ОблагаемаяБазаПревышение = ПревышениеОблагаемойБазы;				
				
			КонецЕсли;	  
			
			ВзносыСтрока.ОблагаемаяБазаВсего = ВзносыСтрока.ОблагаемаяБаза
			                                 + ВзносыСтрока.ОблагаемаяБазаПревышение;
		
		КонецЦикла;
		
	КонецЦикла;	

КонецПроцедуры

Процедура ВзносыТаблица_ОблагаемаяБазаФСС(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	ПредельнаяВеличинаБазы = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ОблагаемаяБаза","ФСС"); 
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		НакопительнаяБаза = 0;
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.ОблагаемаяБазаФСС	= ВзносыСтрока.Доход
			                            	- ВзносыСтрока.Ст9ФЗ212
											- ВзносыСтрока.МатПомощь
											- ВзносыСтрока.ДоговорыГПХ;
											
			НакопительнаяБаза = НакопительнаяБаза 
			                  + ВзносыСтрока.ОблагаемаяБазаФСС;	
			Если ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
				НакопительнаяБаза = ВзносыСтрока.ОблагаемаяБазаФСС; 
			КонецЕсли;
							  
			Если НакопительнаяБаза >= ПредельнаяВеличинаБазы Тогда
				
				ПревышениеОблагаемойБазы = НакопительнаяБаза - ПредельнаяВеличинаБазы;
				Если ПревышениеОблагаемойБазы > ВзносыСтрока.ОблагаемаяБазаФСС Тогда
					ПревышениеОблагаемойБазы = ВзносыСтрока.ОблагаемаяБазаФСС;
				КонецЕсли;
				
				ВзносыСтрока.ОблагаемаяБазаФСС	= ВзносыСтрока.ОблагаемаяБазаФСС
				                            	- ПревышениеОблагаемойБазы;
				ВзносыСтрока.ОблагаемаяБазаФССПревышение = ПревышениеОблагаемойБазы;				
				
			КонецЕсли;	
		
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_МатПомощь(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	КодДохода2760 = Справочники.инкДоходНДФЛ.Код2760;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		НакопительнаяБаза = 0;                  
		МатПомощьСвод = 0;
		ПредельнаяВеличинаСкидки = 4000;
		
		// В общем случае мат. помощь облагается только с сумм свыше 
		// предоставленной скидки: 
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.МатПомощь = 0;
			
			ПоискСтроки = Новый Структура;
			ПоискСтроки.Вставить("Сотрудник",ВзносыСтрока.Сотрудник);
			ПоискСтроки.Вставить("Месяц",ВзносыСтрока.Месяц);
			ПоискСтроки.Вставить("ДоходНДФЛ",КодДохода2760);
			НачисленияМассив = НачисленияТаблица.НайтиСтроки(ПоискСтроки);

			Для каждого НачислениеСтрока из НачисленияМассив Цикл
			
				НачисленнаяМатПомощь   = НачислениеСтрока.СуммаНачисления
									   + НачислениеСтрока.РайонныйКоэффициент
									   + НачислениеСтрока.СевернаяНадбавка;
									   
				Если ПредельнаяВеличинаСкидки = 0 Тогда
					Прервать;
				КонецЕсли; 					   
									   
				ПредельнаяВеличинаСкидки = ПредельнаяВеличинаСкидки - НачисленнаяМатПомощь; 					   
				Если ПредельнаяВеличинаСкидки >= 0 Тогда
					ВзносыСтрока.МатПомощь = НачисленнаяМатПомощь;
				Иначе
					ВзносыСтрока.МатПомощь = НачисленнаяМатПомощь + ПредельнаяВеличинаСкидки;
					ПредельнаяВеличинаСкидки = 0;
				КонецЕсли; 
				
			КонецЦикла;	
				
		КонецЦикла;
		
	КонецЦикла; 	

КонецПроцедуры

Процедура ВзносыТаблица_ВыплатыИнвалидам(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	
	Для каждого ВзносыСтрока из ВзносыТаблица_Месяц Цикл
		
		ПоискСтруктура = ПолучитьСтруктуруПоиска(ВзносыСтрока);
		НачисленияСтроки = НачисленияТаблица.НайтиСтроки(ПоискСтруктура);
		ВзносыСтрока.ВыплатыИнвалидам = 0;
		
		Для каждого НачислениеСтрока из НачисленияСтроки Цикл
			
			Если ВзносыСтрока.Инвалид Тогда
				ВзносыСтрока.ВыплатыИнвалидам = ВзносыСтрока.ВыплатыИнвалидам + НачислениеСтрока.СуммаНачисления;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ВзносыТаблица_ДоговорыГПХ(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	ДоходНДФЛ_2010	= Справочники.инкДоходНДФЛ.Код2010; 
	ВычетНДФЛ_403	= Справочники.инкВычетыНДФЛ.Код403;	
	ВычетНДФЛ_404	= Справочники.инкВычетыНДФЛ.Код404;	
	ВычетНДФЛ_405	= Справочники.инкВычетыНДФЛ.Код405;	
	
	Для каждого ВзносыСтрока из ВзносыТаблица_Месяц Цикл
		
		ПоискСтруктура = ПолучитьСтруктуруПоиска(ВзносыСтрока);
		НачисленияСтроки = НачисленияТаблица.НайтиСтроки(ПоискСтруктура);
		ВзносыСтрока.ДоговорыГПХ = 0;
		
		
		Для каждого НачислениеСтрока из НачисленияСтроки Цикл
			
			Если НачислениеСтрока.ДоходНДФЛ = ДоходНДФЛ_2010 ИЛИ
				 НачислениеСтрока.ВычетНДФЛ = ВычетНДФЛ_403  ИЛИ
				 НачислениеСтрока.ВычетНДФЛ = ВычетНДФЛ_404  ИЛИ
				 НачислениеСтрока.ВычетНДФЛ = ВычетНДФЛ_405  				 
			Тогда
			
				ВзносыСтрока.ДоговорыГПХ = ВзносыСтрока.ДоговорыГПХ
							             + НачислениеСтрока.СуммаНачисления
										 + НачислениеСтрока.РайонныйКоэффициент
										 + НачислениеСтрока.СевернаяНадбавка;
										 
			КонецЕсли;
					
		КонецЦикла;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ВзносыТаблица_Всего(ИсходныеДанные,РезультатРасчета)

	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.ВЗНВсего = ВзносыСтрока.ВЗНВсего
			                      + ВзносыСтрока.ПФ
								  + ВзносыСтрока.ФСС
								  + ВзносыСтрока.ФФОМС; 
								  
			ВзносыСтрока.ПФВсего 	= ВзносыСтрока.ПФВсего
			                     	+ ВзносыСтрока.ПФ
								 	+ ВзносыСтрока.ПФПревышение; 
								  
			ВзносыСтрока.ФФОМСВсего	= ВзносыСтрока.ФФОМСВсего
				                    + ВзносыСтрока.ФФОМС
									+ ВзносыСтрока.ФФОМСПревышение; 

			ВзносыСтрока.ФССВсего	= ВзносыСтрока.ФССВсего
				                    + ВзносыСтрока.ФСС
									+ ВзносыСтрока.ФССПревышение; 

		КонецЦикла;
		
	КонецЦикла;	

КонецПроцедуры

Процедура ВзносыТаблица_СФР(ИсходныеДанные,РезультатРасчета)
	
	ТарифВПределахЛимита    = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифВПределахЛимита","СФР");
	ТарифСверхЛимита		= ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифСверхЛимита","СФР"); 
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.СФР = ВзносыСтрока.СФР
			                 + ВзносыСтрока.ОблагаемаяБаза * ТарифВПределахЛимита;
							
			ВзносыСтрока.СФРПревышение = ВзносыСтрока.СФРПревышение
			 				           + ВзносыСтрока.ОблагаемаяБазаПревышение * ТарифСверхЛимита;      
									   
			ВзносыСтрока.СФРИтого = ВзносыСтрока.СФР + ВзносыСтрока.СФРПревышение;						   
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_ПФ(ИсходныеДанные,РезультатРасчета)
	
	ТарифВПределахЛимита    = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифВПределахЛимита","ПФ");
	ТарифСверхЛимита		= ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифСверхЛимита","ПФ"); 
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.ПФ = ВзносыСтрока.ПФ
			                + ВзносыСтрока.ОблагаемаяБаза * ТарифВПределахЛимита;
							
			ВзносыСтрока.ПФПревышение = ВзносыСтрока.ПФПревышение
			 				          + ВзносыСтрока.ОблагаемаяБазаПревышение * ТарифСверхЛимита;
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_ФФОМС(ИсходныеДанные,РезультатРасчета)
	
	ТарифВПределахЛимита    = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифВПределахЛимита","ФФОМС");
	ТарифСверхЛимита		= ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифСверхЛимита","ФФОМС"); 
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			Если НЕ ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
				
				ВзносыСтрока.ФФОМС = ВзносыСтрока.ФФОМС
				                   + (ВзносыСтрока.ОблагаемаяБаза + ВзносыСтрока.ОблагаемаяБазаПревышение) 
								   * ТарифВПределахЛимита;
			Иначе					   
				
				ВзносыСтрока.ФФОМС = ВзносыСтрока.ФФОМС
			                       + ВзносыСтрока.ОблагаемаяБаза * ТарифВПределахЛимита;
							   
				ВзносыСтрока.ФФОМСПревышение = ВзносыСтрока.ФФОМСПревышение
								   + ВзносыСтрока.ОблагаемаяБазаПревышение * ТарифСверхЛимита;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_ФСС(ИсходныеДанные,РезультатРасчета)
	
	ТарифВПределахЛимита    = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифВПределахЛимита","ФСС");
	ТарифСверхЛимита		= ПолучучитьНастройкиВЗН(ИсходныеДанные,"ТарифСверхЛимита","ФСС"); 
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	ВременноПребывающий = Перечисления.инкСтатусыИностранцев.ВременноПребывающий;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ВзносыСтрока.ФСС = ВзносыСтрока.ФСС
			                 + ВзносыСтрока.ОблагаемаяБазаФСС * ТарифВПределахЛимита;
							 
			ВзносыСтрока.ФССПревышение = ВзносыСтрока.ФССПревышение
							 + ВзносыСтрока.ОблагаемаяБазаФССПревышение * ТарифСверхЛимита;
							 
			Если СотрудникСтрока.СтатусИностранца = ВременноПребывающий ИЛИ СотрудникСтрока.Иностранец  Тогда
				ВзносыСтрока.ФССИностранцы = ВзносыСтрока.ФССИностранцы
				                           + ВзносыСтрока.ОблагаемаяБазаФСС * ТарифВПределахЛимита;
			КонецЕсли; 				 
				 
							 
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_НСиПЗ(ИсходныеДанные,РезультатРасчета)
	
	ТарифТравматизм = Константы.инкФондТравматизма.Получить()/100;
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		СтавкаИнвалида = 1;
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			ОблагаемаяБазаНСиПЗ = ВзносыСтрока.ОблагаемаяБазаФСС
			                     + ВзносыСтрока.ОблагаемаяБазаФССПревышение;
							     //+ ВзносыСтрока.ДоговорыГПХ;
			Если ИсходныеДанные.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
				ОблагаемаяБазаНСиПЗ = ВзносыСтрока.Доход
				                    - ВзносыСтрока.Ст9ФЗ212
									- ВзносыСтрока.ДоговорыГПХ;
			КонецЕсли;	
			
			ВзносыСтрока.ОблагаемаяБазаНСиПЗ = ОблагаемаяБазаНСиПЗ;
			ВзносыСтрока.НСиПЗ = ОблагаемаяБазаНСиПЗ * ТарифТравматизм * СтавкаИнвалида;
				             			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВзносыТаблица_НДФЛ(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	СотрудникиТаблица = ИсходныеДанные.СотрудникиТаблица;
	НДФЛТаблица = ИсходныеДанные.НДФЛТаблица;
	
	Для каждого СотрудникСтрока из СотрудникиТаблица Цикл
		
		ПоискСтроки = Новый Структура("Сотрудник",СотрудникСтрока.Сотрудник);
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтроки);
		
		Для каждого ВзносыСтрока из ВзносыМассив Цикл
			
			НДФЛПоиск = Новый Структура;
			НДФЛПоиск.Вставить("Сотрудник",СотрудникСтрока.Сотрудник);
			НДФЛПоиск.Вставить("Период",ВзносыСтрока.Период);
			НДФЛМассив = НДФЛТаблица.НайтиСтроки(НДФЛПоиск); 
			Для каждого НДФЛСтрока Из НДФЛМассив Цикл
				ВзносыСтрока.НДФЛ = ВзносыСтрока.НДФЛ
				                  + НДФЛСтрока.НДФЛ;
			КонецЦикла;	
				             			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

Функция РасчетСреднесписочнойЧисленности(ВзносыСтрока,НачисленияТаблица)
	
	СреднесписочнаяЧисленность = 0;
	
	Если ВзносыСтрока.Доход > 0 Тогда
		
		КоличествоДнейВМесяце = День(КонецМесяца(ВзносыСтрока.Период));
		
		КоличествоОтработанныхДнейВМесяце = КоличествоДнейВМесяце;
		
		// Если приняли на работу в текущем месяце:
		Если (ВзносыСтрока.ДатаПриемаНаРаботу >= НачалоМесяца(ВзносыСтрока.Период)) И
			(ВзносыСтрока.ДатаПриемаНаРаботу <= КонецМесяца(ВзносыСтрока.Период))
			Тогда
			КоличествоОтработанныхДнейВМесяце = КоличествоДнейВМесяце -
			День(ВзносыСтрока.ДатаПриемаНаРаботу);
		КонецЕсли; 
		
		// Если уволили в текущем месяце:
		Если (ВзносыСтрока.ДатаУвольнения >= НачалоМесяца(ВзносыСтрока.Период)) И
			(ВзносыСтрока.ДатаУвольнения <= КонецМесяца(ВзносыСтрока.Период))
			Тогда
			КоличествоОтработанныхДнейВМесяце = День(ВзносыСтрока.ДатаУвольнения);
		КонецЕсли; 
		
		// Если есть начисления по уходу за ребенком:
		Если КоличествоОтработанныхДнейВМесяце > 0 Тогда
			
			ПоискСтруктура = Новый Структура;
			ПоискСтруктура.Вставить("Сотрудник",ВзносыСтрока.Сотрудник);
			ПоискСтруктура.Вставить("Месяц",ВзносыСтрока.Месяц);
				
			НачисленияМассив = НачисленияТаблица.НайтиСтроки(ПоискСтруктура);

			Если НачисленияМассив.Количество() > 0 Тогда
				
				ПособиеПоУходуЗаребенкомНачисление = ПланыВидовРасчета.инкНачисления.НайтиПоКоду("00028");
				Для каждого НачислениеЭлемент Из НачисленияМассив Цикл
					
					Если НачислениеЭлемент.Начисление = ПособиеПоУходуЗаребенкомНачисление Тогда
						КоличествоОтработанныхДнейВМесяце = 0;
						Прервать;
					КонецЕсли; 
					
				КонецЦикла; 
				
			КонецЕсли; 
			
		КонецЕсли;
		
		Если КоличествоОтработанныхДнейВМесяце < 0 Тогда
			КоличествоОтработанныхДнейВМесяце = 0;
		КонецЕсли; 
		
		Если КоличествоОтработанныхДнейВМесяце > 0 Тогда
			СреднесписочнаяЧисленность = Окр(КоличествоОтработанныхДнейВМесяце / КоличествоДнейВМесяце,2);
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат СреднесписочнаяЧисленность;
		
КонецФункции

Процедура ВзносыТаблица_Численность(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	
	Для каждого ВзносыСтрока Из ВзносыТаблица_Месяц Цикл
		
		ВзносыСтрока.СреднесписочнаяЧисленность = РасчетСреднесписочнойЧисленности(ВзносыСтрока,НачисленияТаблица);
		
		Если ВзносыСтрока.Доход <> 0 Тогда
		
			Если ВзносыСтрока.Пол = Перечисления.инкПол.Мужской Тогда
				ВзносыСтрока.МужчиныКоличество = 1;
			Иначе
				ВзносыСтрока.ЖенщиныКоличество = 1;
			КонецЕсли; 

			Если ВзносыСтрока.Инвалид Тогда
				ВзносыСтрока.ИнвалидыКоличество = 1;
			КонецЕсли;
			
			Если ВзносыСтрока.ОблагаемаяБаза <> 0 Тогда
				ВзносыСтрока.ЗастрахованныеПФКоличество	= 1;
				ВзносыСтрока.ЗастрахованныеКоличество = 1;
			КонецЕсли; 
			
			Если ВзносыСтрока.ОблагаемаяБазаПревышение <> 0 Тогда
				ВзносыСтрока.ЗастрахованныеПФПревышениеБазыКоличество	= 1;
			КонецЕсли; 
			
			Если ВзносыСтрока.ОблагаемаяБазаФСС <> 0 Тогда
				ВзносыСтрока.ЗастрахованныеФССКоличество = 1;
				ВзносыСтрока.ЗастрахованныеКоличество = 1;
			КонецЕсли; 
			
			Если ВзносыСтрока.ОблагаемаяБазаФССПревышение <> 0 Тогда
				ВзносыСтрока.ЗастрахованныеФССПревышениеБазыКоличество	= 1;
			КонецЕсли; 
			
		КонецЕсли;
			
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#Область о // Сводные данные:

Процедура РасчетСводныхДанных(ИсходныеДанные,РезультатРасчета,СводныеДанные)
	
	СводныеДанные_Таблица_Месяц_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	СводныеДанные_Таблица_Квартал_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	СводныеДанные_Таблица_Год_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	//
	СводныеДанные_Численность(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	//
	СводныеДанные_Пособия(ИсходныеДанные,РезультатРасчета,СводныеДанные);
		
КонецПроцедуры

#Область о // Сводные данные [ВЗНОСЫ]:

Процедура СводныеДанные_Таблица_Месяц_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные)
    
    ВзносыСводТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц.Скопировать();
	
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	ВзносыСводТаблица_Месяц.Свернуть("Месяц,Период",ПоляДляСуммирования);
    
    СводныеДанные.Взносы.ВзносыСводТаблица_Месяц = ВзносыСводТаблица_Месяц;
    
КонецПроцедуры

Процедура СводныеДанные_Таблица_Квартал_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные)
    
    ВзносыСводТаблица_Квартал = РезультатРасчета.ВзносыТаблица_Квартал.Скопировать();
	
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	ВзносыСводТаблица_Квартал.Свернуть("Квартал,Период",ПоляДляСуммирования);
    
    СводныеДанные.Взносы.ВзносыСводТаблица_Квартал = ВзносыСводТаблица_Квартал;
	
КонецПроцедуры
   
Процедура СводныеДанные_Таблица_Год_Рассчет(ИсходныеДанные,РезультатРасчета,СводныеДанные);
    
    ВзносыСводТаблица_Год = РезультатРасчета.ВзносыТаблица_Год.Скопировать();
    
    ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
    ВзносыСводТаблица_Год.Свернуть("",ПоляДляСуммирования);
    
    СводныеДанные.Взносы.ВзносыСводТаблица_Год = ВзносыСводТаблица_Год;
    
КонецПроцедуры

#КонецОбласти 

#Область о // Сводные данные [ЧИСЛЕННОСТЬ]:

Процедура ДобавитьКЧисленности(ИмяКолонки,ЧисленностьСтрока,ВзносыЭлемент)
	
	ЧисленностьСтрока[ИмяКолонки] = ЧисленностьСтрока[ИмяКолонки]
	                              + ВзносыЭлемент[ИмяКолонки]; 
	
КонецПроцедуры

Процедура ДобавитьКЧисленностиПериод(ИмяКолонки,ЧисленностьСтрока,ВзносыЭлемент)
	
	Если ВзносыЭлемент[ИмяКолонки] <> 0 Тогда
		ЧисленностьСтрока[ИмяКолонки] = 1;	
	КонецЕсли; 
	
КонецПроцедуры

Процедура СвернутьЧисленностьТаблица(ЧисленностьСводТаблица)
	
	ПоляДляСуммированияЧисленности = "";
	Для каждого КолонкаТаблицы Из ЧисленностьСводТаблица.Колонки Цикл
		Если КолонкаТаблицы.Имя <> "Период" Тогда
			ПоляДляСуммированияЧисленности = ПоляДляСуммированияЧисленности 
										   + КолонкаТаблицы.Имя 
										   + ",";
		КонецЕсли; 
	КонецЦикла; 

	ПоляДляСуммированияЧисленности = Лев(ПоляДляСуммированияЧисленности,СтрДлина(ПоляДляСуммированияЧисленности)-1);
	ЧисленностьСводТаблица.Свернуть("Период",ПоляДляСуммированияЧисленности);
	
КонецПроцедуры

Процедура СводныеДанные_Численность(ИсходныеДанные,РезультатРасчета,СводныеДанные)
	
	СводныеДанные_Численность_Месяц(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	СводныеДанные_Численность_Квартал(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	СводныеДанные_Численность_Год(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	
КонецПроцедуры	

Процедура СводныеДанные_Численность_Месяц(ИсходныеДанные,РезультатРасчета,СводныеДанные)
	
	ЧисленностьСводТаблица_Месяц 	= СводныеДанные.Численность.ЧисленностьСводТаблица_Месяц;	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	
	Для каждого СотрудникСтрока из ИсходныеДанные.СотрудникиТаблица Цикл

		Период   = НачалоМесяца(ИсходныеДанные.Дата1);
		Для Месяц = 1 По 12 Цикл 
			
			ЧисленностьМесяцСтрока 			= ЧисленностьСводТаблица_Месяц.Добавить();
			ЧисленностьМесяцСтрока.Период 	= Период;
			
			ПоискСтруктура = Новый Структура;
			ПоискСтруктура.Вставить("Месяц", Месяц);
			ПоискСтруктура.Вставить("Сотрудник", СотрудникСтрока.Сотрудник);
			
			ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтруктура);
			Для каждого ВзносыЭлемент Из ВзносыМассив Цикл
				
				Для каждого КолонкаТаблицы Из ЧисленностьСводТаблица_Месяц.Колонки Цикл

					Если КолонкаТаблицы.Имя <> "Период" Тогда
						ДобавитьКЧисленности(КолонкаТаблицы.Имя,ЧисленностьМесяцСтрока,ВзносыЭлемент);
					КонецЕсли; 
					
				КонецЦикла;
				
			КонецЦикла;
			
			Период = ДобавитьМесяц(Период,1);
			
		КонецЦикла;	
		
	КонецЦикла;
	
	СвернутьЧисленностьТаблица(ЧисленностьСводТаблица_Месяц);
	
КонецПроцедуры

Процедура СводныеДанные_Численность_Квартал(ИсходныеДанные,РезультатРасчета,СводныеДанные)
	
	ЧисленностьСводТаблица_Квартал 	= СводныеДанные.Численность.ЧисленностьСводТаблица_Квартал;	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	
	Для каждого СотрудникСтрока из ИсходныеДанные.СотрудникиТаблица Цикл

		Период   = НачалоМесяца(ИсходныеДанные.Дата1);
		Для Квартал = 1 По 4 Цикл 
			
			ЧисленностьКварталСтрока 		= ЧисленностьСводТаблица_Квартал.Добавить();
			ЧисленностьКварталСтрока.Период 	= НачалоКвартала(Период);
			
			ПоискСтруктура = Новый Структура;
			ПоискСтруктура.Вставить("Квартал", Квартал);
			ПоискСтруктура.Вставить("Сотрудник", СотрудникСтрока.Сотрудник);
			
			ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтруктура);
			Для каждого ВзносыЭлемент Из ВзносыМассив Цикл
				
				Для каждого КолонкаТаблицы Из ЧисленностьСводТаблица_Квартал.Колонки Цикл

					Если КолонкаТаблицы.Имя = "Период" Тогда
						Продолжить;
					ИначеЕсли КолонкаТаблицы.Имя = "СреднесписочнаяЧисленность" Тогда
						ДобавитьКЧисленности(КолонкаТаблицы.Имя,ЧисленностьКварталСтрока,ВзносыЭлемент);
					Иначе
						ДобавитьКЧисленностиПериод(КолонкаТаблицы.Имя,ЧисленностьКварталСтрока,ВзносыЭлемент);
					КонецЕсли; 
					
				КонецЦикла;

			КонецЦикла;
			
			ЧисленностьКварталСтрока.СреднесписочнаяЧисленность = Окр(ЧисленностьКварталСтрока.СреднесписочнаяЧисленность / 3,2);
			Период = ДобавитьМесяц(Период,3);
			
		КонецЦикла;	
		
	КонецЦикла;
	
	СвернутьЧисленностьТаблица(ЧисленностьСводТаблица_Квартал);
	
КонецПроцедуры

Процедура СводныеДанные_Численность_Год(ИсходныеДанные,РезультатРасчета,СводныеДанные)
	
	ЧисленностьСводТаблица_Год 	= СводныеДанные.Численность.ЧисленностьСводТаблица_Год;	
	ВзносыТаблица_Месяц = РезультатРасчета.ВзносыТаблица_Месяц;
	
	Для каждого СотрудникСтрока из ИсходныеДанные.СотрудникиТаблица Цикл

		ЧисленностьГодСтрока 		= ЧисленностьСводТаблица_Год.Добавить();
		ЧисленностьГодСтрока.Период	= НачалоГода(ИсходныеДанные.Дата1);
		
		ПоискСтруктура = Новый Структура;
		ПоискСтруктура.Вставить("Сотрудник", СотрудникСтрока.Сотрудник);
		
		ВзносыМассив = ВзносыТаблица_Месяц.НайтиСтроки(ПоискСтруктура);
		Для каждого ВзносыЭлемент Из ВзносыМассив Цикл
			
			Для каждого КолонкаТаблицы Из ЧисленностьСводТаблица_Год.Колонки Цикл

				Если КолонкаТаблицы.Имя = "Период" Тогда
					Продолжить;
				ИначеЕсли КолонкаТаблицы.Имя = "СреднесписочнаяЧисленность" Тогда
					ДобавитьКЧисленности(КолонкаТаблицы.Имя,ЧисленностьГодСтрока,ВзносыЭлемент);
				Иначе
					ДобавитьКЧисленностиПериод(КолонкаТаблицы.Имя,ЧисленностьГодСтрока,ВзносыЭлемент);
				КонецЕсли; 
				
			КонецЦикла;
			
		КонецЦикла;
		
		ЧисленностьГодСтрока.СреднесписочнаяЧисленность = Окр(ЧисленностьГодСтрока.СреднесписочнаяЧисленность / 12,2);
		
	КонецЦикла;
	
	СвернутьЧисленностьТаблица(ЧисленностьСводТаблица_Год);
	
КонецПроцедуры

#КонецОбласти 

#Область о // Сводные данные [ПОСОБИЯ]:

Процедура СводныеДанные_Пособия(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	
	НачисленияТаблица = ИсходныеДанные.НачисленияТаблица;
	Пособия_Год = СводныеДанные.Пособия.Пособия_Год;
	
	ПоискСтруктура = Новый Структура;
	ПоискСтруктура.Вставить("Начисление");
	
	МесяцДата = НачалоГода(ИсходныеДанные.Дата1);
	
	Пособия_Год.Период = МесяцДата;
	
КонецПроцедуры
             
#КонецОбласти 

#КонецОбласти 

#Область о // Расчет взносов - регресс:

Процедура Регресс_Взносы_Рассчет(ИсходныеДанные,РезультатРасчета)

	Если НЕ ЗначениеЗаполнено(ДанныеДляРасчетаВзносов.Организация) Тогда
		Возврат;
	КонецЕсли;	
	
	Если НЕ ДанныеДляРасчетаВзносов.Организация.ПредприятиеМалогоИлиСреднегоБизнеса Тогда
		Возврат;	
	КонецЕсли;
	
	ПредельнаяВеличинаБазы = ПолучучитьНастройкиВЗН(ИсходныеДанные,"ОблагаемаяБаза","ПФ"); 
	ВзносыТаблица_Год = РезультатРасчета.ВзносыТаблица_Год;
	Регресс_Взносы = РезультатРасчета.Регресс_Взносы;
	Регресс_Взносы.Очистить();
	
	СтрокаЛимит         = Регресс_Взносы.Добавить();
	СтрокаСверхЛимита   = Регресс_Взносы.Добавить(); 
	
	//
	СтрокаЛимит.СтрокаТарифа        = "Всего в пределах МРОТ: ";
	СтрокаСверхЛимита.СтрокаТарифа  = "Всего свыше МРОТ: "; 
	//
	
	Для каждого СтрокаВзносы из ВзносыТаблица_Год Цикл
		
		// Численность:
		Если СтрокаВзносы.ОблагаемаяБаза <> 0 Тогда
			СтрокаЛимит.Численность	= СтрокаЛимит.Численность + 1;
		КонецЕсли;
		Если СтрокаВзносы.ОблагаемаяБазаПревышение <> 0 Тогда
			СтрокаСверхЛимита.Численность = СтрокаСверхЛимита.Численность + 1;
		КонецЕсли;
		
        // Облагаемая база + превышение:
		СтрокаЛимит.ОблагаемаяБазаСПревышением  = СтрокаЛимит.ОблагаемаяБазаСПревышением
		                                        + СтрокаВзносы.ОблагаемаяБаза;
		СтрокаСверхЛимита.ОблагаемаяБазаСПревышением  = СтрокаСверхЛимита.ОблагаемаяБазаСПревышением
		                                              + СтрокаВзносы.ОблагаемаяБазаПревышение;
		// Облагаемая база:										
		СтрокаЛимит.ОблагаемаяБаза  = СтрокаЛимит.ОблагаемаяБаза
		                            + СтрокаВзносы.ОблагаемаяБаза;
		СтрокаСверхЛимита.ОблагаемаяБаза  = СтрокаСверхЛимита.ОблагаемаяБаза
		                                  + СтрокаВзносы.ОблагаемаяБазаПревышение;
		// ПФ:
		СтрокаЛимит.ПФ = СтрокаЛимит.ПФ
	                   + СтрокаВзносы.ПФ;
		СтрокаСверхЛимита.ПФ = СтрокаСверхЛимита.ПФ
		                     + СтрокаВзносы.ПФПревышение;
		// ФСС:
		СтрокаЛимит.ФСС = СтрокаЛимит.ФСС
		                + СтрокаВзносы.ФСС;
		СтрокаСверхЛимита.ФСС = СтрокаСверхЛимита.ФСС
		                  	  + СтрокаВзносы.ФССПревышение;
		// ФФОМС:
		СтрокаЛимит.ФФОМС = СтрокаЛимит.ФФОМС
	                      + СтрокаВзносы.ФФОМС; 
		СтрокаСверхЛимита.ФФОМС = СтрокаСверхЛимита.ФФОМС
		                  	  + СтрокаВзносы.ФФОМСПревышение;
        // НСиПЗ:
		СтрокаЛимит.НСиПЗ = СтрокаЛимит.НСиПЗ
						  + СтрокаВзносы.НСиПЗ; 
		
		// Всего ВЗН: 
		СтрокаЛимит.ВЗНВсего = СтрокаЛимит.ВЗНВсего
	                         + СтрокаВзносы.ПФ
							 + СтрокаВзносы.ФФОМС
							 + СтрокаВзносы.ФСС; 

		СтрокаСверхЛимита.ВЗНВсего = СтрокаСверхЛимита.ВЗНВсего
	                         + СтрокаВзносы.ПФПревышение
							 + СтрокаВзносы.ФФОМСПревышение
							 + СтрокаВзносы.ФССПревышение; 
		
		
	КонецЦикла;
		
КонецПроцедуры	

#КонецОбласти

#Область о // Расчет взносов:

Процедура ВыполнитьРасчетВзносов() Экспорт
	
	ВремяНачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ИсходныеДанные = ПолучитьИсходныеДанные(ДанныеДляРасчетаВзносов);
    // 
	ВзносыТаблица_Месяц_Рассчет(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_Квартал_Рассчет(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_Год_Рассчет(ИсходныеДанные,РезультатРасчета);
	//
	Регресс_Взносы_Рассчет(ИсходныеДанные,РезультатРасчета);
    //
	РасчетСводныхДанных(ИсходныеДанные,РезультатРасчета,СводныеДанные);
	//
	Взносы_СформироватьТабличныеДокументы(ИсходныеДанные);
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("Обработка.инкРасчетВзносов.РасчетДанных",ВремяНачалоЗамера,ИсходныеДанные.СотрудникиТаблица.Количество());
	
КонецПроцедуры

Процедура ВзносыТаблица_Месяц_Рассчет(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_НачальноеЗаполнениеДанными(ИсходныеДанные,РезультатРасчета);
	//
	ВзносыТаблица_Доход(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_Ст9ФЗ212(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_МатПомощь(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ДоговорыГПХ(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ВыплатыИнвалидам(ИсходныеДанные,РезультатРасчета);
	//
	ВзносыТаблица_ОблагаемаяБаза(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ОблагаемаяБазаФСС(ИсходныеДанные,РезультатРасчета);
    //              
	ВзносыТаблица_СФР(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ПФ(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ФФОМС(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_ФСС(ИсходныеДанные,РезультатРасчета);
	ВзносыТаблица_НСиПЗ(ИсходныеДанные,РезультатРасчета);
	//                                                   
	ВзносыТаблица_НДФЛ(ИсходныеДанные,РезультатРасчета);
	//
	ВзносыТаблица_Всего(ИсходныеДанные,РезультатРасчета);
	//
	ВзносыТаблица_Численность(ИсходныеДанные,РезультатРасчета);
	               
КонецПроцедуры

Функция ПолучитьСтруктуруПоиска(ВзносыСтрока)
	
	ПоискСтруктура = Новый Структура;
	ПоискСтруктура.Вставить("Сотрудник");
	ПоискСтруктура.Вставить("Месяц");
	
	ЗаполнитьЗначенияСвойств(ПоискСтруктура,ВзносыСтрока);
	
	Возврат ПоискСтруктура;	
	
КонецФункции

Процедура ВзносыТаблица_Год_Рассчет(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Год	= ПолучитьСтруктуруВзносы_Год(ИсходныеДанные,РезультатРасчета);
	РезультатРасчета.ВзносыТаблица_Год = ВзносыТаблица_Год;
	
КонецПроцедуры

Процедура ВзносыТаблица_Квартал_Рассчет(ИсходныеДанные,РезультатРасчета)
	
	ВзносыТаблица_Квартал = РезультатРасчета.ВзносыТаблица_Месяц.Скопировать();
	УдалитьСтрокиНеВходящиеВПериод(ИсходныеДанные, ВзносыТаблица_Квартал);
	Для каждого СтрокаТаблицы Из ВзносыТаблица_Квартал Цикл
		СтрокаТаблицы.Период = НачалоКвартала(СтрокаТаблицы.Период);
	КонецЦикла; 
	
	ПоляДляГруппировки = ПолучитьПоляДляГруппировкиТаблицВЗН()+",Квартал,Период";
	ПоляДляСуммирования = ПолучитьПоляДляСуммированияТаблицВЗН();
	
	ВзносыТаблица_Квартал.Свернуть(ПоляДляГруппировки,ПоляДляСуммирования);
    РезультатРасчета.ВзносыТаблица_Квартал = ВзносыТаблица_Квартал;
    
КонецПроцедуры

Функция ПолучитьПоляДляГруппировкиТаблицВЗН()
	
	Возврат "Сотрудник,"
	  	  + "ФИО,"
	  	  + "ГодРождения,"
	  	  + "СтатусРезидента,"
		  + "Инвалид";
	
КонецФункции

Функция ПолучитьПоляДляСуммированияТаблицВЗН()
	
	Возврат "Доход,"
        + "ОблагаемаяБаза," 
		+ "ОблагаемаяБазаПревышение,"   
		+ "ОблагаемаяБазаВсего,"   
		+ "ОблагаемаяБазаФСС,"
		+ "ОблагаемаяБазаФССПревышение,"
		+ "Ст9ФЗ212,"
		+ "МатПомощь,"
		+ "ВыплатыИнвалидам,"
		+ "ДоговорыГПХ,"
		+ "ПФ,"
		+ "ПФПревышение,"
		+ "ФФОМС,"
		+ "ФФОМСПревышение,"
		+ "НСиПЗ,"
		+ "ФСС,"
		+ "ФССПревышение,"
		+ "ФССИностранцы,"          
		+ "ВЗНВсего,"
		+ "ПФВсего,"
		+ "ФФОМСВсего,"
		+ "ФССВсего,"
		+ "НДФЛ,"
		+ "СФР,"
		+ "СФРПревышение," 
		+ "СФРИтого," 
		+ "ОблагаемаяБазаНСиПЗ,"
		
		+ "СреднесписочнаяЧисленность";
		
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

