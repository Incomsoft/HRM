					
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначениямиПоУмолчанию();
		
КонецПроцедуры             

&НаКлиенте
Процедура ПриОткрытии(Отказ)        
	
	СформироватьОтчет(Неопределено);
	//СформироватьОтчетОтладка();

КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетОтладка()
	
	мОрганизация = Справочники.инкОрганизации.ПолучитьТекущуюОрганизацию();
	мДата1 		= НачалоГода(ТекущаяДата());
	мДата2		= ДобавитьМесяц(мДата1,3);
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",мОрганизация);
	ПараметрыОтчета.Вставить("Дата1",мДата1);
	ПараметрыОтчета.Вставить("Дата2",мДата2);
	
	СформироватьОтчетНаСервере(ПараметрыОтчета);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначениямиПоУмолчанию()
	
	мОрганизация 	= Неопределено;
	ПериодВЗапросе 	= Неопределено;
	
КонецПроцедуры                        

&НаКлиенте
Процедура СформироватьОтчет(Команда)                      

	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборПериодаИОрганизацииЗавершение", ЭтотОбъект);
	
	ПараметрыОткрытияФормы = Новый Структура; 
	ПараметрыОткрытияФормы.Вставить("Организация"); 
	ОткрытьФорму("ОбщаяФорма.инкВыборСтандартногоПериодаГодКвартал", ПараметрыОткрытияФормы, , , , , ОписаниеОповещения);

КонецПроцедуры    

&НаКлиенте
Процедура ВыборПериодаИОрганизацииЗавершение(Знач РезультатРедактирования, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРедактирования) Тогда
		
		Если НЕ ЗначениеЗаполнено(РезультатРедактирования.Организация) Тогда
			инкОбщийКлиентСервер.СообщитьПользователю("Внимание! Необходимо выбрать организацию.");
			Возврат;
		КонецЕсли;
		
		СформироватьОтчетНаСервере(РезультатРедактирования);
		
		мОрганизация 	= РезультатРедактирования.Организация;
		мДата1			= РезультатРедактирования.Дата1;
		мДата2			= РезультатРедактирования.Дата2;
		
	КонецЕсли;

КонецПроцедуры 

&НаСервере
Процедура СформироватьОтчетНаСервере(ПараметрыОтчета)
	
	МенеджерПечати = РеквизитФормыВЗначение("Отчет");
	ТабличныеДокументыОтчета = МенеджерПечати.СформироватьТабличныеДокументы(ПараметрыОтчета);

	Титульный 		= ТабличныеДокументыОтчета.Титульный;
	Раздел1 		= ТабличныеДокументыОтчета.Раздел1;
	Раздел1Подр1 	= ТабличныеДокументыОтчета.Раздел1Подр1; 
	Раздел1Подр1_2  = ТабличныеДокументыОтчета.Раздел1Подр1_2;
	Раздел1Подр1_20		= ТабличныеДокументыОтчета.Раздел1Подр1_20; 
	Раздел1Подр1_20_2	= ТабличныеДокументыОтчета.Раздел1Подр1_20_2;
	Раздел3			= ТабличныеДокументыОтчета.Раздел3;
	
	УстановитьНастройкиТабличногоДокумента(Титульный);
	УстановитьНастройкиТабличногоДокумента(Раздел1);
	УстановитьНастройкиТабличногоДокумента(Раздел1Подр1);
	УстановитьНастройкиТабличногоДокумента(Раздел1Подр1_2);
	УстановитьНастройкиТабличногоДокумента(Раздел1Подр1_20);
	УстановитьНастройкиТабличногоДокумента(Раздел1Подр1_20_2);
	УстановитьНастройкиТабличногоДокумента(Раздел3);

КонецПроцедуры 

&НаСервере
Процедура УстановитьНастройкиТабличногоДокумента(ТабличныйДокумент)

	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	инкОтчетыСервер.УстановитьНастройкиМаштабаДокумента(ТабличныйДокумент);
	ТабличныйДокумент.Автомасштаб = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	СформироватьОтчет(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ПериодУдержанияПриИзменении(Элемент)
	
	СформироватьОтчет(Неопределено);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	
	Если мОрганизация = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ФайлВыгрузки = СформироватьXMLФайлНаСервере();
	Если НЕ ЗначениеЗаполнено(ФайлВыгрузки.ФайлАдрес) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ФайлВыгрузкиАдрес",ФайлВыгрузки.ФайлАдрес);
	ДополнительныеПараметры.Вставить("ИмяФайла",ФайлВыгрузки.ФайлИмя);
	ДополнительныеПараметры.Вставить("ВидКлиента");
	
	#Если ВебКлиент Тогда     
		
		ДополнительныеПараметры.ВидКлиента = "ВебКлиент";	
		
		списокКнопокВопроса = новый СписокЗначений();
		списокКнопокВопроса.Добавить("Сохранить","Сохранить");
		списокКнопокВопроса.Добавить("Отмена","Отмена");
		
		Оповещение = новый ОписаниеОповещения("СохранитьФайлЗавершение",ЭтотОбъект,ДополнительныеПараметры);
	  	ПоказатьВопрос(Оповещение,"Выберите действие",списокКнопокВопроса,30,"Сохранить","Сохранить файл выгрузки?","Отмена");

	#Иначе	
		
		ДополнительныеПараметры.ВидКлиента = "";	

		// Выбор файла для открытия:	
		Режим = РежимДиалогаВыбораФайла.Сохранение;
		Диалог = Новый ДиалогВыбораФайла(Режим);
		Диалог.ПолноеИмяФайла = ДополнительныеПараметры.ИмяФайла;
		
		ОбратныйВызов = Новый ОписаниеОповещения("СохранитьФайлЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
		// Открывает диалоговое окно:
		Диалог.Показать(ОбратныйВызов);
		
	#КонецЕсли 	

КонецПроцедуры            

&НаКлиенте
Процедура СохранитьФайлЗавершение(РезультатВопроса,ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	ФайлВыгрузкиАдрес = ДополнительныеПараметры.ФайлВыгрузкиАдрес;
	Если ДополнительныеПараметры.ВидКлиента = "ВебКлиент" Тогда

		Если РезультатВопроса = "Сохранить" тогда
			НачатьПолучениеФайлаССервера(ФайлВыгрузкиАдрес,ДополнительныеПараметры.ИмяФайла);
		Иначе
			Возврат;
		КонецЕсли;		

	Иначе                              
		ПутьКФайлу = РезультатВопроса[0];
		НачатьПолучениеФайлаССервера(,ФайлВыгрузкиАдрес, ПутьКФайлу);	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СформироватьXMLФайлНаСервере()
	
	МенеджерОтчета = РеквизитФормыВЗначение("Отчет");
    //
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",мОрганизация);
	ПараметрыОтчета.Вставить("Дата1",мДата1);
	ПараметрыОтчета.Вставить("Дата2",мДата2);
	ФайлВыгрузкиСтруктура = МенеджерОтчета.СформироватьФайл(ПараметрыОтчета);
	//
	Возврат ФайлВыгрузкиСтруктура; 
	
КонецФункции
