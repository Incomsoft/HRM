
// Процедура - Включение подсистемы "Оценка производительности АПДЕХ":
//
Процедура ВключениеАПДЕХНаСервере() Экспорт
	
	Константы.ВыполнятьЗамерыПроизводительности.Установить(Истина); 
	Константы.ОценкаПроизводительностиПериодЗаписи.Установить(5);
	
КонецПроцедуры

// Функция - Добавить ключевую операцию
//
// Параметры:
//  КлючеваяОперацияСтруктура	 - Структура	 - Структура для заполнения полей справочника
// 
// Возвращаемое значение:
//   - Ссылка
//
Функция ДобавитьКлючевуюОперацию(КлючеваяОперацияСтруктура) Экспорт

	КлючеваяОперацияСсылка = Справочники.КлючевыеОперации.НайтиПоРеквизиту("Имя",КлючеваяОперацияСтруктура.Имя);
	
	Если ЗначениеЗаполнено(КлючеваяОперацияСсылка) Тогда
		КлючеваяОперацияОбъект = КлючеваяОперацияСсылка.ПолучитьОбъект();
	Иначе
		КлючеваяОперацияОбъект = Справочники.КлючевыеОперации.СоздатьЭлемент();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(КлючеваяОперацияОбъект,КлючеваяОперацияСтруктура);
	КлючеваяОперацияОбъект.Записать();
	
	Возврат КлючеваяОперацияОбъект.Ссылка;
	
КонецФункции

// Процедура - Добавить ключевые операции в профиль
//
// Параметры:
//  ИмяПрофиляСтрока		 - Строка	 - Имя профиля
//  КлючевыеОперацииМассив	 - Массив	 - Массив ссылок на справочник "Ключевые операции"
//
Процедура ДобавитьКлючевыеОперацииВПрофиль(ИмяПрофиляСтрока,КлючевыеОперацииМассив) Экспорт

	ПрофильСсылка = Справочники.ПрофилиКлючевыхОпераций.НайтиПоНаименованию(ИмяПрофиляСтрока);
	Если ЗначениеЗаполнено(ПрофильСсылка) Тогда
		ПрофильОбъект = ПрофильСсылка.ПолучитьОбъект();
	Иначе
		ПрофильОбъект = Справочники.ПрофилиКлючевыхОпераций.СоздатьЭлемент();
	КонецЕсли;
	
	ПрофильОбъект.Наименование = ИмяПрофиляСтрока;
	Для каждого КлючеваяОперацияЭлемент Из КлючевыеОперацииМассив Цикл
		
		КлючеваяОперацияПоиск = Новый Структура;
		КлючеваяОперацияПоиск.Вставить("КлючеваяОперация",КлючеваяОперацияЭлемент);
		
		КлючевыеОперацииПрофиляМассив = ПрофильОбъект.КлючевыеОперацииПрофиля.НайтиСтроки(КлючеваяОперацияПоиск);
		Если КлючевыеОперацииПрофиляМассив.Количество() <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		КлючевыеОперацииПрофиляСтрока = ПрофильОбъект.КлючевыеОперацииПрофиля.Добавить();
		ЗаполнитьЗначенияСвойств(КлючевыеОперацииПрофиляСтрока,КлючеваяОперацияЭлемент);
		КлючевыеОперацииПрофиляСтрока.КлючеваяОперация = КлючеваяОперацияЭлемент;
		
	КонецЦикла;
	             
	ПрофильОбъект.Записать();
	
КонецПроцедуры

