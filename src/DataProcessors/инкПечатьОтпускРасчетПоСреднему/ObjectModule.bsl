#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Получить печатный документ на сервере
//
// Параметры:
//  ДокументСсылка	 - Ссылка	 - Ссылка на документ (источник данных);
//  ВидОтчета		 - Строка	 - Вид отчета "Т60";
// 
// Возвращаемое значение:
// 	ТабличныйДокумент  - Отчет для печати Т-60;
//
Функция ПолучитьПечатныйДокументНаСервере(ДокументСсылка, ВидОтчета = Неопределено) Экспорт
	
	Если ВидОтчета = "Т60" Тогда
		ТабличныйДокумент = ПолучитьТабличныйДокументТ60(ДокументСсылка);	
	Иначе
		ТабличныйДокумент = ПолучитьТабличныйДокументРасчетПоСреднему(ДокументСсылка);
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции                                                            

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТабличныйДокументТ60(ДокументСсылка)
	
	МакетВедомости = ПолучитьМакет("ПФ_MXL_Т60"); 
	ОтпускТабличныйДокумент = Новый ТабличныйДокумент;

	Если Не ЗначениеЗаполнено(ДокументСсылка.Ссылка) Тогда
		Возврат ОтпускТабличныйДокумент;	
	КонецЕсли;	

	// Шапка:
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("Шапка");
	ОбластьДокумента.Параметры.НазваниеОрганизации = ДокументСсылка.Организация;
	ОбластьДокумента.Параметры.НомерДок = ДокументСсылка.Номер;				
	ОбластьДокумента.Параметры.ДатаДок = ДокументСсылка.Дата;		
	ОбластьДокумента.Параметры.Работник = ДокументСсылка.Сотрудник;													
	ОбластьДокумента.Параметры.ТабельныйНомер = ДокументСсылка.Сотрудник.ТабельныйНомер;	
	ОбластьДокумента.Параметры.Подразделение = ДокументСсылка.Подразделение;
	ОбластьДокумента.Параметры.Должность = ДокументСсылка.Должность;
	ОбластьДокумента.Параметры.РазрядКатегория = "";
	ОбластьДокумента.Параметры.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = ДокументСсылка.ПериодС;		
	ОбластьДокумента.Параметры.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = ДокументСсылка.ПериодПО;				
	ОбластьДокумента.Параметры.Продолжительность = ДокументСсылка.ОсновнойОтпускДни;	
	ОбластьДокумента.Параметры.ДатаНачалаЧисло = День(ДокументСсылка.ПериодС);
	ОбластьДокумента.Параметры.ДатаНачалаМесяцГод = Формат(ДокументСсылка.ПериодС,"ДФ='MMММ yyyy'");
	ОбластьДокумента.Параметры.ДатаОкончанияЧисло = День(ДокументСсылка.ПериодПО);
	ОбластьДокумента.Параметры.ДатаОкончанияМесяцГод = Формат(ДокументСсылка.ПериодПО,"ДФ='MMММ yyyy'");;
	ОбластьДокумента.Параметры.ПродолжительностьДоп = ДокументСсылка.ДополнительныйОтпускДни;	  
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	// Доп. отпуск:
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("СтрокаДопОтпуска");
	Если ЗначениеЗаполнено(ДокументСсылка.ДокументОснование) Тогда
		ОбластьДокумента.Параметры.ВидДопОтпуска = ДокументСсылка.ДокументОснование.ВидДополнительногоОтпуска;	
		ОбластьДокумента.Параметры.ДатаДопС = ДокументСсылка.ДокументОснование.ПериодСДопОтпуск;		
		ОбластьДокумента.Параметры.ДатаДопПо = ДокументСсылка.ДокументОснование.ПериодПОДопОтпуск;		
		ОбластьДокумента.Параметры.ОснованиеДопОтпуска = ДокументСсылка.ДокументОснование.Основание;			  
	КонецЕсли;
							
	ОбластьДокумента.Параметры.ДнейКОплатеДополнительногоОтпуска = ДокументСсылка.ДополнительныйОтпускДни;	
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	// Подвал:
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("Подвал");
	ОбластьДокумента.Параметры.ПродолжительностьВсего = ДокументСсылка.ОсновнойОтпускДни + ДокументСсылка.ДополнительныйОтпускДни;
    ОбластьДокумента.Параметры.ДатаНачалаВсегоЧисло = День(ДокументСсылка.ПериодС);
	ОбластьДокумента.Параметры.ДатаНачалаВсегоМесяцГод = Формат(ДокументСсылка.ПериодС,"ДФ='MMММ yyyy'");
	ОбластьДокумента.Параметры.ДатаОкончанияВсегоЧисло = День(ДокументСсылка.ПериодПО);
	ОбластьДокумента.Параметры.ДатаОкончанияВсегоМесяцГод = Формат(ДокументСсылка.ПериодПО,"ДФ='MMММ yyyy'");
	ОбластьДокумента.Параметры.ДолжностьРаботникаКадровойСлужбы = "";						
	ОбластьДокумента.Параметры.РаботникКадровойСлужбыРасшифровкаПодписи = "";	
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	// 				Оборотная сторона формы № Т-60 (сделать позже):
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("РасчетСреднего");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("РасчетСреднегоИтого");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ПодвалРасчета");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	Возврат ОтпускТабличныйДокумент; 
	
КонецФункции                                        

Функция ПолучитьТабличныйДокументРасчетПоСреднему(ДокументСсылка)

	МакетВедомости = ПолучитьМакет("инкОтпускРасчетПоСреднемуМакет"); 
	ОтпускТабличныйДокумент = Новый ТабличныйДокумент;

	Если Не ЗначениеЗаполнено(ДокументСсылка.Ссылка) Тогда
		Возврат ОтпускТабличныйДокумент;	
	КонецЕсли;	
	
	// Заголовок:
	ЗаголовокТекст = ДокументСсылка.Сотрудник.ФИОСокращенное
	               + " - " 
				   + "расчет отпуска за " 
				   + Формат(ДокументСсылка.МесяцНачисления,"ДФ='MMММ yyyy'") + "г. "
				   + " с " + Формат(ДокументСсылка.ПериодС,"ДФ=dd.MM.yyyy")
				   + " по " + Формат(ДокументСсылка.ПериодПо,"ДФ=dd.MM.yyyy") + ".";
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ОтчетЗаголовок");
	ОбластьДокумента.Параметры.ЗаголовокОтчета	= ЗаголовокТекст;
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ОтчетОтступ");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	// Таблица:
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ТаблицаЗаголовок");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	
	Для каждого РасчетныеДанныеСтрока Из ДокументСсылка.РасчетныеДанные Цикл

		ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ТаблицаСтрока");
		ОбластьДокумента.Параметры.Год 		= Формат(РасчетныеДанныеСтрока.Год,"ДФ=yyyy");
		ОбластьДокумента.Параметры.Месяц	= Формат(РасчетныеДанныеСтрока.Месяц,"ДФ=ММ");
		ОбластьДокумента.Параметры.Норма 	= Формат(РасчетныеДанныеСтрока.ДниЧасы,"ЧДЦ=0; ЧН=0");
		ОбластьДокумента.Параметры.Факт 	= Формат(РасчетныеДанныеСтрока.ДниЧасыФакт,"ЧДЦ=0; ЧН=0");
		ОбластьДокумента.Параметры.Г 	= Формат(РасчетныеДанныеСтрока.ПремииГод,"ЧДЦ=2; ЧН=0,00");
		ОбластьДокумента.Параметры.К 	= Формат(РасчетныеДанныеСтрока.ПремииКвартал,"ЧДЦ=2; ЧН=0,00");
		ОбластьДокумента.Параметры.М 	= Формат(РасчетныеДанныеСтрока.ПремииМесяц,"ЧДЦ=2; ЧН=0,00");
		ОбластьДокумента.Параметры.Зарплата = Формат(РасчетныеДанныеСтрока.Зарплата,"ЧДЦ=2; ЧН=0,00");
		ОбластьДокумента.Параметры.Дни 	= Формат(РасчетныеДанныеСтрока.КорректировкаДни,"ЧДЦ=2; ЧН=0");
		ОбластьДокумента.Параметры.Итог = Формат(РасчетныеДанныеСтрока.Итого,"ЧДЦ=2; ЧН=0,00");
		
		ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	КонецЦикла;  
	
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ТаблицаИтог");
	ОбластьДокумента.Параметры.Норма 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("ДниЧасы"),"ЧДЦ=0; ЧН=0");
	ОбластьДокумента.Параметры.Факт 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("ДниЧасыФакт"),"ЧДЦ=0; ЧН=0");
	ОбластьДокумента.Параметры.Г 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("ПремииГод"),"ЧДЦ=2; ЧН=0,00");
	ОбластьДокумента.Параметры.К 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("ПремииКвартал"),"ЧДЦ=2; ЧН=0,00");
	ОбластьДокумента.Параметры.М 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("ПремииМесяц"),"ЧДЦ=2; ЧН=0,00");
	ОбластьДокумента.Параметры.Зарплата = Формат(ДокументСсылка.РасчетныеДанные.Итог("Зарплата"),"ЧДЦ=2; ЧН=0,00");
	ОбластьДокумента.Параметры.Дни 	= Формат(ДокументСсылка.РасчетныеДанные.Итог("КорректировкаДни"),"ЧДЦ=2; ЧН=0");
	ОбластьДокумента.Параметры.Итог = Формат(ДокументСсылка.РасчетныеДанные.Итог("Итого"),"ЧДЦ=2; ЧН=0,00");
	
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);
	

	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ОтчетОтступ");
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	// Расшифровка:
	ОбластьДокумента = МакетВедомости.ПолучитьОбласть("ОтчетРасшифровка");
	ОбластьДокумента.Параметры.РасшифровкаРасчета = ДокументСсылка.ПояснениеРасчета;
	ОтпускТабличныйДокумент.Вывести(ОбластьДокумента);

	Возврат ОтпускТабличныйДокумент; 

КонецФункции

#КонецОбласти

#КонецЕсли

